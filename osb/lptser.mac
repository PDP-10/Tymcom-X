TITLE	LPTSER - LINE PRINTER SERVICE ROUTINE FOR MULTIPLE LINE PRINTERS - V1420
SUBTTL	T. N. MCMANUS /TNM/CHW/RCC   TS 7 JUL 71

	STOPCD(,ENTRY,LPTSER)

IFNCPU(<KI,KL>),<PRINTX ?This version of LPTSER works on KI and KL only
		END>

;THE FOLLOWING EXTERNAL SYMBOLS ARE DEFINED IN COMMON:

	EXTERNAL CPOPJ1,CPOPJ

;THE FOLLOWING EXTERNAL SYMBOLS ARE DEFINED IN CLOCK1:

	EXTERNAL SETIOD

;THE FOLLOWING EXTERNAL SYMBOLS ARE DEFINED IN ERRCON:

	EXTERNAL HNGSTP, ERROCI

;THE FOLLOWING EXTERNAL SYMBOLS ARE DEFINED IN UUOCON:

	EXTERNAL ADVBFE, IOSET, OUT, SETACT, STOIOS, DEVERR

;THE FOLLOWING SYMBOLS ARE REFERENCED OUTSIDE OF LPTSER:

	INTERN	LPTINT, LPTNXT, LPTECM, LPTDON, LPTDSP
;LINE PRINTER CONTROL REGISTER MNEMONIC DEFINITIONS

	LPTCLR==1B25			;CLEAR LINE PRINTER BUFFER
	LPTLOV==1B26			;LINE OVERFLOW FLAG (PDP-6 ONLY)
	LPTERR==1B27			;ERROR FLAG
	LPTBSY==1B28			;BUSY FLAG
	LPTDON==1B29			;DONE FLAG
	LPTECM==7B32			;ERROR CHANNEL MASK

	LPTBDM==LPTBSY+LPTDON		;BUSY/DONE FLAG MASK

;LINE PRINTER DEVICE DEPENDENT I/O STATUS MNEMONIC DEFINITIONS

	LPTEND==400000			;CLOSE UUO HAS BEEN DONE
	LPTSYN==200000			;CRFF AFTER CLOSE UUO HAS BEEN SENT
	LPTRBL==100000			;LPT TROUBLE DETECTED AT INTERRUPT LEVEL
	LPTPAR==40000			;PARTIAL BUFFER ALREADY OUTPUT

;BITS IN RH OF S

	LPTNFF==100			;SUPPRESS FREE FORM FEEDS

;LINE PRINTER DEVICE DATA BLOCK ADDRESSING MNEMONIC DEFINITIONS

	EXTERN	LPTCON,LPTPTR,LPTCH,LPTSVE,LPTEX1,LPTSV2,LPTECH
	EXTERN	LPTDNE,LPTCSO,LPTCSZ,LPTCNI,LPTCNO,LPTDTO,LPTBKO
	EXTERN	LPXSTS,SIZLPT
	EXTERN	WSYNC

;LINE PRINTER SERVICE DISPATCH TABLE

LPTDSP:	PHASE 0
DDXZ:	DSPSIZ
DDINT:	POPJ P,
DBYT:	4400,,4400
DVSIZ:	MOVEI T1,SIZLPT
DINI:	JRST LPTINI
DHNG:	JRST LPTHNG
DRL:	JRST LPTREL
DCL:	JRST LPTCLS
DOU:	JRST LPTOUT
DIN:	JRST ERROCI
DXFR:	JRST LPTXFR
	DSPSIZ==.-1
	DEPHASE
;LINE PRINTER INITIALIZATION, HUNG DEVICE AND RELEASE CODE

	;LPTINI IS CALLED AT SYSTEM INITIALIZATION TIME FROM
	;IOGO IN SYSINI WITH THE DDB ADDRESS IN F

	;LPTHNG IS CALLED FROM DEVCHK IN UUOCON (IOCSS) WITH
	;THE DDB ADDRESS IN F WHENEVER A LINE PRINTER
	;HUNG DEVICE COUNT IS DECREMENTED TO ZERO

	;LPTREL IS CALLED FROM RELEA1 IN UUOCON WITH THE DDB
	;ADDRESS IN F WHENEVER A RELEASE UUO IS EXECUTED
	;FOR A LINE PRINTER

	;EACH OF THE ABOVE ROUTINES MUST:
	;
	;	1.  CLEAR THE SPECIFIED LINE PRINTER
	;	2.  DEASSIGN BOTH THE ERROR AND DONE INTERRUPT
	;	     CHANNELS FOR THAT LINE PRINTER
	;	3.  CLEAR THE SKIP CHAIN INTERRUPT MASK FLAGS
	;	     FOR THAT LINE PRINTER

	;NOTE:  THE LPTINI CODE FORCES IOGO IN SYSINI TO INVOKE
	;       LPTINI FOR EACH LINE PRINTER ON THE SYSTEM RATHER
	;       THAN FOR THE NORMAL CASE WHERE IT INVOKES THE
	;       INITIALIZATION CODE ONCE FOR EACH DISPATCH TABLE.
	;
	;       THEREFORE, THE CORRECT OPERATION OF THE LPTINI CODE
	;       IS DEPENDANT UPON THE IOGO CODE WHICH SHOULD BE:
	;
	;		PUSHJ P,DINI(P3)
	;		HRRZM P3,SAVITM

LPTHNG:
LPTREL:
	SOS	(P)		;COUNTERACT SKIP RETURN
LPTINI:	MOVEI	U,LPTCLR	;CLEAR THE LINE PRINTER
	XCT	LPTCNO(F)
	XCT LPXSTS(F)	;CONI INTO DEVSTS FOR 96 CHR CHECK
	HLLZS	LPTCON(F)	;CLEAR SKIP CHAIN MASK FLAGS
	TLZ	S,LPTRBL!LPTPAR	;CLEAR TROUBLE FLAGS
	PUSHJ	P,STOIOS
	JRST	CPOPJ1		;SKIP RETURN IF ENTERED AT LPTINI
					; TO FORCE CALL FOR EACH LPT

;LINE PRINTER CLOSE UUO ROUTINE

LPTCLS:	TLO	S,LPTEND	;TURN ON THE END FLAG
	TLZ	S,LPTSYN+LPTRBL+LPTPAR ;TURN OFF SYNCHRONIZATION FLAG
	MOVEM	S,DEVIOS(F)	;SAVE S IN THE DDB
;LINE PRINTER OUTPUT UUO ROUTINE

LPTOUT: TLZN	S,LPTRBL	;TROUBLE DETECTED?
	JRST	LPTOU2		;NO
	MOVEM	S,DEVIOS(F)	;YES, SAVE NEW STATUS
LPTSTP:	PUSHJ	P,HNGSTP	;HALT JOB UNTIL I/O IS OK
LPTOU2:	MOVEI	U,0	;CLEAR LP FLAGS
	TLO	S,IO		;INDICATE OUTPUT
	XCT	LPTCNO(F)	;CLEAR ALL FLAGS
	MOVEI	U,LPTERR	;MASK FOR ERROR FLAG
	XCT	LPTCSZ(F)	;ERROR?
	JRST	LPTSTP	;YES.
	MOVE	U,LPTCH(F)	;SET UP CHANNEL ASSIGNMENT
	TLNN	S,IOBEG	;FIRST OUTPUT?
	JRST	LPTGO	;NO
	HRRZ	T1,DEVCLS(F)	;YES,SET UP INITIAL BLKO POINTER
	SUBI	T1,1		;TO BUFFER ADDRESS - 1
	MOVEM	T1,LPTPTR(F)	; AND STORE IT
	TRC	U,LPTDON!LPTCLR	;CLEAR LPTDON AND SET LPTCLR
LPTGO:	PUSHJ	P,SETACT	;SET I/O ACTIVE, ETC.
	HRLI	U,LPTLOV+LPTERR+LPTDON
	WRPI	LI.PIF	;TURN OFF ALL INTERRUPTS
	XCT	LPTCNO(F)	;DO IT TO IT
	HLRM	U,LPTCON(F)	;SET CHAIN MASK BITS
	WRPI	LI.PIN	;TURN ON INTERRUPTS AGAIN
	TLNN	S,LPTEND	;CLOSE?
	POPJ	P,		;NO. RETURN
	PUSHJ	P,WSYNC		;YES. GO TO I/O WAIT FOR BUFFER TO EMPTY
	TLZN	S,LPTRBL	;ANY ERRORS?
	POPJ	P,		;NO
	JRST	LPTSTP		;YES. TELL OPERATOR AND HANG
;
; ADVANCE BUFFER POINTER.
;   ON ENTRY (T1)= LAST BLKO POINTER
;   SKIP RETURN INDICATES BUFFER NOT EMPTY
;   NO-SKIP RETURN INDICATES BUFFER IS EMPTY
;
ABOP:	HRRZS	T1		;CLEAR COUNT
	AOJ	T1,		;INCREMENT ADDRESS
	HRRZ	T2,DEVCLS(F)	;BUFFER POINTER
	CAML	T1,-1(T2)	;OUT .GE. LAST ?
	MOVE	T1,T2		;YES. OUT = FIRST
	HRRM	T1,-2(T2)	;STORE NEW OUT POINTER
	MOVS	T1,-2(T2)	;SWAP IN AND OUT
	CAME	T1,-2(T2)	;IN .EQ. OUT?
	AOS	(P)		;NO. BUFFER CONTAINS DATA
	POPJ	P,		;RETURN
;

;
;  SET UP NEW BLKO POINTER FOR NEXT BLOCK
;  IN MONITOR BUFFER.  ASSUMES BUFFER IS
;  NOT EMPTY.
;    ON EXIT (T1)= NEW POINTER
;
BPTSET:	HRRZ	T3,DEVCLS(F)	;GET BUFFER POINTER
	HLRZ	T1,-2(T3)	;LOAD IN POINTER
	HRRZ	T2,-2(T3)	;LOAD OUT POINTER
	SUB	T1,T2		;COMPUTE LENGTH
	JUMPGE	T1,BPTSE1	;JUMP IF IN .GE. OUT
	HRRZ	T1,-1(T3)	;GET LAST POINTER
	SUB	T1,T2		;COMPUTE LENGTH TO END OF BUFFER
BPTSE1:	MOVN	T1,T1		;NEGATE COUNT
	SOS	T2		;COMPUTE ADDRESS - 1
	HRL	T1,T2		;COMBINE ADDRESS AND LENGTH
	MOVSS	T1		; IN PROPER ORDER
	POPJ	P,		;RETURN
;LINE PRINTER INTERRUPT SERVICE ROUTINE

LPTINT:	XCT	LPXSTS(F)	;STORE CONI STATUS IN DDB
	XCT	LPTECH(F)	;SKIP IF NO ERROR CHANNEL ASSIGNED
	XCT	LPTDNE(F)	;SKIP IF DONE FLAG IS UP
	JRST	LPTER1		; BRANCH TO ERROR SERVICE ROUTINE
	SKIPL	LPTPTR(F)	;BLKO COUNT TO 0 ON PREV. INTERRUPT?
	JRST	LPTSVE(F)	; YES, GO SAVE ORIGINAL AC'S
	AOSLE	DEVWUC(F)	;ADVANCE EARLY STARTUP COUNT
	JRST	LPTSVE(F)	;COUNT EXHAUSTED SO START UP USER
	XCT	LPTBKO(F)	; NO, SEND NEXT WORD FOR PRINTING
	JRST	LPTEX1(F)	;LAST WORD SENT BUT INTERRUPT PENDING
	JRST	LPTEX1(F)	;GO RESTORE F AND RETURN

LPTNXT:					;ENTER HERE AFTER DDB ROUTINE SAVES
					; ORIGINAL AC'S IN PROPER CHANNEL
					; SAVE AREA AND SETS UP F
					; TO POINT TO DDB AGAIN

	PUSHJ	P,IOSET		;SET AC R:= JOB AREA ADDRESS AND
					; AC S:= DEVIOS IN THE DDB
	TLNE	S,LPTRBL	;TROUBLE DETECTED ON INTERRUPT
	JRST	LPTERG		;YES, FIX IT
	TLZE	S,IOBEG		;NO, FIRST BUFFER SINCE INIT?
	JRST	LPTBG1		; YES, GO OUTPUT A CRFF
	MOVE	T1,LPTPTR(F)	;GET OLD BLKO POINTER
	PUSHJ	P,ABOP	; NO, ADVANCE TO NEXT BLOCK
	JRST	LPTOFF		;CANNOT ADVANCE, BUFFER EMPTY
	PUSHJ	P,BPTSET	;SET UP NEW BLKO POINTER
	MOVEM	T1,LPTPTR(F)	;STORE NEW POINTER
	HLREM	T1,DEVWUC(F)	;SET WAKE UP COUNT TO SAME AS BLOCKO COUNT
LPTWCK:	TLZE	S,IOW		;IS JOB WAITING FOR I/O COMPLETION?
	PUSHJ	P,SETIOD	; YES, ARRANGE FOR JOB TO RUN AGAIN
	JRST	STOIOS		;SAVE S, RESET HUNG DEVICE COUNT
					; AND DISMISS INTERRUPT

LPTOFF:	TLNN	S,LPTEND	;SKIP IF CLOSE HAS BEEN DONE
	JRST	LPTOF1		; GO TURN PRINTER OFF UNTIL NEXT OUTPUT
	TLON	S,LPTSYN	;HAS FINAL CRFF BEEN OUTPUT?
	JRST	LPTBG2		; NO, SO GO DO IT
	TLZ	S,LPTEND	; YES, SO CLEAR END FLAG
LPTOF1:	TRZ	S,IOACT		;CLEAR I/O ACTIVE BIT
	MOVEI	U,0		;CLEAR U AND
	XCT	LPTCNO(F)	; TURN LPT OFF
	HLLZS	LPTCON(F)	;CLEAR SKIP CHAIN MASK FLAGS
	JRST	LPTWCK		; AND BRANCH

LPTBG1:	MOVE	T1,LPTPTR(F)	;OLD BLKO POINTER
	PUSHJ	P,BPTSET	;SET UP INITIAL BLKO POINTER *******
	MOVEM	T1,LPTPTR(F)	;STORE NEW POINTER
LPTBG2:	MOVEI	U,[EXP 15B6+14B13]	;SEND OUT A CRFF
	TRNN	S,LPTNFF	;SKIP IF SUPPRESSING FF (100 MODE)
	XCT	LPTDTO(F)
	JRST	STOIOS		;GO DISMISS ITERRUPT
;LINE PRINTER ERROR HANDLING ROUTINE

LPTER1:	MOVEM	U,LPTSV2(F)	;SAVE U IN DDB
	MOVEI	U,LPTLOV	;GET LINE OVERFLOW ERROR MASK
	XCT	LPTCSO(F)	;SKIP IF LINE OVERFLOW FLAG IS ON
	JRST	LPTER2		; GO CHECK IF PREVIOUS ERROR OCCURRED
	MOVN	U,[EXP 1000001]	;DECREMENT BLK0 POINTER
	ADDM	U,LPTPTR(F)
	MOVEI	U,[EXP 15B6+12B13]	;PRINT CRLF
	XCT	LPTDTO(F)
	JRST	LPTER4

LPTER2:	XCT	LPTECH(F)	;SKIP IF ERROR INTERRUPT NOT ASSIGNED
	JRST	LPTER5		; ERROR CONDITION DETECTED
	XCT	LPTCNI(F)	;GET HARDWARE STATUS
	ANDI	U,LPTBDM	;EXTRACT BUSY AND DONE FLAGS
	XOR	U,LPTCH(F)	;PUT CHANNEL ASSIGNMENTS BACK IN
	XORI	U,LPTDON	;RECOMPLEMENT LPTDON TO PREVIOUS STATE
	XCT	LPTCNO(F)	;SEND IT OUT TO THE LPT
	MOVEI	U,LPTLOV!LPTERR!LPTDON	;ENABLE FOR ALL INTERRUPTS
	HRRM	U,LPTCON(F)	;SAVE SKIP CHAIN MASK FLAGS
LPTER4:	MOVE	U,LPTSV2(F)	;RESTORE SAVED ACCUMULATOR
	JRST	LPTEX1(F)	; AND GO DISMISS INTERRUPT

LPTER5:	MOVSI	U,LPTRBL!LPTPAR	;INDICATE TROUBLE NOTICED ON
	ORM	U,DEVIOS(F)	; INTERRUPT LEVEL
	MOVE	U,LPTSV2(F)	;RESTORE U
	JRST	LPTSVE(F)	;GO SAVE ALL AC'S

;RETURN HERE WITH ACS SAVED
LPTERG:	PUSHJ	P,LPTOF1	;TURN OFF LPT, CLEAR IOACT
	JRST	DEVERR		;CAUSE UUOCON TO RETRY ON UUO LEVEL
;
;	DEVICE DEPENDENT TRANSFER FROM USER BUFFER
;	ON ENTRY: LH(T2)=DATA AREA IN USER BUFFER
;	          RH(T2)=ADDRESS OF NEXT BLOCK IN MONITOR BUFFER
;		  (T3)  =LAST ADDRESS + 1 IN MONITOR BUFFER OF DATA TO BE TRANSFERRED
;	          (T1)  =NO. OF WORDS IN BLOCK TO BE XFERRED
;	ON EXIT: (T1)  RETURNS NO. OF WORDS ACTUALLY XFERRED
;
LPTXFR:	XCTFU <BLT T2,-1(T3)>	;ALL SET UP FOR BLT
	AOS	(P)		;SKIP  RETURN
	POPJ	P,

	$END	(LP1)		;End of LPTSER (LP1LIT: LP1END:)
    