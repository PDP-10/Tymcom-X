TITLE	PTPSER - PAPER TAPE PUNCH SERVICE ROUTINE - V405
SUBTTL	/RCC/GR TS 18 JUL 70

	STOPCD(,ENTRY,PTPSER)

IFNCPU(<KI,KL>),<PRINTX PTPSER works only on the KI and KL>

EXTERNAL PTPCHN, PTPSAV, ADVBFE, ITMCT1, SETIOD, SETBYT, IOSET, CKS12
EXTERNAL ERROCI,  OUT, WAIT1,PTPCHL,PIOMOD,CPOPJ
EXTERNAL SETACT,STOIOS,CBLAMB,CBLAUB,WSYNC
INTERNAL PTPINT

;PTP DEVICE DATA BLOCK LINKAGE

; PARAMETER ASSIGNMENTS

;   PTP CONTROL REGISTER
	PTPDON==10

;   FORMAT CONTROL
	PTPFDN==200
;OUT OF TAPE FLAG
	PTPNTP==100

;   SPECIAL IO STATUS WORD ASSIGNMENTS
	IODISC==400000
	PTPFED==200000
	PTPBIN==100000	;CHECKSUM BINARY
	PTPIB==40000	;IMAGE BINARY
	PTPEND==20000	;CLOSE FLAG
	PTPEOT==10000	;END OF TAPE INDICATOR

;   SPECIAL ASCII CHARACTERS
;   DEFINED WITH THE CORRECT PARITY
	RUBOUT==377
	HORTAB==11
	VERTAB==213
	FORMFD==14

;PTP DEVICE DATA BLOCK

	INTERN	PTPDDB
PTPDDB:	PHASE	0		;TO DEFINE DDB
DEVNAM:	SIXBIT	/PTP/		;NAME
DEVCHR:	XWD	4*HUNGST,41	;CHARACERISTICS
DEVIOS:	0			;I/O STATUS
DEVSER:	EXP	PTPDSP		;SERVICE ROUTINE DISPATCH TABLE
DEVMOD:	XWD	DVPTP+DVOUT,14403;
DEVLOG:	0
DEVBUF:	0
DEVIAD:	0
DEVPTR:	0
DEVCLS:	XWD	00,PTPBUF	;CLASS AND MONITOR BUFFER LOCATION
DEVSTS:	0
DEVPWC:	XWD	PTPBL/2,0	;WAKE UP BUFFER COUNT AND PARTIAL WORD COUNT
DEVWUC:	0			;ACTIVE WAKE UP COUNT
DEVCTR:	0
DEVINT:	0
	DEPHASE
  PTPIOS=PTPDDB+DEVIOS
  PTPPTR=PTPDDB+DEVIAD
  PTPADR=PTPDDB+DEVPTR
  PTPSTS=PTPDDB+DEVSTS
  PTPCTR=PTPDDB+DEVCTR
PTPCNT:	0
PTPCHA:	0
PTPSIO:	0
PTPCON:	0
SIZPTP==.-PTPDDB
;
;  MONITOR BUFFER DEFINITION
;
  PTPBL=144			;LENGTH
	XWD	PTPBUF,PTPBUF	;IN AND OUT
	EXP	PTPBUF+PTPBL	;LAST
PTPBUF:	BLOCK	PTPBL
;


;PTP SERVICE DISPATCH TABLE

PTPDSP:	PHASE 0
DDXZ:	DSPSIZ
DDINT:	POPJ P,
DBYT:	4400,,4400
DVSIZ:	MOVEI T1,SIZPTP
DINI:	JRST PTPINI
DHNG:	JRST PTPREL
DRL:	JRST PTPREL
DCL:	JRST PTPCLS
DOU:	JRST PTPOUT
	DIN:	JRST ERROCI
DXFR:	JRST PTPXFR
	DSPSIZ==.-1
	DEPHASE
PTPINI:
PTPREL:	CONO PTP,0
	HLLZS PTPCON		;CLEAR CONSO FLAG BITS
	POPJ P,		;RETURN

PTPCLS:
	PUSHJ	P,WAIT1		;WAIT UNTIL MB IS EMTY
	MOVSI	S,PTPEND+IODISC		;SET CLOSE DONE FLAG
	IORB	S,DEVIOS(F)	;STORE STATUS
	JRST	PTPOUT		;GO OUTPUT TRAILER

	EXTERN HNGSTP

PTPOUT:	CONSO PTP,PTPNTP	;OUT OF TAPE?
	JRST PTPIN0		;NO -ENTER SERVICE
	PUSHJ P,HNGSTP	;ISSUE "DEVICE PTP OK?"
	JRST PTPOUT		;TRY AGAIN WHEN "CONT" TYPED
PTPIN0:	TLZE S,IOBEG		;VIRGIN DEVICE? (IOBEG:=0)
	JRST PTPIN2		;YES

PTPIN1:
	PUSHJ P,SETACT	;SET ACTIVE FLAG,STORE S, AND
				;RESET HUNG DEVICE TIMEOUT COUNT
	HRLI T1,PTPDON		;CONSO FLAG
	HRRI T1,PTPCHN		;CONO FLAGS
	TRO T1,PTPDON
	WRPI	LI.PIF
	CONO	PTP,(T1)
	HLRM	T1,PTPCON
	WRPI	LI.PIN
	POPJ P,		;RETURN

PTPIN2:	LDB T1,PIOMOD
	TLO S, IOFST+PTPFED+IO	;IOFST:=PTPFED:=IO:=1
	TLZ S,PTPBIN+PTPIB+IODISC	;CLEAR BINARY AND IMAGE BINARY BITS
	CAIN T1,IB		;IMAGE BINARY?
	TLO S,PTPIB		;YES
	CAIN T1,B		;MODE=BINARY?
	TLO S,PTPBIN		;YES, PTPBIN:=1
	MOVEI T1,PTPFDN	;PTPCNT:=PTPFDN
	MOVEM T1,PTPCNT
	PUSHJ P,SETBYT	;SETBYT
	HRR	T1,DEVCLS(F)	;BUFFER POINTER
	HRR	T1,-2(T1)	;NEXT WORD TO TAKE FROM BUFFER
	SUBI	T1,1		;ADJUST FOR POINTER
	MOVEM	T1,PTPPTR	;SET UP BYTE POINTER
	IBP	PTPPTR		;TO ACTUAL FIRST BYTE
	JRST PTPIN1

;PUNCH INTERRUPT SERVICE

PTPINT:	CONSO PTP,@PTPCON
	JRST PTPINT
	CONI	PTP,PTPSTS	;STORE CONI STATUS
	CONSZ	PTP,PTPNTP	;ARE WE OUT OF TAPE?
	JRST	NOTAPE		;YES - GO HANDLE IT
	MOVEM S,PTPSIO	;PTPSIO:=C(S)  SAVE S
	MOVE S,PTPIOS		;S:=C(PTPIOS)
	TLNE S,PTPFED		;FEED REQUEST? (PTPFED=1?)
	JRST PTPS2		;YES
	MOVE S,PTPSIO		;RESTORE S
	JSR PTPSAV		;SAVE ACS AND ESTABLISH P
	MOVEI F,PTPDDB
	PUSHJ P,IOSET		;R:=(JBTADR 18-35),J:=C(DEVCTR)
				;S:=C(PTRIOS)
	TLZE S,IODISC		;DISCONNECT?  (IODISC=1?)
	JRST PTPADV		;YES
	TLZE	S,IOFST		;START OF NEW MB?
	JRST	PTPB1		;YES.  SET UP FIRST CHARACTER
	TLNN S,PTPBIN+PTPIB	;BINARY PUNCH MODE?
	JRST PTP3		;NO

	MOVE T1,PTPCHA		;BINARY OUTPUT BYTE SIZE=36
	ROT T1,6
	MOVEM T1,PTPCHA
	ANDI T1,77		;T1:=XX, SIXBIT SUB-BYTE
	ADDI T1,200
	DATAO PTP,T1		;PUNCH 2XX
	SOSL PTPCNT		;C(PTPCNT):=C(PTPCNT)-1.  WORD DONE?
	JRST STOIOS		;NO

;FOR BINARY AND IMAGE BINARY
PTPB1:	MOVEI T1,5		;PTPCNT:=5
	MOVEM T1,PTPCNT
	JRST PTP1
;FOR ASCII AND IMAGE MODE
PTP3:	DATAO PTP,PTPCHA	;PUNCH CHARACTER
	TRNE S,14		;IS MODE AN ALPHA MODE
	JRST PTP1		;NO
	MOVE U,PTPCHA
	CAIE U,HORTAB		;HORIZONTAL OR VERTICAL TAB?
	CAIN U,VERTAB
	JRST PTPP1		;YES
	CAIE U,FORMFD		;FORM FEED?
	JRST PTP1		;NO
	TLO S,PTPFED	;PTPFED:=PTPEOL:=1
	MOVEI U,20
	MOVEM U,PTPCNT	;PTPCNT:=20
	JRST STOIOS
	EXTERNAL PEVEN8		;IN IOCSS, FOR PARITY IN ASCII

; GET NEXT ITEM FROM BUFFER
;
PTP1:	HRRZ	T1,DEVCLS(F)	;BUFFER POINTER
PTP4:	HRRZ	T2,-2(T1)	;OUT
	HLRZ	T3,-2(T1)	;IN
	CAMN	T2,T3		;BUFFER EMPTY?
	JRST	PTP2		;YES
PTP6:	LDB	U,PTPPTR	;NO. GET CHARACTER
	IBP	PTPPTR		;ADVANCE POINTER MANUALLY
	HRRZ	T3,PTPPTR	;NEW OUT
	CAMN	T3,-1(T1)	;OUT = LAST?
	MOVE	T3,T1		;YES. OUT:=FIRST
	HRRM	T3,-2(T1)	;STORE IT
	HRRM	T3,PTPPTR	;ALSO IN POINTER
	LDB	T3,PIOMOD	;GET MODE
	CAIE	T3,A		;ASCII?
	JRST	PTP5		;NO.
	JUMPE	U,PTP4		;YES. JUMP ON ASCII NULL
	MOVE	T3,U		;FOR CALL PEVEN8
	PUSHJ	P,PEVEN8	
	MOVE	U,T3
PTP5:	MOVEM	U,PTPCHA	;NEXT ITEM TO PUNCH
	JRST	STOIOS		;DISMIS INTERRUPT
PTP2:	TLO	S,IODISC	
	JRST 	STOIOS
;PUNCH FEEDER
PTPS2:	SETZM	PTPCHA		;PUNCH FEED
	DATAO PTP,PTPCHA
	SOSG PTPCNT		;COUNT FEED LINES
	TLZ S,PTPFED		;PTPFED:=0
	MOVEM S,PTPIOS	;PTPIOS:=C(S)
	MOVE S,PTPSIO		;RESTORE S
	JEN @PTPCHL		;DISMISS

;
;  DEVICE DEPENDENT TRANSFER ROUTINE
;  ON ENTRY LH(T2)=FROM ADDRESS
;           RH(T2)=TO ADDRESS
;           (T3)  =END ADDRESS +1
;            (T1) = NO. OF WORDS TO TRANSFER
;  ON EXIT (T1)RETURNS THE NO. OF WORDS TRANSFERRED
;
;      NO-SKIP RETURN INDICATES BUFFER COMPLETED
;
PTPXFR:	LDB	P1,PIOMOD	;GET MODE
	CAIN	P1,B		;BINARY?
	JRST	PTPXF1		;YES. SPECIALHANDLING
	XCTFU <BLT T2,-1(T3)> ;DO TRANSFER
	AOS	(P)		;SKIP RETURN
PTPXF2:	POPJ	P,

PTPXF1:	HRRZ	P1,T2		;SAVE MB ADDRESS
	HRRZ	T2,DEVOAD(F)	;BUFFER ADDRESS
	PUSHJ	P,CKS12		;COMPUTE CHECKSUM
	ADD	T2,DEVOAD(F)	;COMPUTE ADDRESSOF THIRD WORD
	XCTBU <HRR T1,(T2)>	;FORM CKSUM AND COUNT WORD
	MOVEM	T1,(P1)		;STORE IN MB
	ADDI	P1,1
PTPXF4:	HRRZ	T1,DEVCLS(F)	;MB ADDRESS
	CAMN	P1,-1(T1)	;NEW IN = LAST?
	HRRZ	P1,T1		;IN:=FIRST
	HRLM	P1,-2(T1)	;STORE NEW POINTER
PTPXF5:	PUSHJ	P,CBLAUB	;GET UB DATA INFO
	JUMPE	T4,PTPXF2	;JUMP IF UB IS EMPTY
	PUSHJ	P,CBLAMB	;COMPUTE SPACE IN MB
	JUMPE	T1,PTPXF3	;MB FULL
	CAMLE	T1,T4		;MORE  SPACE THAN NEEDED
	HRRZ	T1,T4		;COUNT FULL BUFFER
	HRL	T2,T3		;SET UP FROM
	HRRZ	T3,T2	
	ADD	T3,T1		;COMPUTE END+1
	XCTFU <BLT T2,-1(T3)>	;DO TRANSFER
	ADDM	T1,DEVPWC(F)	;ADVANCE PARTIAL WORD COUNT
	HRRZ	P1,DEVCLS(F)	;GET BUFFER POINTER
	HLRZ	P1,-2(P1)	;OLD IN
	ADD	P1,T1		;COMPUTE NEW IN
	JRST	PTPXF4
PTPXF3:	HLRZ	T1,DEVPWC(F)	;GET WAKEUP COUNT
	MOVNM	T1,DEVWUC(F)	;SET IT
	PUSHJ	P,WSYNC		;WAIT FOR ROOM IN MB
	TRNE	S,IOACT
	JRST	PTPXF5
	PUSHJ	P,CBLAMB	;CHECK MB
	JUMPE	T1,PTPXF5	;IF EMPTY GO FILL IT
	HRRZ	T4,DEVSER(F)	;NOT EMPTY SO STARTDEVICE
	PUSHJ	P,DOU(T4)
	JRST	PTPXF5

;  BUFFER EMPTY AFTER LAST INTERRUPT
;
PTPADV:	HRRZ	T1,DEVCLS(F)	;BUFFER POINTER
	HRRZ	T2,-2(T1)	;OUT
	HLRZ	T3,-2(T1)	;IN
	CAME	T2,T3		;STILL EMPTY?
	JRST	PTP6		;NO. SETUP ITEM AND WAIT FOR ANOTHR INTERRUPT
	TLNN	S,PTPEND	;YES.  CLOSE DONE?
	JRST	PTP9		;NO. TURN OFF DEVICE
	TLON	S,PTPEOT	;YES. EOT FEED DONE?
	JRST	PTP10		;NO. GO DO IT
	TLZ	S,PTPEND+PTPEOT	;YES. ALL DONE
PTP9:	TRZ	S,IOACT		;CLEAR IO ACTIVE
	CONO	PTP,0		;STOP DEVICE
	HLLZS,PTPCON		;CLEAR INTERRUPT
	TLZE	S,IOW		;JOB WAITING?
	PUSHJ	P,SETIOD	;YES. WAKE UP
	TLO	S,IODISC	;SET SO NEW DATA WILL GET SET UP
	JRST	STOIOS		;AND DISMISS INTERRUPT
PTP10:	MOVEI	T1,200
	MOVEM	T1,PTPCNT	;SET FEED COUNT
	TLO	S,IODISC+PTPFED; FEED REQUEST
	JRST	STOIOS		;START FEED

PTPP1:	MOVEI U,RUBOUT	;PTPCHA:=RUBOUT
	MOVEM U,PTPCHA
	JRST STOIOS

PTPDSC:	PUSHJ P,PTPREL	;CLEAR PTP CONTROL REG AND CONSO BITS
	TRZ S,IOACT		;IOACT:=0
	TLO S,IOFST		;IOFST:=1
	TLZE S,IOW		;IN A WAIT?  IOW:=0
	PUSHJ P,SETIOD	;YES.  IOWS:=1

		;WHEN END OF TAPE ENCOUNTERED, COME HERE TO FORCE A HUNG DEVICE
NOTAPE:	CONO	PTP,0		;TURN OFF PTP-RESTART NOT ALLOWED
	JEN	@PTPCHL		;DISMISS INTERRUPT-BUT NO MORE
				;TO BE RECEIVED SINCE PTP NOW OFF

PTPLIT:	LIT
PT.END:	END
   