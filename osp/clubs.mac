TITLE	CLUBS	UUOS FOR CLUBS

;KAREN KOLLING		FEBRUARY 1978
;MURRAY BOWLES		OCTOBER 1978 (MULTIPLE CLUBS PER JOB)

ENTRY CLUBS
CLUBS:

;EXTERNALS

EXTERN JBTCLB
EXTERN %UPLMA,LMMEXS,LMPMXW
EXTERN FCREQ,GETWDS,FCWAIT,GIVWDS
EXTERN JBTSTS,PJBSTS,SETRUN,WSCHED,ISQ,JBTQ,ILWQ
EXTERN HNGMON,JBYWAK,TAKTRP,JBTAWQ,ABTUUO
EXTERN PUUOAC,CPOPJ,CPOPJ1,TPOPJ,RBREAL,GETDPA,JOB,RBMASK

SUBTTL CLUB DATA BASE

;CLUB MEMBERSHIP CARD (CMC) -- EACH JOB BELONGING TO A CLUB HAS
;  A MEMBERSHIP CARD FOR THAT CLUB. EACH MEMBERSHIP CARD HAS TWO
;  DOUBLY-LINKED LISTS RUNNING THROUGH IT: A LIST OF ALL THE
;  MEMBERSHIP CARDS HELD BY THE JOB HOLDING THE CARD, AND A
;  LIST OF ALL THE MEMBERSHIP CARDS FOR THE CLUB.

PHASE 0

CMCCLK::!	0	;PREV,,NEXT CMC FOR THIS CLUB (0 FOR END OF LIST)

CMCJLK::!	0	;PREV,,NEXT CMC FOR THIS JOB (0 FOR END OF LIST)

CMCSTS::!		;IN LH
 CM.FRC==(1B0)	;1ST-CLASS CLUB MEMBER
 CM.UNU==(177B7)	;UNUSED BITS OF STATUS FIELD
CMCJOB::!		;ALSO IN LH
 CM.JMK==(1777B17)	;MASK FOR JOB
CMCCDB::! 0	;RH=CDB ADDR

CMCVPG::!
 CMNVPG==^D8
 CMSVPG==^D9
CMCCID::! 0
 CMNCID==^D35
 CMSCID==^D27

LENCMC==.-CMCCLK
DEPHASE

CMYVPG:	POINT CMSVPG,CMCVPG(T1),CMNVPG
CMYCID:	POINT CMSCID,CMCCID(T1),CMNCID

;CLUB DATA BLOCK (CDB) -- EACH CLUB HAS A DATA BLOCK, WHICH DESCRIBES
;  THE STATUS OF THE CLUB

PHASE 0

CDBRET::!	0	;RETRIEVAL POINTER FOR CLUB PAGE
CDBLOK::!		;LH = CMC ADDR OF INTERLOCKER, OR 0 IF NOT INTERLOCKED
CDBCMC::!	0	;RH = ADDR OF FIRST CMC FOR THIS CLUB
CDBSTS::!		;LH = STATUS BITS
	CD.NML==200000	;1 IF LAST INTERLOCK RELEASE WAS NORMAL
			;(INITIALLY 0 FOR A NEW CLUB)
	CD.EVI==100000	;1 IF THIS CLUB HAS EVER BEEN INTERLOCKED
CDBHLK::!	0	;RH = HASH LINK (0 OR ADDR OF NEXT CDB IN BUCKET)
CDBCID::!	0	;LAST JOB-WITHIN-CLUB ID GIVEN OUT

LENCDB==.-CDBRET
DEPHASE

;LOCATION WHICH COUNTS NUMBER OF TIMES SOMEONE LEFT CLUB.
; USED FOR MAKING SURE THAT NO ONE HAS LEFT CLUB WHILE IN
; PROCESS OF SCANNING CMCS TO RETURN CLUB IDS IN CLBMEM
; UUO. IF COUNTER CHANGES WHILE IN CLBMEM SCANNING ROUTINE,
; CMC CHAIN HAS BEEN CHANGED AND MUST START THE SCAN OVER.

LVCCNT:	0

;CLBTBL IS A HSHCLB-LONG HASH TABLE MAPPING A DISK ADDRESS ONTO
;  THE ADDRESS OF THE FIRST CDB FOR THAT HASH CODE (OR 0 IF THERE
;  IS NONE)

HSHCLB==^D11
CLBTBL:	BLOCK HSHCLB

;JBTCLB IS A JOB TABLE MAPPING A JOB NUMBER ONTO THE ADDRESS OF
;  THE FIRST CMC FOR THAT JOB (OR 0 IF THERE IS NONE)

SUBTTL CLUB UUO ERROR CODES

ERRVPR==:1	;VP NUMBER OUT OF LEGAL RANGE.

ERRNEX==:2	;VP HAS NO DISK PAGE MAPPED INTO IT (DOES NOT
		;EXIST.).

ERRINC==:3	;JOB IS ALREADY IN A CLUB FOR THIS VP

ERRIDO==:4	;THIS CLUB'S ID NUMBER WOULD OVERFLOW IF
		;ANOTHER JOB WERE ADDED TO IT.

ERRNIC==:5	;YOU MUST BE IN A CLUB TO EXECUTE THIS UUO,
		;AND YOU ARE NOT IN ANY CLUB.

ERRVPM==:6	;VP MISMATCH.  (THE VP SPECIFIED IS NOT THE VP
		;THROUGH WHICH YOU ENTERED ANY OF THE CLUBS YOU'RE
		;NOW IN)

ERRHIL==:7	;THIS UUO CANNOT BE EXECUTED BECAUSE YOU HAVE
		;THE CLUB INTERLOCK.

ERRAIL==:10	;VALID ATTEMPT TO GET INTERLOCK, BUT IT IS BUSY.

ERRNIL==:11	;ATTEMPT TO RELEASE INTERLOCK WHEN YOU DON'T
		;HAVE IT.

ERRCNT==:12	;REQUESTED COUNT OF CLUB MEMBERS IS <0.

ERRNFC==:13	;THIS UUO CAN ONLY BE EXECUTED BY FIRST CLASS
		;MEMBERS OF THE CLUB, AND YOU ARE ONLY A
		;SECOND CLASS MEMBER.

ERRJNI==:14	;ATTEMPT TO PERFORM AN OPERATION ON OR GET
		;INFORMATION ABOUT A JOB WHICH IS NOT IN YOUR
		;CLUB.

SUBTTL CLUB UUO COMMON DISPATCH CODE
COMMENT #
@@SUBROUTINE KCBKER
@@PURPOSE
KERNEL SUBR FOR THE VARIOUS CLUB UUOS.
@@ENTRY
EXPECTS P1/ ADDRESS OF THE MAIN ROUTINE FOR THE PARTICULAR UUO.
EXPECTS USER ARGS IN T1 AND T2.  T1 IS ALWAYS THE VP NUMBER.
THERE MAY OR MAY NOT BE A SECOND ARG, DEPENDING ON THE UUO.
ALSO EXPECTS M/ USER'S DECODED UUO, SO VALUES CAN BE RETURNED.
@@ACCUM
DESTROYS T1-T4, P1-P4, J, U, PG, AND W.
@@EXIT
SKIP RETURNS ON SUCCESS, WITH VALUE RETURNED, IF ANY, ALREADY
STORED.
NON-SKIP RETURNS ON FAILURE, WITH ERROR CODE STORED IN USER'S
AC.
@@ #

EXTERNAL %UPT,UPTLDC,REDLMA

    ;CHECK THAT THE VP NUMBER IS IN RANGE AND THE VP EXISTS.
    ;INDIVIDUAL ROUTINES WILL CHECK THE SECOND ARGUMENT IF ANY.
    ;SET UP P3 AND P4/ LMA INFORMATION, P2/ SECOND ARG,
    ;J/ JOB NUMBER, AND W/ VP NUMBER.
KCBKER::SKIPL	W,T1		;SET UP W/ VP FOR GETUPT.
	CAILE	W,^D511		;VP IN RANGE?
	JRST	[MOVEI T1,ERRVPR ;NO.
		JRST KCBERR]	;
	MOVE	P2,T2		;SAVE P2/ SECOND ARG, IF ANY.
	PUSHJ	P,REDLMA	;GET LMAP SLOT IN P3 AND P4
	TLNN	P3,LMMEXS	;MUST BE EXISTENT PG
	JRST	[MOVEI T1,ERRNEX
		JRST KCBERR]	;
	MOVE	J,JOB		;SET UP J/ OUR JOB NUMBER.
    ;DISPATCH TO APPROPRIATE ROUTINE.
	PUSHJ	P,(P1)		;PASS P3,P4 LMAP SLOT
	JRST	.+2		;TAKE FAIL RETURN.
	JRST	CPOPJ1		;SUCCESS RETURN.
KCBERR:	UMOVEM	T1,(M)		;STORE ERROR FLAG IN USER'S AC.
	POPJ	P,

SUBTTL JOIN AND LEAVE CLUBS
COMMENT #
@@SUBROUTINE KCBADD
@@PURPOSE
SUBR FOR THE CLBADD UUO.  ADDS A JOB TO A CLUB.  IF NECESSARY,
CREATES THE CLUB.
@@ENTRY
EXPECTS W/ VP NUMBER, J/ JOB NUMBER, AND P3 AND P4/ LMA
INFORMATION.  EXPECTS THE VP TO EXIST.
EXPECTS M/ USER'S DECODED UUO, SO ID NUMBER CAN BE RETURNED.
@@ACCUM
DESTROYS T1-T4, P1-P4, J, U, PG, AND W.
@@EXIT
SKIP RETURNS ON SUCCESS, WITH THE JOB'S ID NUMBER STORED IN THE
USER'S AC.
NON-SKIP RETURNS ON FAIL, WITH THE ERROR CODE IN THE RH(T1)
AS FOLLOWS: ERRINC, ERRIDO.
@@ #

KCBADD::PUSHJ	P,SRCCMC	;ARE WE ALREADY IN A CLUB FOR THIS VP?
	JRST	KCBAD2		;NO -- OK
	MOVEI	T1, ERRINC
	JRST	KCBERR		;YES -- ERROR-RETURN
KCBAD2:	PUSHJ	P,SRCBTB	;LOOK FOR OUR DISK PAGE'S CLUB.
	JRST	KCBAD3		;CLUB NOT FOUND
	JRST	KCBAD4		;FOUND

;HERE TO CREATE A NEW CLUB FOR THIS DISK PAGE

KCBAD3:	PUSH	P,T2		;SAVE RETRIEVAL POINTER
	PUSH	P,T3		;SAVE CLBTBL HASH INDEX

    ;ALLOCATE A NEW CLUB DATA BLOCK

KCBA3A:	MOVEI	T2,LENCDB	;T2/ CDB SIZE
	SKIPGE	FCREQ		;ARE OTHER JOBS WAITING FOR FREECORE?
	PUSHJ	P,GETWDS	;IS THERE ENOUGH FREECORE FOR US?
	JRST	[		;OTHERS WAITING OR NOT ENOUGH
		AOS	FCREQ
		PUSHJ	P, FCWAIT
		SOS	FCREQ
		JRST	KCBA3A	;WAIT, THEN TRY AGAIN
	]

    ;INITIALIZE THE NEW CLUB DATA BLOCK (T1/ CDB ADDR)

	MOVE	T4, T1		;T4/ NEW CDB
	POP	P, T3		;T3/ CLBTBL HASH INDEX
	HRRZ	T1, CLBTBL(T3)	;T1/ 0,,OLD 1ST CDB
	HRRZM	T4, CLBTBL(T3)	;WE'RE NOW 1ST CDB
	HRRZM	T1, CDBHLK(T4)	;STATUS BITS 0,,HASH LINK=OLD 1ST
	POP	P,CDBRET(T4)	;RETRIEVAL POINTER
	SETZM	CDBCID(T4)	;FIRST JOB-IN-CLUB ID WILL BE 1
	SETZM	CDBCMC(T4)	;NO INTERLOCKER,,NOBODY IN CLUB

;HERE TO ADD A JOB TO AN EXISTING CLUB (T4/ CDB ADDR)

KCBAD4:	AOS	T2, CDBCID(T4)	;T2/ NEW JOB-IN-CLUB ID
	CAML	T2,[1B<CMNCID>_<CMSCID>]	;OVERFLOW?
	JRST	[			;YES --
		SOS	CDBCID(T4)	;  RESTORE THE VALUE
		MOVEI	T1, ERRIDO
		POPJ	P,		;  AND ERROR-RETURN
	]
	PUSH	P, T2		;SAVE JOB-IN-CLUB ID
	PUSH	P, T4		;SAVE CDB ADDR
KCBA4A:	MOVEI	T2, LENCMC	;T2/ CLUB MEMBERSHIP CARD SIZE
	SKIPGE	FCREQ		;ARE OTHER JOBS WAITING FOR FREECORE?
	PUSHJ	P, GETWDS	;IS THERE ENOUGH FREECORE FOR US?
	JRST	[		;OTHERS WAITING OR NOT ENOUGH
		AOS	FCREQ
		PUSHJ	P, FCWAIT
		SOS	FCREQ
		JRST	KCBA4A	;WAIT, THEN TRY AGAIN
	]
	POP	P, T4		;T4/ CDB ADDR

    ;LINK THE CMC TO THE CDB (T1/ CMC ADDR)

	HRRZ	T2, CDBCMC(T4)	;T2/ 0 OR 1ST CMC FOR CLUB
	JUMPE	T2, .+2		;IF THERE IS A 1ST CMC,
	HRLM	T1, CMCCLK(T2)	;  POINT IT BACK TO US
	HRRZM	T2, CMCCLK(T1)	;BACK=0,,FORW=OLD 1ST OR 0
	HRRM	T1, CDBCMC(T4)	;WE BECOME NEW 1ST

    ;LINK THE CMC TO OTHERS FOR THE JOB

	HRRZ	T2, JBTCLB(J)	;T2/ 1ST CMC FOR JOB OR 0
	JUMPE	T2, .+2		;IF THERE IS A 1ST CMC,
	HRLM	T1, CMCJLK(T2)	;  POINT IT BACK TO US
	HRRZM	T2, CMCJLK(T1)	;BACK=0,,FORW=OLD 1ST OR 0
	HRRM	T1, JBTCLB(J)	;WE BECOME NEW 1ST

    ;INITIALIZE THE REST OF THE CMC

	HRL	T4, J		;T4/ JOB#,,CDB ADDR
	TLNE	P3, LMPMXW	;IF WE'RE FIRST-CLASS MEMBERS,
	TLO	T4, CM.FRC	;  SET THE FIRST-CLASS BIT
	MOVEM	T4,CMCJOB&CMCSTS&CMCCDB(T1) ;GET ALL REFS IN CREF
	DPB	W,CMYVPG	;VP#
	POP	P, T2		;T2/ JOB-IN-CLUB ID
	DPB	T2,CMYCID
	LDB	T1, PUUOAC
	UMOVEM	T2, (T1)	;AND GIVE IT TO THE CALLER
	JRST	CPOPJ1		;AND SUCCESS-RETURN

COMMENT #
@@SUBROUTINE KCBLEV
@@PURPOSE
SUBR FOR THE CLBLEV UUO.  REMOVES A JOB FROM A CLUB, AND IF THAT
WAS THE ONLY JOB IN THE CLUB, DELETES THE CLUB.
@@ENTRY
EXPECTS W/ VP NUMBER, J/ JOB NUMBER, AND P3 AND P4/ LMA
INFORMATION.  EXPECTS THE VP TO EXIST.
@@ACCUM
DESTROYS T1-T4, P1-P4, J, U, PG, AND W.
@@EXIT
SKIP RETURNS ON SUCCESS.
NON-SKIP RETURNS ON FAIL, WITH THE ERROR CODE IN THE RH(T1)
AS FOLLOWS: ERRNIC, ERRVPM, ERRHIL.
@@ #

KCBLEV::SKIPN	JBTCLB(J)	;ARE WE IN ANY CLUBS?
	JRST	[		;NO -- ERROR-RETURN
		MOVEI	T1, ERRNIC
		POPJ	P,
	]
	PUSHJ	P, SRCCMC	;ARE WE IN A CLUB WITH THIS VP?
	JRST	[		;NO -- ERROR-RETURN
		MOVEI	T1, ERRVPM
		POPJ	P,
	]

    ;HERE WITH T1/ ADDR OF CMC FOR CLUB WE'RE LEAVING

	HRRZ	T2, CMCCDB(T1)	;T2/ CDB ADDR
	HLRZ	T2, CDBLOK(T2)	;T2/ INTERLOCKER CMC
	CAMN	T1, T2		;IS IT US?
	JRST	[		;YES -- ERROR-RETURN
		MOVEI	T1, ERRHIL
		POPJ	P,
	]
	AOS	(P)		;PREPARE FOR SUCCESS-RETURN
;	PJRST	RELCLB		;LEAVE (AND MAYBE DELETE) THE CLUB

COMMENT #
@@SUBROUTINE RELCLB
@@PURPOSE
REMOVES A JOB FROM A CLUB, AND IF THAT WAS THE ONLY JOB IN THE
CLUB, DELETES LUB.
@@ENTRY
EXPECTS T1/ ADDR OF MEMBERSHIP CARD
EXPECTS THE JOB TO BE IN THE CLUB AND TO NOT HAVE THE CLUB'S
INTERLOCK.
@@ACCUM
DESTROYS T1-T4.
@@ #

RELCLB:	AOS	LVCCNT		;INCREMENT COUNTER SO ANYONE IN MIDDLE OF
				; SCANNING MEMBERS WILL START OVER
	HRRZ	T2, CMCCDB(T1)	;T2/ CDB ADDR FOR CLUB

    ;REMOVE US FROM THE JOB'S CMC CHAIN

	HRRZ	T3, CMCJLK(T1)	;T3/ NEXT CMC FOR JOB
	HLRZ	T4, CMCJLK(T1)	;T4/ PREV CMC FOR JOB
	SKIPE	T3		;IF WE HAD A SUCCESSOR,
	HRLM	T4, CMCJLK(T3)	;  POINT IT AT OUR PREDECESSOR
	JUMPE	T4, .+3		;IF WE HAD A PREDECESSOR,
	HRRM	T3, CMCJLK(T4)	;  POINT IT AT OUR SUCCESSOR
	JRST	.+2		;OTHERWISE,
	HRRM	T3, JBTCLB(J)	;  POINT JBTCLB AT OUR SUCCESSOR

    ;REMOVE US FROM THE CLUB'S CMC CHAIN

	HRRZ	T3, CMCCLK(T1)	;T3/ NEXT CMC FOR CLUB
	HLRZ	T4, CMCCLK(T1)	;T4/ PREV CMC FOR CLUB
	SKIPE	T3		;IF WE HAD A SUCCESSOR,
	HRLM	T4, CMCCLK(T3)	;  POINT IT AT OUR PREDECESSOR
	JUMPE	T4, .+3		;IF WE HAD A PREDECESSOR,
	HRRM	T3, CMCCLK(T4)	;  POINT IT AT OUR SUCCESSOR
	JRST	.+2		;OTHERWISE,
	HRRM	T3, CDBCMC(T2)	;  POINT THE CDB AT OUR SUCCESSOR

    ;GIVE BACK THE CMC FREECORE

	PUSH	P, T2		;SAVE CDB ADDR
	MOVE	T2, T1		;T2/ CMC ADDR
	MOVEI	T1, LENCMC	;T1/ CMC LENGTH
	PUSHJ	P, GIVWDS	;RETURN THE FREECORE

    ;DELETE THE CLUB IF WE WERE THE LAST MEMBER

	POP	P, T4		;T4/ CDB ADDR
	HRRZ	T1, CDBCMC(T4)	;T1/ 1ST CMC
	JUMPN	T1, CPOPJ	;IF THERE IS ONE, RETURN
	PUSHJ	P, ULKCDB	;OTHERWISE, UNLINK THE CDB,
	MOVE	T2, T4		;  T2/ CDB ADDR
	MOVEI	T1, LENCDB	;  T1/ CDB LENGTH
	PJRST	GIVWDS		;  AND GIVE BACK ITS FREECORE

SUBTTL GET AND RELEASE CLUB INTERLOCKS
COMMENT #
@@SUBROUTINE KCBINW/CLBINI
@@PURPOSE
SUBR FOR THE CLBINW AND CLBINI UUOS.  GETS THE CLUB INTERLOCK.
IF THE INTERLOCK IS BUSY, KCBINI ERROR RETURNS BUT KCBINW
WAITS FOR THE INTERLOCK.
THIS UUO HAS TO BE REINITIALIZABLE SINCE SOFTWARE INTERRUPTS
CAN HAPPEN DURING ILW.
@@ENTRY
EXPECTS W/ VP NUMBER, J/ JOB NUMBER, AND P3 AND P4/ LMA
INFORMATION.  EXPECTS THE VP TO EXIST.
EXPECTS P1/ KCBINI OR KCBINW.
EXPECTS M/ USER'S DECODED UUO, SO THAT, IF NECESSARY, KCBINI CAN
RETURN THE ID NUMBER OF THE JOB HAVING THE INTERLOCK.
@@ACCUM
DESTROYS T1-T4, P1-P4, J, U, PG, AND W.
@@EXIT
SKIP RETURNS ON SUCCESS, WITH NORMAL RELEASE OF INTERLOCK FLAG
AND THE EVER BEEN INTERLOCKED FLAG RETURNED IN THE USER'S AC.
NON-SKIP RETURNS ON FAIL, WITH THE ERROR CODE IN THE RH(T1)
AS FOLLOWS: ERRNIC, ERRVPM, ERRNFC, ERRHIL, ERRAIL.
(MAY ALSO EXIT THRU ABTUUO IF A TRAP NEEDS TO BE TAKEN.).
@@ #

KCBINI::JRST	.+1		;MAKE NOT SAME ADDR AS CLBINW.
KCBINW::SKIPGE	JBTAWQ(J)	;IF AN INTERRUPT IS WAITING,
	PJRST	ABTUUO		;  BACK UP THE PC AND TAKE THE TRAP
	PUSHJ	P,CHKFRC	;IN CLUB, VP MATCH, 1ST CLASS?
	POPJ	P,		;NO -- ERROR-RETURN(CODE ALREADY IN T1)
	HRRZ	T4, CMCCDB(T1)	;T4/ CDB ADDRESS
	HLRZ	T2, CDBLOK(T4)	;T2/ INTERLOCKER CMC OR 0
	JUMPE	T2,	[		;IF CLUB IS NOT INTERLOCKED,
		HRLM	T1, CDBLOK(T4)	;  SET US AS THE INTERLOCKER
		JRST	KCBIW2		;RETURN WITH THE INTERLOCK
	]

    ;HERE IF SOMEONE HAS THE CLUB INTERLOCKED
    ;(T1/ OUR CMC, T2/ INTERLOCKER CMC, T4/ CLUB DATA BLOCK)

	CAMN	T1, T2		;ARE WE THE INTERLOCKER?
	JRST	[		;YES -- ERROR-RETURN
		MOVEI	T1, ERRHIL
		POPJ	P,
	]
	CAIN	P1, KCBINI	;IF THE CALLER DOESN'T WANT TO WAIT,
	JRST	[
		MOVE	T1,T2
		LDB	T1,CMYCID	;T1/INTERLOCKER ID
		LDB	T2, PUUOAC
		UMOVE	T2, (T2)
		UMOVEM	T1, (T2)	;RETURN IT
		MOVEI	T1, ERRAIL	;AND "ALREADY INTERLOCKED" CODE
		POPJ	P,
	]

    ;HERE TO WAIT FOR THE CLUB INTERLOCK

	HRLM	T1, JBTCLB(J)	;STORE CMC OF AWAITED CLUB
	MOVEI	T2, ILWQ
	DPB	T2, PJBSTS
	PUSHJ	P, WSCHED	;WAIT FOR INTERLOCK
	HLRZ	T2, CDBLOK(T4)
	CAMN	T2, T1		;WERE WE GIVEN THE INTERLOCK?
	JRST	KCBIW2		;YES
	HRRZS	JBTCLB(J)	;NO -- CLEAR AWAITED CMC
	PJRST	ABTUUO		;      AND TAKE TRAP AND RETRY

    ;HERE TO RETURN WITH THE INTERLOCK

KCBIW2:	HLLZ	T1, CDBSTS(T4)	;T1/ CLUB STATUS BITS
	MOVSI	T2, CD.EVI
	IORM	T2, CDBSTS(T4)	;SET "EVER INTERLOCKED"
	LDB	T2, PUUOAC
	UMOVEM	T1, (T2)	;RETURN THE CLUB STATUS
	JRST	CPOPJ1

COMMENT #
@@SUBROUTINE KCBRLI
@@PURPOSE
SUBR FOR THE CLBRLI UUO.  RELEASES THE CLUB INTERLOCK, IFF
THIS JOB HAS IT, AND STARTS UP NEXT IN ILWQ FOR THIS CLUB.
@@ENTRY
EXPECTS W/ VP NUMBER, J/ JOB NUMBER, AND P3 AND P4/ LMA
INFORMATION.  EXPECTS THE VP TO EXIST.
@@ACCUM
DESTROYS T1-T4, P1-P4, J, U, PG, AND W.
@@EXIT
SKIP RETURNS ON SUCCESS.
NON-SKIP RETURNS ON FAIL, WITH THE ERROR CODE IN THE RH(T1)
AS FOLLOWS: ERRNIC, ERRVPM, ERRNFC, ERRNIL.
@@ #

KCBRLI::PUSHJ	P, CHKFRC	;IN CLUB, VP MATCH, 1ST CLASS?
	POPJ	P,		;NO.
	HRRZ	T4, CMCCDB(T1)	;T4/ CDB ADDR
	HLRZ	T2, CDBLOK(T4)	;T2/ INTERLOCKER CMC
	CAME	T2, T1		;ARE WE THE INTERLOCKER?
	JRST	[		;NO -- ERROR-RETURN
		MOVEI	T1, ERRNIL
		POPJ	P,
	]
	PUSHJ	P, RELINL	;RELEASE INTERLOCK, START 1ST WAITER
	MOVSI	T2, CD.NML
	IORM	T2, CDBSTS(T4)	;SET "NORMAL RELEASE"
	JRST	CPOPJ1

COMMENT #
@@SUBROUTINE RELINL
@@PURPOSE
RELEASES THE INTERLOCK FOR A CLUB AND RESTARTS
THE FIRST WAITER, IF THERE IS ONE, FOR THE CLUB'S INTERLOCK.
@@ENTRY
EXPECTS J/ JOB NUMBER OF INTERLOCKER
EXPECTS T4/ INTERLOCKED CLUB CDB ADDRESS
@@ACCUM
DESTROYS T1-T3, PRESERVES T4.
@@EXIT
@@ #

EXTERNAL UPTIME

	RNDMSK==17		;EVERY TIME UPTIME HAS THIS BIT ON, GIVE IT
				; TO FIRST WAITER REGARDLESS OF MRQ AS
				; A FAIRNESS CONSIDERATION.

RELINL:	HRRE	T1, JBTQ-ILWQ	;T1/ 1ST JOB IN ILWQ
	JUMPL	T1, RELIN7	;NONE
	PUSH	P,P1		;P1 WILL HAVE MRQ JOB IF ANY
	SETZ	P1,
RELIN2:	HLRZ	T2, JBTCLB(T1)	;T2/ AWAITED CMC FOR JOB
	JUMPE	T2,RELIN3	;JUMP IF ALREADY AWAKENED
	HRRZ	T3, CMCCDB(T2)	;T3/ ITS CDB
	CAMN	T3, T4		;WAITING FOR THIS CLUB?
	CAIN	J, (T1)		;AND NOT THE CURRENT JOB?
	JRST	RELIN3
	MOVE	T3,UPTIME	;JOB IS OK TO GIVE LOCK TO.
	TRNN	T3,RNDMSK	;TIME TO GIVE TO HIM EVEN IF CAN'T RUN YET?
	JRST	RELIN5		;YES.
	MOVE	T3,JBTSTS(T1)	;NOT YET, SEE IF MRQ ON
	TLNN	T3,MRQ		;IS IT?
	JRST	RELIN5		;NO, GIVE LOCK TO HIM.
	SKIPN	P1		;SEE ANOTHER JOB BEFORE THIS IN QUEUE WITH MRQ?
	MOVE	P1,T1		;NO, THIS GETS LOCK IF NO ONE ELSE WANTS IT
;	JRST	RELIN3		;AND GO SCAN FOR A BETTER JOB
RELIN3:	HRRE	T1, JBTQ(T1)	;T1/ NEXT JOB
	JUMPGE	T1, RELIN2

    ;HERE IF NO ONE WITHOUT MRQ IS WAITING FOR THE INTERLOCK.

RELIN4:	JUMPE	P1,RELIN6	;JUMP IF NO ONE AT ALL WANTS THE LOCK.
	MOVE	T1,P1		;SOMEONE WITH MRQ WANTS IT, GIVE IT AWAY TO HIM
	HLRZ	T2,JBTCLB(T1)	;RELIN5 EXPECTS CMC IN T2
	JRST	RELIN5
RELIN6:	POP	P,P1		;RESTORE P1
RELIN7:	HRRZS	CDBLOK(T4)	;CLEAR THE INTERLOCKER FIELD
	POPJ	P,

    ;HERE TO WAKE UP A WAITER (T1/ WAITER'S JOB #, T2/ CMC)

RELIN5:	HRLM	T2, CDBLOK(T4)	;GIVE IT THE INTERLOCK
	HRRZS	JBTCLB(T1)	;CLEAR ITS AWAITED CMC
	PUSH	P, J		;SAVE J
	HRRZ	J, T1
	MOVEI	T1, ISQ		;INTERLOCK SATISFIED (NOT A REAL SEPERATE CODE)
	DPB	T1, PJBSTS
	PUSHJ	P, SETRUN	;MARK IT RUNNABLE
	POP	P, J		;RESTORE J
	POP	P,P1		;AND P1
	POPJ	P,

SUBTTL CLBMEM UUO
COMMENT #
@@SUBROUTINE KCBMEM
@@PURPOSE
SUBR FOR THE CLBMEM UUO.  GETS THE COUNT OF MEMBERS IN THE CLUB
AND AS MANY OF THEIR ID NUMBERS AS THE USER HAS ASKED FOR.
@@ENTRY
EXPECTS W/ VP NUMBER, J/ JOB NUMBER, AND P3 AND P4/ LMA
INFORMATION.  EXPECTS THE VP TO EXIST.
EXPECTS P2/ COUNT ARG FROM USER.
EXPECTS M/ USER'S DECODED UUO, SO VALUES CAN BE RETURNED.
@@ACCUM
DESTROYS T1-T4, P1-P4, J, U, PG, AND W.
@@EXIT
SKIP RETURNS ON SUCCESS, WITH VALUES STORED IN USER AREA.
NON-SKIP RETURNS ON FAIL, WITH THE ERROR CODE IN THE RH(T1)
AS FOLLOWS: ERRNIC, ERRVPM, ERRNFC, ERRCNT.
@@ #

KCBMEM::PUSHJ	P,CHKFRC	;IN CLUB, VP MATCHES, 1ST CLASS?
	POPJ	P,		;NO.
	JUMPL	P2, [		;IF COUNT < 0, ERROR-RETURN
		MOVEI	T1, ERRCNT
		POPJ	P,
	]
	HRRZ	P1, CMCCDB(T1)	;P1/ CDB ADDR
	MOVE	P4,P2		;SAVE COUNT IN CASE HAVE TO START OVER
	LDB	T2, PUUOAC
	UMOVE	T2, (T2)	;T2/ USER SPACE COUNT ADDRESS
	PUSH	P,T2		;SAVE IT
	MOVE	P3,LVCCNT	;GET COUNT OF NUMBER OF TIMES SOMEONE LEFT A CLUB
KCBME0:	SETZ	T3,		;T3/ COUNT
	ADDI	T2, 1		;T2/ USER SPACE ID ADDRESS
	HRRZ	T1, CDBCMC(P1)	;T1/ 1ST CMC FOR CLUB
	JUMPN	T1, .+2
	  STOPCD		;AT LEAST CURRENT JOB MUST BE IN CLUB
KCBME2:	AOJ	T3,		;T3/ COUNT OF JOBS IN CLUB
	SOJL	P2, KCBME4	;IF THERE'S ROOM TO STORE THIS ID,
	LDB	T4,CMYCID	; T4/ JOB-IN-CLUB ID
	UMOVEM	T4, (T2)	;  STORE IT IN CALLER'S TABLE
	CAME	P3,LVCCNT	;ANYONE POSSIBLY DESTROY THE CHAIN?
	JRST	KCBMRS		;YES, MUST RESTART THE UUO
	MOVEI	T2, 1(T2)	;  AND UPDATE THE ADDRESS
KCBME4:	HRRZ	T1, CMCCLK(T1)	;T1/ NEXT CMC FOR CLUB
	JUMPN	T1, KCBME2
	POP	P, T2		;RESTORE COUNT ADDRESS
	UMOVEM	T3, (T2)	;AND STORE THE JOB COUNT
	JRST	CPOPJ1

;HERE IF SOMEONE LEFT A CLUB SOMEWHERE ON THE SYSTEM. MUST
; START FROM THE FIRST CMC FOR THE CLUB AGAIN, SINCE WE DON'T KNOW
; WHICH CMC CHAIN WAS BROKEN AND WHERE (COULD HAVE BEEN THE ONE
; WE WERE SCANNING).

KCBMRS:	MOVE	P2,P4		;RESTORE NUMBER OF WORDS IN USER SPACE THERE ARE
	MOVE	P3,LVCCNT	;GET NEW COUNT OF FRAMES THAT HAVE LEFT CLUBS
	MOVE	T2,(P)		;GET ADDRESS OF USER ARG BLOCK.
	JRST	KCBME0		;AND RESTART.

SUBTTL DISCOVER THINGS ABOUT JOBS IN YOUR CLUB
COMMENT #
@@SUBROUTINE KCBSTS
@@PURPOSE
SUBR FOR CLBSTS UUO. GETS A SPECIFIED CLUB MEMBER'S JBTSTS BITS.
@@ENTRY
EXPECTS W/ VP NUMBER, J/ JOB NUMBER, AND P3 AND P4/ LMA
INFORMATION.  EXPECTS THE VP TO EXIST.
EXPECTS P2/ ID NUMBER OF SPECIFIED JOB.
EXPECTS M/ USER'S DECODED UUO, SO VALUES CAN BE RETURNED.
@@ACCUM
DESTROYS T1-T4
@@EXIT
SKIP RETURNS ON SUCCESS, WITH THE VALUES STORED IN THE USER'S
AC.
NON-SKIP RETURNS ON FAIL, WITH THE ERROR CODE IN THE RH(T1)
AS FOLLOWS: ERRNIC, ERRVPM, ERRNFC, ERRJNI.
@@ #

KCBSTS::PUSHJ	P,FNDJOB	;CHECK US, LOOK FOR JOB.
	POPJ	P,		;FAILURE.

    ;HERE ON FOUND JOB.

	MOVE	T1, JBTSTS(T1)	;T1/ JOB STATUS BITS
	LDB	T2, PUUOAC
	UMOVEM	T1, (T2)	;GIVE IT TO THE LUSER
	JRST	CPOPJ1

SUBTTL DO THINGS TO JOBS IN YOUR CLUB
COMMENT #
@@SUBROUTINE KCBWAK
@@PURPOSE
SUBR FOR THE CLBWAK UUO.  WAKES UP A JOB IN THE SAME CLUB.
@@ENTRY
EXPECTS W/ VP NUMBER, J/ JOB NUMBER, AND P3 AND P4/ LMA
INFORMATION.  EXPECTS THE VP TO EXIST.
EXPECTS P2/ ID NUMBER OF SPECIFIED JOB.
@@ACCUM
DESTROYS T1-T4, P1-P4, J, U, PG, AND W.
@@EXIT
SKIP RETURNS ON SUCCESS.
NON-SKIP RETURNS ON FAIL, WITH THE ERROR CODE IN THE RH(T1)
AS FOLLOWS: ERRNIC, ERRVPM, ERRNFC, ERRJNI.
@@ #

KCBWAK::PUSHJ	P,FNDJOB	;CHECK US, LOOK AT JOB.
	POPJ	P,		;FAILURE.

    ;HERE ON FOUND JOB.

	AOS	(P)		;SET UP FOR SKIP RETURN.
	MOVEI	J,(T1)		;GET JOB'S NUMBER IN J.
	LDB	T1,JBYWAK	;JUMP IF HIBERING OR IF ENABLED
	JUMPN	T1,TAKTRP	;FOR SOFTWRE INTRPT ON WAKE UUO.
	MOVEI	T1,WAKFLG	;SAY
	IORM	T1,JBTSTS(J)	;"WAKE UP!"
	POPJ	P,		;AND
				;RETURN
				;TO
				;THE
				;USER

COMMENT #
@@SUBROUTINE KCBHNG
@@PURPOSE
SUBR FOR THE CLBHNG UUO.  HANGS UP A JOB IN THE SAME CLUB.
@@ENTRY
EXPECTS W/ VP NUMBER, J/ JOB NUMBER, AND P3 AND P4/ LMA
INFORMATION.  EXPECTS THE VP TO EXIST.
EXPECTS P2/ ID NUMBER OF SPECIFIED JOB.
@@ACCUM
DESTROYS T1-T4, P1-P4, J, U, PG, AND W.
@@EXIT
SKIP RETURNS ON SUCCESS.
NON-SKIP RETURNS ON FAIL, WITH THE ERROR CODE IN THE RH(T1)
AS FOLLOWS: ERRNIC, ERRVPM, ERRNFC, ERRJNI.
@@ #

KCBHNG::PUSHJ	P,FNDJOB	;IN CLUB, VP MATCHES, 1ST CLAS?
	POPJ	P,		;FAILURE.

    ;HERE ON FOUND JOB.

	TLO	T1,3		;TELL HNGMON THIS IS A JOB # AND
	PUSHJ	P,HNGMON	;FORCE LOGOUT EVN IF DET ON DIS.
	STOPCD
	JRST	CPOPJ1		;

SUBTTL FORCE JOBS OUT OF CLUBS
COMMENT #
@@SUBROUTINE CBREMV
@@PURPOSE
CALLED WHEN A VP IS REMOVED FROM A JOB'S MAP TO THROW THE
JOB OUT OF THE CLUB IT ENTERED VIA THAT VP, IF THERE
IS SUCH A CLUB.
@@ENTRY
EXPECTS J/ JOB NUMBER
EXPECTS W/ VP NUMBER
@@ACCUM
DESTROYS T1-T4
@@ #

CBREMV::PUSHJ	P, SRCCMC	;IN A CLUB WITH THIS VP?
	POPJ	P,		;NO -- JUST RETURN

    ;HERE TO LEAVE CLUB (T1/ CMC ADDR)

	HRRZ	T4, CMCCDB(T1)	;T4/ CDB ADDR
	HLRZ	T2, CDBLOK(T4)	;T2/ INTERLOCKER CMC
	CAME	T1, T2		;IF IT'S NOT THE INTERLOCKER,
	PJRST	RELCLB		;  JUST LEAVE THE CLUB

    ;HERE TO RELEASE THE INTERLOCK

	PUSH	P, T1		;SAVE CMC ADDR
	PUSHJ	P, RELINL	;RELEASE THE INTERLOCK
	MOVSI	T2, CD.NML
	ANDCAM	T2, CDBSTS(T4)	;AND MARK ABNORMAL RELEASE
	POP	P, T1		;RESTORE CMC ADDR
	PJRST	RELCLB		;LEAVE THE CLUB AND RETURN

COMMENT #
@@SUBROUTINE CBSWER
@@PURPOSE
CALLED WHEN A JOB IS ABOUT TO BE ZAPPED BECAUSE OF A CONTEXT-PAGE
SWAP ERROR TO FORCE THE JOB OUT OF ALL THE CLUBS IT'S IN.
@@ENTRY
EXPECTS J/ JOB NUMBER
@@ACCUM
DESTROYS T1-T4
@@ #

CBSWER::HRRZ	T1, JBTCLB(J)	;T1/ FIRST CMC FOR JOB
	JUMPE	T1, CPOPJ	;NONE -- JUST RETURN
	PUSH	P, T1		;0(P)/ CMC ADDR
CBSWE2:	HRRZ	T4, CMCCDB(T1)	;T4/ ITS CDB
	HLRZ	T2, CDBLOK(T4)	;T2/ ITS INTERLOCKER CMC
	CAME	T1, T2		;IF IT'S THE INTERLOCKER,
	JRST	CBSWE3
	PUSHJ	P, RELINL	;  RELEASE THE INTERLOCK
	MOVSI	T2, CD.NML
	ANDCAM	T2, CDBSTS(T4)	;  AND MARK ABNORMAL RELEASE
CBSWE3:	MOVE	T1, 0(P)	;T1/ CMC ADDR
	PUSHJ	P, RELCLB	;LEAVE THE CLUB
	MOVE	T1, 0(P)	;T1/ CMC ADDR (AGAIN?)
	HRRZ	T1, CMCJLK(T1)	;T1/ NEXT CMC FOR JOB
	JUMPE	T1, TPOPJ
	MOVEM	T1, 0(P)
	JRST	CBSWE2

COMMENT #
@@SUBROUTINE CBJERR
@@PURPOSE
CALLED BY THE CODE THAT SETS JERR TO KICK THE JOB OUT OF ALL
THE CLUBS IT'S IN
@@ENTRY
EXPECTS J/ JOB NUMBER.
@@ACCUM
DESTROYS T1.
@@ #

CBJERR::PUSH	P, T2
	PUSH	P, T3
	PUSH	P, T4		;SAVE T2-T4 FROM CBSWER
	PUSHJ	P, CBSWER	;EXPEL JOB FROM CLUBS IT'S IN
	POP	P, T4
	POP	P, T3
	POP	P, T2		;RESTORE T2-T4
	POPJ	P,

SUBTTL CHECK INTERLOCK AVAILABILITY ON CONTINUE
COMMENT #
@@SUBROUTINE CBCONT
@@PURPOSE
SUBR CALLED BY SETRUN, WHEN SETRUN IS ABOUT TO SEND A JOB INTO
ILWQ.  CBCONT CHECKS TO SEE IF THE JOB'S CLUB IS STILL INTER-
LOCKED, AND IF IT ISN'T, GIVES THE INTERLOCK TO THE JOB AND SETS
THE JOB UP TO GO INTO THE RNQ INSTEAD FOR THE ILWQ.
@@ENTRY
EXPECTS J/ JOB NUMBER AND T1/ ILWQ.
@@ACCUM
DESTROYS NO ACS EXCEPT PUTS ISQ IN T1 IF NECESSARY, SINCE
SETRUN EXPECTS THIS.
@@EXIT
SEE ACCUM
@@ #

CBCONT::HLRZ	T1, JBTCLB(J)	;T1/ AWAITED CMC
	JUMPN	T1, .+2
	  STOPCD		;CAN'T GO INTO ILWQ WITHOUT ONE
	PUSH	P, T2
	PUSH	P, T4

	HRRZ	T4, CMCCDB(T1)	;T4/ CDB ADDR
	HLRZ	T2, CDBLOK(T4)	;T2/ INTERLOCKER CMC
	JUMPE	T2, CBCON2	;IF IT'S STILL INTERLOCKED,
	MOVEI	T1, ILWQ	;  GO INTO ILWQ
	JRST	CBCON3

    ;HERE WHEN CLUB IS NOT STILL INTERLOCKED

CBCON2:	HRLM	T1, CDBLOK(T4)	;MAKE US THE INTERLOCKER
	HRRZS	JBTCLB(J)	;MARK US NO LONGER WAITING
	MOVEI	T1, ISQ
	DPB	T1, PJBSTS	;CHANGE WAIT STATE

CBCON3:	POP	P, T4
	POP	P, T2
	POPJ	P,

SUBTTL LITTLE SUBROUTINES
COMMENT #
@@SUBROUTINE CHKFRC
@@PURPOSE
CHECKS THAT THE JOB IS A FIRST-CLASS MEMBER OF THE CLUB
SPECIFIED BY THE VP NUMBER IN W, AND FINDS THE CMC FOR THE
CLUB.
@@ENTRY
EXPECTS W/ VP NUMBER, AND J/ JOB NUMBER.
@@ACCUM
DESTROYS T1-T2.
@@EXIT
SKIP-RETURNS IF OK, WITH T1/ CMC ADDR.
NONSKIP-RETURNS ON ERRORS, WITH T1/ ERROR CODE (ERRNIC,
ERRVPM, ERRNFC).
@@ #

CHKFRC:	SKIPN	JBTCLB(J)	;ARE WE IN A CLUB?
	JRST	[		;NO -- ERROR-RETURN
		MOVEI	T1, ERRNIC
		POPJ	P,
	]
	PUSHJ	P, SRCCMC	;ARE WE IN A CLUB WITH THIS VP?
	JRST	[		;NO -- ERROR-RETURN
		MOVEI	T1, ERRVPM
		POPJ	P,
	]
	MOVSI	T2, CM.FRC
	TDNN	T2, CMCSTS(T1)	;ARE WE FIRST-CLASS?
	JRST	[		;NO -- ERROR-RETURN
		MOVEI	T1, ERRNFC
		POPJ	P,
	]
	JRST	CPOPJ1

COMMENT #
@@SUBROUTINE FNDJOB
@@PURPOSE
CHECKS THAT THE CALLER IS A FIRST-CLASS MEMBER OF THE CLUB
SPECIFIED, AND LOOKS FOR THE SPECIFIED JOB
@@ENTRY
EXPECTS W/ VP NUMBER, J/ JOB NUMBER, AND P2/ ID NUMBER OF
SPECIFIED JOB.
EXPECTS P3 AND P4 TO BE THE LMA INFORMATION. (NOT USED /AAA)
@@ACCUM
DESTROYS T1-T4 AND PG.
@@EXIT
SKIP RETURNS IF OKAY, WITH T1/ NUMBER OF SPECIFIED JOB.
ELSE, NON-SKIP RETURNS WITH ERROR CODE ALREADY SET UP IN RH(T1)
AS FOLLOWS: ERRNIC, ERRVPM, ERRNFC, ERRJNI.
@@ #

FNDJOB:	PUSHJ	P,CHKFRC	;IN CLUB, VP MATCHES, 1ST CLASS?
	POPJ	P,		;NO.

    ;LOOK FOR A JOB HAVING THE SPECIFIED ID
    ;(T1/ CURRENT JOB'S CMC FOR THE CLUB)

	HRRZ	T4, CMCCDB(T1)	;T4/ CDB ADDR
	HRRZ	T1, CDBCMC(T4)	;T1/ 1ST CMC FOR CLUB
	JUMPN	T1, .+2
	  STOPCD		;AT LEAST CURRENT JOB MUST BE IN CLUB
FNDJB2:	LDB	T2,CMYCID
	CAMN	P2,T2		;IS THISTHE SPECIFIED CLUB MEMBER
	JRST	FNDJB3		;YES -- GO GET ITS JOB NUMBER
	HRRZ	T1, CMCCLK(T1)	;T1/ NEXT CMC FOR CLUB
	JUMPN	T1, FNDJB2	;MORE JOBS IN CLUB?
	MOVEI	T1, ERRJNI	;NO -- ERROR-RETURN
	POPJ	P,

    ;HERE WITH T1/ THE CMC OF THE SPECIFIED JOB

FNDJB3:	HLRZ	T1, CMCJOB(T1)	;T1/ JOB NUMBER + ETC.
	ANDI	T1,CM.JMK	;T1/JOB NUMBER
	JRST	CPOPJ1

COMMENT #
@@SUBROUTINE SRCBTB
@@PURPOSE
SUBROUTINE TO SEARCH THE CLUB TABLE FOR A MATCH WITH THE
RETRIEVAL PNTR ASSOCIATED WITH OUR LMA SLOT.
@@ENTRY
EXPECTS P3 AND P4 TO BE THE LMA SLOTS, J/ JOB NUMBER.
EXPECTS THE PAGE TO EXIST.
@@ACCUM
DESTROYS T1-T4 AND PG.
@@EXIT
IF THERE IS A MATCH, SKIP RETURNS WITH T4/ ADDR OF ENTRY.
IF THERE IS NO MATCH, NON-SKIP RETURNS WITH T2/ RETRIEVAL PNTR
WITH RBREAL ON AND T3/ REL ADDR OF BASE ENTRY IN CLBTBL FOR THIS
HASH CODE.
@@ #

SRCBTB:	PUSHJ	P,GETDPA	;GET T2/ RET PNTR FOR THIS PAGE.
	MOVE	T3,T2		;SET UP T3 FOR HASH.
	TLO	T2,RBREAL	;MAKE GOOD RET PNTR.
	IDIVI	T3,HSHCLB 	;GET T4/ ADDRESS OF CLBTBL
	MOVE	T3,T4		;BASE FOR THIS HASH AND SAVE
	SKIPN	T4,CLBTBL(T4)	;T4/ CDB ADDR OR 0
	POPJ	P,		;NO CDBS -- NONSKIP-RETURN
SRCBT4:	CAMN	T2,CDBRET(T4)	;MATCH?
	JRST	CPOPJ1		;YES, SUCCESS.
	HRRZ	T4,CDBHLK(T4)	;TRY FOR ANOTHER.
	JUMPN	T4,SRCBT4	;JUMP IF THERE IS ONE.
	POPJ	P,		;OUR RET PNTR NOT FOUND.

COMMENT #
@@SUBROUTINE ULKCDB
@@PURPOSE
SUBROUTINE TO REMOVE A CDB FROM THE CLBTBL HASH CHAIN IT'S IN.
@@ENTRY
EXPECTS T4/ CDB ADDR
@@ACCUM
DESTROYS T1-T2.
@@EXIT
EITHER NONSKIP-RETURNS WITH THE CDB DELETED FROM THE HASH CHAIN
OR STOPCDS IF IT CAN'T FIND THE CDB.
@@ #

ULKCDB:	MOVE	T1, CDBRET(T4)	;T1/ RETRIEVAL POINTER
	TLZ	T1, RBMASK	;TURN OFF NON-ADDRESS BITS
	IDIVI	T1, HSHCLB	;T2/ HASH INDEX
	HRRZ	T1, CLBTBL(T2)	;T1/ 1ST CDB ADDR FOR CHAIN
	CAMN	T1, T4		;IF IT'S US,
	JRST	[
		HRRZ	T1, CDBHLK(T1)
		MOVEM	T1, CLBTBL(T2)	;POINT CLBTBL AROUND US
		POPJ	P,		;AND RETURN
	]
	SKIPN	T1		;IF IT'S 0,
	  STOPCD		;  CRASH (CDB MUST BE IN TABLE)
ULKCD2:	HRRZ	T2, CDBHLK(T1)	;T2/ SUCCESSOR
	CAMN	T2, T4		;IF IT'S US,
	JRST	[
		HRR	T2, CDBHLK(T2)
		HRRM	T2, CDBHLK(T1)	;POINT PREDECESSOR AROUND US
		POPJ	P,		;AND RETURN
	]
	SKIPN	T1, T2		;IF IT'S 0,
	  STOPCD		;  CRASH (CDB MUST BE IN TABLE)
	JRST	ULKCD2

COMMENT #
@@SUBROUTINE SRCCMC
@@PURPOSE
SUBROUTINE TO FIND THE CMC (IF THERE IS ONE) FOR A PARTICULAR
VP FOR THIS JOB.
@@ENTRY
EXPECTS J/ JOB NUMBER, W/ VP NUMBER
@@ACCUM
DESTROYS T1-T2.
@@EXIT
SKIP-RETURNS WITH T1/ THE ADDRESS OF THE CMC FOR THE VP,
OR NONSKIP-RETURNS IF THERE IS NONE.
@@ #

SRCCMC:	HRRZ	T1, JBTCLB(J)	;T1/ ADDR OF 1ST CMC FOR JOB
	JUMPE	T1, CPOPJ	;NONE -- FAIL-RETURN
SRCCM2:	LDB	T2,CMYVPG
	CAMN	W, T2		;IF IT'S THE RIGHT CMC,
	JRST	CPOPJ1		;  RETURN ITS ADDR
	HRRZ	T1, CMCJLK(T1)	;T1/ ADDR OF NEXT CMC
	JUMPN	T1, SRCCM2
	POPJ	P,		;NO MORE -- FAIL-RETURN

	LIT

	END
    &d|Í