TITLE ACTSER - STREAM ACCOUNTING SERVICE ROUTINES
COMMENT /@@MODULE ACTSER
@@PURPOSE
THIS MODULE CONSISTS OF THE DDB, DISPATCH TABLE , AND
DEVICE DEPENDENT ROUTINES FOR THE ACCOUNTING PSEUDO DEVICE,
ACT.  IN ADDITION THERE ARE SUBROUTINES FOR WRITING ACCOUNTING
RECORDS TO THE MONITOR BUFFER.

THE ACT DEVICE IS A MONITOR BUFFER CLASS DEVICE THT LOOKS LIKE ANY
I-O BUS DEVICE TO THE REST OF THE SYSTEM. @@/

;-EDITED SEP 76 A.ATKINSON FOR NEW STREAM ACCT'G FUNCTIONS
;-USER PROGRAMS CAN NOW WRITE STREAM RECORDS VIA NEW
;-	PUTSAR UUO (SOME RECORD TYPES REQUIRE LICENSE).
;-	NEW RECORD TYPE 7 IMPLEMENTED FOR PROJECT-CODE CHANGE.
;-	NEW REC TYPE 10 TO RECORD XCHARG TRU'S GIVEN AWAY.
;-";-" INDICATES MOD RELATING TO ALL THIS.
	EXTERNAL JBTAWQ,JOB,JBTLIC

	EXTERNAL CBLAMB,TIME,JFYSEC,CLKTRU,CTMBS,ALR620,DBLMSG
	EXTERNAL ACREQ,ACWAIT,ACAVAL,PJOBN
EXTERN CBLEMB,CPOPJ,ERRICO,MBBXFR,SETIOD,THSDAT,CLKTRU
EXTERN JBTSTS,JBTAUN,JBTFPN,JBTPPN,JBTSOK,JBTNAM,TAKTRJ
EXTERN NINTRP,CPOPJ1,TSTARM,TAKTRP,ABTUUO
EXTERN ACSWT,JBTABT,MLOWQ,PJBSTS,RNQ,SETRUN,WSCHED
EXTERN UPTSTS,REC.WR,LSAREC
; DEFINE ACCOUNTING DEVICE DDB

ACTDDB:	PHASE 0
DEVNAM:	SIXBIT /ACT/
DEVCHR:	100		;NO HUNG DEVICE TIME
DEVIOS:	0
DEVSER:	EXP ACTDSP
DEVMOD:	DVIN,,<1_IB>!<1_I>
DEVLOG:	0
DEVBUF:	0
DEVIAD:	0
DEVOAD:	0
DEVCLS:	XWD 0,ACTBUF	;SAME TYPE AS IO BUS DEVICES
DEVSTS:	0
 XP ACTDED,400000	;WATCHDOG SETS THIS IF CHKPNT DIES
DEVPWC:	0
DEVWUC:	0
DEVCTR:	0
DEVINT:	0		;SOFTWARE INTERRUPT WORD
	DEPHASE
ACTDDS==.-ACTDDB

ACTBL=1000	;BUFFER SIZE
;DEFINE IN & OUT PTRS, WITH BUFFER CONTAINING INITIAL
; TYPE 11 'SYSTEM UP' RECORD OF 2 WORDS. NEVER WRITTEN BY ANYBODY.
	XWD ACTBUF+2,ACTBUF		;IN AND OUT
	EXP ACTBUF+ACTBL		;LAST
ACTBUF:	XWD 011000,2	;SYSTEM UP HEADER WORD
	XWD 011000,2	;REPEAT FOR GOOD MEASURE
	BLOCK ACTBL-2	;TOTAL ACTBL WORDS OF BUFFER

; DISPATCH TABLE

ACTDSP:	PHASE 0
DDXZ:	DSPSIZ
DDINT:	JRST INTACT
DBYT:	4400,,4400
DVSIZ:	MOVEI T1,ACTDDS
DINI:	POPJ P,
DHNG:	POPJ P,
DRL:	JRST ACTREL
DCL:	POPJ P,
DOU:	JRST ERRICO
DIN:	JRST ACTIN
DXFR:	JRST ACTXFR
DGTRD:	POPJ P,
DMT:	JRST ACTEOF
DGF:	JRST ACTUGF
	DSPSIZ==.-1
	DEPHASE
COMMENT /
@@SUBROUTINE ACTIN
@@PURPOSE
DEVICE DEPENDENT FUNCTION FOR INPUT UUO
@@ENTRY
ACCUMULATORS S AND F SET UP TO DEVIOS AND ACTDDB
@@ACCUM
T1,T2,T3
@@TABLES
ACTDDB, ACTBUF
@@FUNCTION
IF MONITOR BUFFER IS NON EMPTY THEN RETURN;
ELSE SET IOACT AND RETURN UNLESS THE MODE (LOW ORDER 9 BITS OF
DEVIOS) IS 110, IN WHICH CASE CLEAR IOACT AND SET IOEND.

THE MODE IS SET TO 110 BY THE MTAPE UUO FOR ACT AND
IS USED TO INDICATE THAT THE CALLER WANTS AND EOF WHEN
THE BUFFER IS EMPTY.
@@EXIT
DEVIOS IS UPDATED
@@CALLS
CBLEMB
@@/

ACTIN:	PUSHJ P,CBLEMB		;FIND SPACE IN MONITOR BUFFER
	JUMPN T1,CPOPJ	;BUFFER IS NON EMPTY. O.K. TO TRANSFER IT
	TRO S,IOACT	;EMPTY BUFFER SET IOACT
	LDB T1,[POINT 9,DEVIOS(F),35]
	CAIN T1,110		;CALLER WANTS EOF WHEN BUFFER EMPTY?
	TDC S,[IOEND,,IOACT] ;YES. CLEAR IOACT AND SET IOEND
	MOVEM S,DEVIOS(F)
	POPJ P,

COMMENT /@@SUBROUTINE ACTREL
@@PURPOSE
DEVICE DEPENDENT CODE FOR RELEASE UUO
@@ACCUM
U,W,T1,T2
@@FUNCTION
SENDS "*** ACCOUNTING DEVICE RELEASED ***" MESSAGE TO
OPER & CTY TERMINALS, OR JUST CTY IF NO OPER. AAA JAN 77
@@CALLS
DBLMSG (CLOCK1, PG. 10)
@@/

ACTREL:	PUSH P,F		;DBLMSG CLOBBERS F,U,T1,..
	MOVEI T1,[BYTE (7)7,7,7,7,7	; RING BELL GOOD
		ASCIZ /
*** ACCOUNTING DEVICE RELEASED ***
/]
	PUSHJ P,DBLMSG	;SEND TO OPR & CTY ELSE JUST CTY
	MOVEI T1,ALRACT
	IORM T1,ALR620
	PUSHJ P,WAKACJ	;IF WAITERS EXIST, UNWAIT ONE OF THEM
	JFCL		;(ALWAYS SKIPS)
	
	POP P,F
	POPJ P,

COMMENT /@@SUBROUTINE ACTEOF
@@PURPOSE
TO FLAG THAT THE CALLER WANTS AN EOF WHEN THE
MONITOR BUFFER IS EMPTY
@@ENTRY
F MUST BE SET UP TO ACTDDB
@@ACCUM
T1
@@TABLES
ACTDDB
@@FUNCTION
SETS THE MODE FIELD OF DEVIOS (LOW ORDER 9 BITS) TO 110.  CALLED
VIA MTAPE DISPATCH.
@@EXIT
DEVIOS UPDATED
@@/
ACTEOF:	MOVEI T1,110
	HRRM T1,DEVIOS(F)	;MAKE SURE IOACT IS RESET
	POPJ P,
; DEVICE DEPENDENT INTERRUPT CHECK ROUTINE
; CALLED FROM UUOCON VIA DDINT DISPATCH (-6)
; ON ENTRY (T2)=INTERRUPT CONDITION CODE

COMMENT /@@SUBROUTINE INTACT
@@PURPOSE
TO CHECK SOFTWARE INTERRUPT CONDITIONS FOR ACT.
@@ENTRY
(T2)=INTERRUPT CONDITION CODE (0-5)
@@ACCUM
T2
@@TABLES
ACTBUF
@@FUNCTION
CALLED FROM INTRMT UUO CODE AND GIVES 0, 1, OR 2 SKIP RETURN:
0 SKIP - INVALID INTERRUPT CONDITION FOR THIS DEVICE

1 SKIP - VALID CONDITION BUT NOT SATISFIED AT THIS TIME

2 SKIP- TAKE IMMEDIATE INTERRUPT
@@/
INTACT:	XCT INTTAB(T2)
	AOS (P)		;TRAP NOW CONDITION
	JRST CPOPJ1	;NO ERROR. BUT NO TRAP EITHER

INTTAB:	PUSHJ P,BUFCHK	;CHECK BUFFER FOR INPUT AVAILABLE
	POPJ P,		;ERROR ON OUTPUT
	JRST CPOPJ1	;NO IMMEDIATE TRAP ON I/O WAIT
	POPJ P,		;NO EOF TRAP
	POPJ P,		;NO ERROR TRAP
	POPJ P,		;NO SPECIAL TRAP

BUFCHK:	MOVS T2,ACTBUF-2	;GET BUFFER IN AND OUT POINTERS
	CAMN T2,ACTBUF-2	;BUFFER EMPTY?
	AOS (P)			;YES.
	POPJ P,			;NO.

; TRANSFER ROUTINE. DO A TRANSFER. THEN UNWAIT SOMEONE
;	IF THE SITUATION WARRANTS (ACJOB .NE. 0)

;REGISTERS:  ASSUMES F, P1,P3 ETC. ARGS FOR MBBXFR
;	     RRETURNS T1,T2,T3 TO CALLER (MBICDR)

ACTXFR:	PUSHJ P,MBBXFR	;DO XFER, SKIP SUCCESS
	JRST CPOPJ
;WAKACJ- WAKE ACT JOB. IF ANY JOB OWNS THE RESOURCE AND IS IN
;MON WAIT QUEUE, HIS JOB IS IN ACJOB. UNWAIT HIM. IF HE
;COMPLETES HE WILL UNWAIT OTHERS.  CALLED BY ACTREL.

WAKACJ:	PUSH P,J	;OWNERS JOB NO.
	SKIPN J,ACJOB	;IS THERE AN ACJOB
	JRST JPOPJ1	;NOPE
	SETZM ACJOB	;KEEP THIS FROM HAPPENING AGAIN
	PUSH P,T1
	MOVEI T1,RNQ	;MAKE RUNNABLE
	DPB T1,PJBSTS	;THE JOB IN J 
	PUSHJ P,SETRUN	;(SETRUN USES ONLY T1)
	POP P,T1
JPOPJ1:	AOS -1(P)	;RETURN IS BEHIND J
JPOPJ:	POP P,J
	POPJ P,
COMMENT ! ROUTINE ACTWRT
PURPOSE: PUT AN ACCOUNTING REC INTO STREAM IF NECESSARY.
EXPECTS: T1/ RECORD TYPE
	J/ JOB NUMBER FOR WHICH ENTRY TO BE MADE
	(SINCE THIS IS ALWAYS CALLED AT UUO LEVEL, THIS
	SHOULD BE CURRENT JOB)
	UPTSTS/ REC.WR AND LSAREC BITS

TABLES: DEPENDING ON RECORD TYPE, MANY JBT'S AND UPT TABLES
	JBTFPN, JBTPPN REF'D HERE
CLOBBERS: T1-T4
FUNCTION:
IF TYPE IS ILLEGAL CRASH SYSTEM. THIS ROUTINE TO BE CALLED ONLY
BY MONITOR WHEN USER DOES SOMETHING (RUN PROG, ETC.) WHICH REQUIRES
A RECORD. NO REFERENCE TO USER CORE..NO PAGE FAULTS ETC POSSIBLE.
PUTSAR & XCHARG DO NOT USE THIS ROUTINE. SEE ACRTAB FOR
RECORD TYPES, SIZES. LICENSE FIELD IN ACRTAB NOT
RELEVANT FOR THIS ROUTINE.
RESTRICTIONS: CALL ONLY AT UUO LEVEL DUE TO POSSIBLE AC WAIT.

ERRORS: STOPCD ON INVALID RECORD TYPE


ACTION FOR EACH RECORD TYPE:
TYPE 0 (RUN): IF JBTFPN=JBTPPN,CLEAR REC.WR&LSAREC,WRITE RECORD
	ELSE CLEAR LSAREC,SET REC.WR,WRITE RECORD
TYPE 1 OR 3 (EXIT OR SETNAM): CLEAR REC.WR & LSAREC; IF REC.WR
	WAS SET WRITE A RECORD
TYPE 2 (EXIT AC,): IF REC.WR SET WRITE A RECORD
TYPE 4,5,6 (START,REE,DDT): IF JBTFPN=0 OR JBTFPN=JBTPPN,RETURN
	IF REC.WR IS SET,RETURN
	ELSE SET REC.WR,CLEAR LSAREC,WRITE RECORD,RETURN
TYPE 7 (PROJ CODE): WRITE REC & RETURN
TYPE 10,11 : STOPCD
TYPE 12 (DATE CHANGE,NOT IMPLEMENTED YET): WRITE RECORD & RETURN
TYPE 13 (LOCAL STREAM ACCOUNTING): IF LSAREC SET,RETURN
	ELSE SET REC.WR & LSAREC,WRITE RECORD,RETURN

MEANING OF REC.WR=1: A RUN OR LOCAL STREAM ACCTG UUO
	HAS PUT A BEGINNING RECORD IN STREAM; NEXT EXIT,
	SETNAM ETC. SHOULD PUT OUT AN ENDING RECORD & RESET  REC.WR
MEANING OF LSAREC: 0-NO LOCAL STR.REC PUT OUT SINCE LAST RUN
	1-ALREADY PUT ONE OUT, NO MORE ALLOWED.

!
 INTERNAL ACTWRT,ACTDDB,ACTDDS
ACTWRT:	SKIPL T1		;TYPE NEGATIVE?
	CAILE T1,NACDTS		;OR TOO BIG?
	STOPCD		;YES. SOMBODY GOOFED
	MOVE T2,%UPT+UPTSTS	;PICK UP REC.WR & LSAREC STATUS
	XCT  ACTTAB(T1)	;-DISPATCH ON TYPE,SPECIAL FOR 7


COMMENT ! THE FOLLOWING ACTTAB TABLE MUST AGREE WITH ACRTAB
	AS TO LEGAL RECORDS, ETC.
!
ACTTAB:	JRST ACTT0	;0- RUN RECORD
	JRST ACTT1	;1- EXIT
	JRST ACTT2	;2- EXIT AC,
	JRST ACTT1	;3- SETNAM UUO
	JRST ACTT3	;4- START COMMAND
	JRST ACTT3	;5- REENTER
	JRST ACTT3	;6- DDT COMMD
	JRST WRTACR	;-7 CHANGE PROJECT..SEE WRTACT DISPATCH
	STOPCD		;-10 XCHARG..DONT COME THRU HERE
	STOPCD		;-11 SYSTEM UP..NEVER SUPPOSED TO BE WRITTEN
	JRST WRTACR	;-12 DATE CHANGE..SEE WRTACT DISPATCH
	JRST ACTT13	;-13 LOCAL STR ACCTG
	JRST WRTACR	;-14 SETLIC UUO
	JRST WRTACR	;-15 SETE UUO
	JRST ACTT16	;-16 RUN LICENSE
        STOPCD          ;-17 TRANSACTION CHARGE; NOT CALLED THRU HERE
        STOPCD          ;-20 ROYALTY BEGIN/END; NOT CALLED THRU HERE
        STOPCD          ;-21 TRU COMP CHKPNT; NOT CALLED THRU HERE
        JRST    ACTT22  ;-22 CREAUX
NACDTS==.-ACTTAB

ACTT0:	;RUN
	MOVE T3,JBTPPN(J)
	SKIPN  JBTLIC(J)	;IF LICENSE SET,FORCE RECORD
	CAME T3,JBTFPN(J)
	JRST ACTT01		;RAN OUT OF ANOTHER UFD
ACTT1:	;EXIT,SETNAM
	TLZE T2,REC.WR!LSAREC	;LSAREC NEVER ON BY ITSELF
ACTT02:	PUSHJ P,WRTACR	;T1/TYPE; T2/UPTSTS..PRESERVED. GO WRITE RECORD
	MOVEM T2,%UPT+UPTSTS	;SAVE UPDATED REC.WR,LSAREC
	POPJ P,
ACTT01:	;RAN FROM OTHER UFD
	TLO T2,REC.WR
	TLZ T2,LSAREC
	JRST ACTT02

ACTT2:	;EXIT AC,
	TLNE T2,REC.WR
	JRST ACTT02
	POPJ P,

ACTT3:	;START,REE,DDT
	SKIPE T3,JBTFPN(J)
	CAMN T3,JBTPPN(J)
	POPJ P,
	TLNN T2,REC.WR
	JRST ACTT01	;GO SET REC.WR,CLEAR LSWRITE RECORD
	POPJ P,

ACTT13:	;LOCAL STR ACCT UUO
	TLNE T2,LSAREC		;IF ON, ALREADY DID THIS. JUST EXIT
	POPJ P,
	TLO T2,REC.WR!LSAREC	;SET BOTH SO LSAREC PREVENTS
		;ANOTHER ONE OF THESE, AND REC.WR CAUSES
		;TERMINATING RECORD TO BE WRITTEN
	JRST ACTT02	;GO WRITE

ACTT16:	;RUN LICENSE, FOLLOWS RUN RECORD IMMEDIATELY
	SKIPE JBTLIC(J)	;IF LICENSE GOT SET,
	PJRST WRTACR	;WRITE RECORD & RETURN
	POPJ P,		;ELSE JUST RETURN

ACTT22: ;CREAUX
        MOVE    T4,AUXEND       ;COMPUTE RECORD
        SUBI    T4,AUXSTR-AUXMIN-1      ; SIZE
        PJRST   WRTACR          ;DO THE WRITE
COMMENT ! @@SUBROUTINE ACTUGF
@@PURPOSE
PERFORM UGETF FUNCTION FOR ACT DEVICE. GIVEN A RECORD
TYPE IT RETURNS THE SIZE OF THE RECORD
@@ENTRY
(M)=LOCATION OF DATA AND TO RETURN DATA
@@FUNCTION
FOR NOW IF TYPE IS 0 TO 6 RETURNS 7 OTHERWISE 0
@@EXIT
HAS ALREADY STORED IN (M)
@@RESTRICTIONS
CAN BE CALLED ONLY AT UUO LEVEL.@@

ACTUGF:	UMOVE T1,(M)
	CAIG T1,6
	CAIGE T1,0
	TDZA T1,T1	;RETURN 0 IF NOT IN RANGE 0-6
	MOVEI T1,7	;ELSE 7. WHEN MORE TYPES, WILL BE HARDER
	UMOVEM T1,(M)
	POPJ P,
!;-NEW UGETF CODE PICKS UP REC SIZE OUT OF TABLE ACRTAB:

ACTUGF:	UMOVE T2,(M)	;TYPE TO FIND
	MOVSI T3,-ACRTL
	LDB T1,ACYTYP
	CAMN T1,T2
	JRST .+4
	AOBJN T3,.-3
	MOVEI	T1,0	;RETURN 0 IF NOT IN TABLE
	JRST	ACTUG1
	LDB T1,ACYSIZ
        LDB     T2,ACYMIN       ;RETURN MIN SIZE IN LHS
        HRL     T1,T2           ; IF REC IS VAR LENGTH
ACTUG1: UMOVEM  T1,(M)
	POPJ P,
COMMENT %	SUBROUTINES GETABS (FOR UUO'S) & MGETABS
PURPOSE: CALLED TO GET ACT BUFFER SPACE (HENCE "GETABS")
 & ACT RESOURCE. IF MGETABS/GETABS SUCCESS-RETURNS (SKIP),
 THE CALLER MAY WRITE HIS RECORD;HE HAS BEEN GIVEN THE
 ACT RESOURCE.  HE MUST RELEASE IT WHEN DONE (CALL RELACB).
  GETABS STARTS UP THE OWNER OF THE ACT DEVICE AS MANY TIMES 
AS NEEDED TO GET C(P1) WORDS OF RECORD SPACE IN THE BUFFER.
 THE ACT-DEVICE OWNER IS NEVER PUT INTO WAIT FOR HIMSELF
 TO EMPTY THE BUFFER.

IF GETABS/MGETABS FRETURNS (NO SKIP) THEN THE AC RESOURCE
HAS NOT BEEN INTERLOCKED. A UUO MAY FRETURN TO HIS CALLER. THE 
MONITOR SHOULD PROCEED DOING NOTHING TO ACT.

VARIOUS STATES OF THE WORLD EXIST WHICH AFFECT THE BEHAVIOUR
OF THESE ROUTINES. SEE COMMENTARY FOLLOWING CODE FOR STATE
DIAGRAM & DISCUSSION.
;-ENTRY
	J/ WRITER'S JOB
	P1/ SIZE OF HIS RECORD
;-RETURNS:  J,P1-4 PRESERVED.  RH(F)=ACTDDB,
		T1,T2,T3,T4 CLOBBERED
	NON-SKIP - NO SPACE AVAILABLE & NO ACT OWNER
		(OR STREAM DEAD & CALLER IS ACTOWNER). CALLER SHOULD
		NOT TRY TO WRITE ACT. PROBABLY BETTER TO FRETURN
		TO HIS CALLER.
	SKIP - OK TO WRITE INTO ACT BUFFER. CALLER NOW
		OWNS AC RESOURCE.
;-TABLES
	ACTBUF,ACTDDB,JBTPIP,JBTAWQ,JBTENB
;-CALLS
	CTMBS,TAKTRJ,SETIOD
;-RESTRICTIONS
	CALL GETABS ONLY FROM UUO LIKE PUTSAR WHICH CAN BE ABORTED
	CALL MGETABS ONLY FROM WRTACR.  NO ABORT POSSIBLE
  %

MGETABS: SKIPA F,[1B0+ACTDDB]	;SIGN BIT FLAGS MONITOR CALL
GETABS:: MOVEI F,ACTDDB		;UUO CALL
	PUSH P,J	;SAVE J FROM TAKTRJ
	PUSH P,P1	;SAVED RECORD SIZE
	MOVSI T1,ACTDED
	MOVE T2,JBTAUN(J)
	LDB T4,PJOBN
	XOR T2,JBTAUN(T4)	;T2_0 IF AUN OWNER=AUN CALLER
	TDNE T1,DEVSTS(F)
	JUMPE T2,XXFRET	;COMMENTARY PT 1
;INTERLOCK
	AOSE ACREQ
	PUSHJ P,ACWAIT	;POSSIBLE WAIT. COMMENTARY PT 2
	SETZM ACSWT	;CLEAN UP SWAPPER AFTER RESCHEDULE
	MOVEI T1,RELACB	;"RELACB" ESTABLISHED AS
	MOVEM T1,JBTABT(J)	;PANIC ABORT ROUTINE
;RETURN HERE FROM MLOWQ WAIT BELOW
MLLOOP:	PUSHJ P,CTMBS	;T1_TOT AVAIL, T2,T3 CLOB
	LDB T4,PJOBN
	MOVE P1,T1
	SUB P1,(P)	;P1_TOT AVAIL -REQUEST = WHAT'S LEFT
	JUMPE T4,[ JUMPGE P1,XXSRET	;COMMENTARY PT 4
		   JRST RAFRET]	;REL ACT & FAIL
;ACT OWNER EXISTS
	CAIL P1,ACBLIM	;COMMENTARY PT 5
	JRST GETAB2	;NO NEED TO NOTIFY
	MOVEI T1,IOACT
	ANDCAB T1,ACTDDB+DEVIOS
	TLNE T1,IOW
	PUSHJ P,SETIOD	;ACT OWNER OUT OF IO WAIT IF IN IOWAIT
	LDB T1,NINTRP	;TRAP NO OF "INPUT AVAIL" TRAP
	PUSHJ P,TAKTRJ	;COMMENTARY PT 5A. CLOB T1,J
GETAB2:
	MOVE J,JOB
	JUMPGE P1,XXSRET	;COMMENTARY PT 6
;NOT ENUF SPACE
	JUMPGE F,[	SKIPL JBTAWQ(J)	;POINT 7
			JRST GETAB3
			PUSHJ P,RELACB ;GIVE BACK AC BEFORE ABORTING.
			JRST ABTUUO]
	LDB T4,PJOBN
	CAMN T4,JOB
	JRST RAFRET	;POINT 8. REL ACT & FAIL
;MLOWQ WAIT. NOT ENUF SPACE. ACTXFR WAKES US UP & WE TRY AGAIN.
GETAB3:
	MOVEM J,ACJOB
	MOVEI T1,MLOWQ
	DPB T1,PJBSTS
	PUSHJ P,WSCHED	;POINT 9
	JRST MLLOOP	;TRY AGAIN

XXSRET:	AOSA -2(P)
RAFRET:	PUSHJ P,RELACB	;DE-INTERLOCK & FAIL RETURN
XXFRET:	POP P,P1
	POP P,J
	POPJ P,

COMMENT ! COMMENTARY ON ABOVE:

THIS ROUTINE RESPONDS TO THE FOLLOWING STATE VARIABLES:
EVENT..1)UUO CALL, OR 2)MONITOR CALL
STATE OF STREAM..
	1) ACTOWNER EXISTS & ACTDED=0 (STREAM OK)
	   BY CONVENTION WE NEVER ALLOW ACTDED=1 IF
	   NO ACTOWNER. WHEN NO ACTOWNER, SYSTEM IS UP FOR
	   FREE, & ACTDED MUST BE KEPT 0.
	2) ACTDED=1 STREAM DEAD
	3) ACT NOT OWNED (SYSTEM BEING RUN FOR FREE OR CHKPNT
	   IS SLOW IN INITTING ACT)
	   ***NOTE THAT CHKPNT JOB HAS ACT ASSIGNED AT
		ALL TIMES EVEN IF IT DIES OR EXITS OR
		DOES RELEASE ACTDV. ONLY A DEASSIGN ACT
		OR LOGOUT WILL MAKE SYSTEM RUN FOR FREE.***
STATE OF ACT RESOURCE..1)AVAILABLE 2)INTERLOCKED
SPACE IN BUFFER..1)ENUF FOR THIS RECORD  2)INSUFFICIENT
AUN OF CALLER	1)SAME AS ACT DEV OWNER, 2)DIFFERENT
JOB NO. OF CALLER (ONLY TESTED WHEN WRTACR CALLS)
		1) SAME AS ACT DEV OWNER
		2)DIFFERENT

EVENT,STREAM,RESOURCE,SPACE,CALLER

(STREAM OK)
X111X - SRETURN
X112X - ML WAIT THEN RETEST STATE
X122X- AC WAIT, THEN TEST STATE

(STREAM DEAD)
X2XX1 - FRETURN. THIS IS THE CASE WHERE USER WHOSE AUN MATCHES
	THAT OF THE (DECEASED) CHKPNT IS TAKING SOME ACTION--UUO
	OR OTHERWISE--WHICH IS TRYING TO WRITE A RECORD.
	SINCE ACTDED IS *ALREADY* ON, WE FRETURN SO HE CAN
	DO ANYTHING TO GET CHKPNT BACK ALIVE.  NOTE THAT IF
	ACTDED GOES ON *AFTER* SUCH A USER HAS ENTERED
	AC OR ML WAIT, THE USER CONTINUES TO HANG.  ONLY SUCH
	USERS WHO COME IN AFTER ACTDED GOES ON GO FOR FREE.

12XX2 - AC AND/OR ML WAIT, THEN TEST/RETEST STATE. NOTE THAT
	EVEN IF ACTDED IS ALREADY ON, A UUO FROM NON-ACTOWNER
	AUN GOES INTO AC/ML WAIT IN GETABS. GETABS DOES NOT
	FRETURN, BUT CONTINUES TO WAIT, EVEN IF AWAKENED.
	THIS ROUTINE WILL FRETURN FOR SUCH A UUO ONLY
	IF ACT IS DEASSIGNED (SYSTEM RUNNING FOR FREE) &
	NO SPACE EXISTS TO WRITE.  IT CAN BE AWAKENED BY
	ACT OWNER COMING ALIVE & DOING RELEASE ACTDEVICE,
	(OR INPUT FROM ACTDEVICE, IN WHICH CASE THIS ROUTINE
	WILL EVENTUALLY SRETURN WITH SPACE).
22XX2 - ML OR AC WAIT, THEN (RE)TEST STATE.  SOMEBODY
	BESIDES ACT OWNER HAS DONE SOMETHING CAUSING MONITOR
	TO TRY TO WRITE A RECORD. ML WAIT WILL AWAKE IF
	ACTXFR OR ACTREL RUNS. AC WAITERS AWAKE WHEN ML WAITER
	DOES RELACB. WHEN STATE IS TESTED OR RETESTED, WE 
	CONTINUE TO WAIT IF STREAM STILL DEAD. IF ACT 
	DEASSIGNED,SEE X3XXX STATES BELOW. IF CHKPNT RUNS AGAIN
	SUCCESSFULLY, IT RESETS ACTDED IN WHICH CASE SEE X1XXX.

(NO OWNER..NO SYSTEM ACCT'G..RUNNING FOR FREE)

X311X - SRETURN. WE WRITE IN TO BUFF AS LONG AS THERE'S SPACE
X312X - FRETURN. SPACE RAN OUT, FORGET ALL SUBSEQUENT RECORDS.
	(ASSUMES CALLER UUO RETURNS SPECIAL "NO ACCTG"
	ERROR CODE TO USER, OR WRTACR EXITS DOING NOTHING).
X32XX - AC WAIT THEN TEST STATE.

POINT 1
FRETURN W/O RESOURCE IF ACTDED=1 & CALLER AUN=OWNER AUN.
NO TEST HERE FOR ACTOWNER IN T4 = 0 SINCE ASSUMPTION
THAT ACTOWNER.NE.0 IF ACTDED=1

POINT 2
IF NO ACT OWNER, OK TO GO INTO ACWAIT BECAUSE
1)NO ONE EVER GOES INTO ML WAIT IF NO ACT OWNER
2)ANY OTHER AC OWNER IS IN PAGE WAIT,ETC & IS SURE TO
WAKE US UP WHEN HE RELEASES RESOURCE AT RELACB

POINT 2A
ITS OK TO PUT ACT DEV OWNER INTO ACWAIT.  IF ACT OWNER
DOES SOMETHING REQUIRING A RECORD YET AC RESOURCE NOT AVAIL,
THEN SOMEONE ELSE OWNS IT. THIS SOMEONE HAS RESCHEDULED
SINCE ACT OWNER IS RUNNING. THE SOMEONE RESCHEDULED
FOR EITHER 1)PAGE FAULT,PARITY ERROR ETC. ,OR 2)ML WAIT,
FOR ACT DEV OWNER. JBTABT HANDLES ILL MEM REF,PARITY
ETC. BY RELEASING AC RESOURCE..IN CWHICH CASE WE WILL
BE AWAKENED. IF PAGE WAIT SATISFIES, THE SOMEONE WILL AWAKEN
US AS HE RELEASES AC RESOURCE.  CASE 2, ML
WAIT, IS IMPOSSIBLE BECAUSE IF THE SOMEONE WAS IN ML WAIT
IT WOULD BE DUE TO LACK OF SPACE, WHICH CAUSES ACTOWNER
TO GET TRAP AT PI LEVEL FOR INPUT AVAILABLE. SO ACTOWNER
WOULD NOW BE RUNNING AT THAT PI LEVEL (WHICH DOES NOT *EVER*
PUT ANYTHING INTO STREAM--KEY ASSUMPTION) WHEREAS
WE KNOW THAT HE IS NOW RUNNING AT SOME LOWER LEVEL PI
PUTTING SOMETHING INTO STREAM.
 BEWARE--ACTOWNER MUST NEVER PUT ANYTHING INTO STREAM
	AT THE INPUT AVAILABLE PI LEVEL..EVEN DUE TO EXECUTING
	UUO'S WHICH IN THE FUTURE MAY BE CHANGED TO WRITING
	STREAM RECORDS THRU WRTACR.

POINT 3 NONEXISTENT

POINT 4
THERE IS NO ACTOWNER. WHILE SPACE EXISTS, SRETURN WITH SPACE &
RESOURCE. WHEN IT RUNS OUT, FRETURN W/O RESOURCE.
CALLER SHOULD DODGE THIS. SEE STATE X312X.
POINT 5
ACBLIM IS ALWAYS GREATER THAN ANY REQUESTED RECORD SIZE.
POINT 5A
HERE WE IGNORE POSSIBLITY THAT ACTOWNER ISNT ARMED FOR THE
INPUT AVAIL INTERRUPT. IT IS OK TO INGORE & GO AHEAD
PUTTING OURSELVES INTO ML WAIT ANYWAY BECAUSE:
1)IF ACTOWNER IS RUNNING HE WILL SOON ARM THE INT, GET THE
TRAP, DO INPUT, AWAKENING ANY ML WAITER.
2)IF ACTOWNER IS DYING OR DEAD, AN OPERATOR WILL EITHER
FIRE HIM UP AGAIN (WAKING UP ML WAITER) OR DEASSIGN ACT,
WHICH WAKES UP ANY ML WAITER, AS ACTREL IS EXECUTING.

POINT 6
P1>0 MEANS MORE THAN ENOUGH SPACE. P1=0 MEANS EXACTLY ENOUGH.

POINT 7
THE JUMPGE WILL NOT JUMP IF WRTACR CALLED. ON UUO 
CALL, WE ABORT THE UUO IF ANY HIGHER PI IS PENDING THAN
THE ONE ACTIVE WHILE THIS UUO WAS DONE; THIS CATCHES THE
CASE WHERE ACTOWNER WRITES TO THE STREAM VIA PUTSAR ON
PI LEVEL 3 & RUNS OUT OF SPACE; ALSO INCLUDES CASES
WHERE OTHER JOBS WITH PI GET ACTIVATED AT A HIGHER LEVEL
BY AN INTERRUPT FROM SCNSER ETC. WHILE THE UUO IS RUNNING.
MIGHT AS WELL LET HIM HAVE IT RAHTER THAN MAKE HIM WAIT
NOW. HE'LL COME BACK AGAIN WHEN HE RE-EXECUTES HIS UUO.
 ***GETABS MUST NOT BE USED IF UUO MAY NOT BE ABORTED
   AT THIS POINT***
IF NO PI IS PENDING, WE DO THE MLOWQ WAIT. WE ARE GUARANTEED
TO COME OUT SINCE WE ARE NOT ACTOWNER BECAUSE ACTOWNER HAS
HIGHER PI NOW PENDING SINCE THERE IS NOT ENOUGH SPACE
AND ACBLIM > ALL RECORD SIZES.  THIS PREVENTS ACTOWNER FROM GOING
INTO ML WAIT FOR HIMSELF.

POINT 8
WRTACR CALLED MGETABS. BEFORE DOING ML WAIT,
CHECK IF WRTACR IS RUNNING BECAUSE MONITOR IS TRYING
TO WRITE A RECORD FOR THE ACTOWNER DUE TO SOME ACTION
OF HIS BESIDES AN EXPLICIT UUO CALL TO
WRITE THE STREAM.  SINCE WE HAVEN'T GOT ANY SPACE,
AND WE CAN'T ABORT OUT OF THE MIDDLE OF THIS UNKNOWN
SYSTEM CALL WHICH CALLED WRTACR, WE HAVE TO MISS A
RECORD.  MIGHT BE INTERESTING SOMETIME TO PUT A COUNTER
IN TO SEE IF THIS EVER HAPPENS.

POINT 9
WAKEUP FOR THIS IS BY ONE OF:
1)ACTXFR RUNS DURING INPUT UUO FROM ACTDEV
2)ACTREL RUNS
	A)ACTOWNER DID RELEASE,EXIT ETC.
	B)OPERATOR DID DEASSIGN ACT OR LOGGED JOB OUT.
!

COMMENT /@@SUBROUTINE WRTACR
@@PURPOSE
TO GENERATE AN ACCOUNTING RECORD OF A SPECIFIED TYPE AND PUT
IT INTO ACTBUF.
@@ENTRY
(T1)=RECORD TYPE 0-7		;-SPECIAL ACTION ON 7
(T4)=RECORD SIZE FOR VARIABLE LENGTH RECORDS
@@ACCUM
T1
@@TABLES
ACTBUF,JBTNAM,JBTFPN,JBTSOK,ACTDDB,JBTAUN
@@GLOBAL
ACREQ,ACAVAL,JFYSEC,TIME,THSDAT
@@FUNCTION
RETURN IMMEDIATELY IF CALLERS AUN=0 (LOGINN NOT YET RUN, SO
ELIMINATE TYPE 0 REC FOR INITIAL LOGINN RUN FOR JOB).
GET ACT BUFFER SPACE. IF CANT, GIVE UP.
WRITE THE RECORD. (TYPES 0-6 DONE HERE. OTHERS HAVE
THEIR OWN ROUTINES WHICH DISPATCH FROM WRTAC1+2 AND
RETURN BACK AT FINBUF.
@@EXIT
1. RECORD IN BUFFER IF POSSIBLE
2. BUFFER POINTERS ADVANCED
3. AC RESOURCE RELEASED.
4. ALL AC'S PRESERVED
@@CALLS
MGETABS,CBLAMB,MORBUF,PUTBUF,CLKTRU
@@RESTRICTIONS
TO BE CALLED ONLY AT UUO LEVEL DUE TO POSSIBLE WAIT
@@/

WRTACR::PUSH P,P1	;SAVE P1 FOR CALL TO GETABS
	PUSH P,T2	;SAVE FOR CALLER BOTH T2 & F
	PUSH P,F
	SKIPN JBTAUN(J)	;IF AUN NOT YET SET,
	JRST ACFULL	;DONT WRITE RECORD
	MOVE T3,T1	;GET TYPE CODE IN T3 FOR ACYSIZ
	PUSH P,T3	;SAVE IT FOR LATER
	CAIL	T3,40	;IF ITS A USER TYPE,
	ADDI	T3,ACUTAB-40 	;PICK UP DATA FROM PROPER PLACE
	LDB P1,ACYSIZ	;GET SIZE OF THIS TYPE IN P1 FOR GETABS
        LDB     T2,ACYMIN       ;IS THIS A VAR LENGTH REC?
        JUMPN   T2,    [CAML    T4,T2       ;YES: IS SIZE SPEC TOO SMALL?
                        CAMLE   T4,P1   ;NO: IS SIZE > MAX?
                        STOPCD          ;YES: NOT ALLOWED
                        MOVE    P1,T4   ;PUT SIZE IN P1 FOR GETABS
                        JRST    .+1]
	PUSHJ P,MGETABS	;GET THAT MUCH SPACE IN ACT BUFFER
	JRST [POP P,T3	;DO NOTHING. LOSE RECORD. CANT GET SPACE
		JRST ACFULL]	 ;BECAUSE NO ACTOWNER OR WE ARE THE
			 ;ACTOWNER & BUFFER FULL & WE CANT
			 ;PUT OURSELF INTO WAIT,OR ABORT.
; SUCCESS RETURN MGETABS
WRTAC1:
	PUSHJ P,CBLAMB	;GET LOC & SIZ OF CONTIGUOUS BLOCK IN ACTBUF
 ;-FOR TYP 0-6, EXECUTE SMALL ROUTINE TO WRITE THE  HEADER
;-	WORDS STANDARD WITH ALL STREAM RECORDS. FOR TYPE 7,ETC.
;-	DISPATCH TO SPECIAL CODE.


	POP P,T3	;GET TYPE CODE BACK INTO T3
	MOVE T4,P1	;GET SIZE IN T4 FOR ROUTINES
	CAIL T3,40	;ONE OF THE BIGGIES?
	JRST @SPCACT-40(T3) ;YES, USE SPECIAL TABLE.
	XCT WRTACT(T3)	;DISPATCH ON TYPE

;-RETURN HERE TO COMPLETE LAST 3 WDS OF TYPE 0-6. T1,T2
;-	ARE AS RETURNED BY LAST PUTBUF CALL.

	MOVE T3,JBTNAM(J)
	PUSHJ P,PUTBUF
	MOVE T3,JBTFPN(J)	
	PUSHJ P,PUTBUF
	MOVE T3,JBTSOK(J)
	PUSHJ P,PUTBUF
; FINBUF..JRST HERE FROM SPECIAL ROUTINES FOR TYPES 7,12,ETC.
FINBUF:	PUSHJ P,FINAC1	;UN-INTERLOCK, UPDATE BUFFER POINTERS, NON-SKIP EXIT
ACFULL:	POP P,F
	POP P,T2
	POP P,P1	;RESTORE P1
	POPJ P,

;-FINACB..FINISH UP USE OF ACTBUF.  FOR USE BY UUO'S
;-	WHICH WRITE ACTBUF WITHOUT USING ACTWRT.
;-	(PUTSAR,XCHARG,,)
;-UPDATE BUFF PTRS, UN-INTERLOCK, SKIP EXIT.
;-ENTRY:  T2=AS RETURNED BY LAST PUTBUF=NEXT LOC TO USE
;	  F =ACTDDB   J=JOB

;-CAN BE USED TO CAUSE A SKIP RTN FROM THE UUO IF
;-	PJRST FINACB IS LAST INSTR IN UUO.

;-FINAC1..USED BY WRTACR AT FINBUF. NO SKIP

;-RELACB..TO RELEASE ACT RESOURCE. ENTRY: J=JOB


INTERNAL FINACB,FINAC1,RELACB
FINACB:	AOS (P)		;FINACB GIVES SKIP RETURN
FINAC1:	HRRZS T2	;AAA. ELIM ANY GARB IN LH FOR CAML
	CAML T2,ACTBUF-1	;TEST WRAPAROUND
	MOVEI T2,ACTBUF
	HRLM T2,ACTBUF-2	;RECORD NEXT POS. TO USE
;
;RELACB..FALL THRU FROM ABOVE. ALSO JBTABT(J) SET TO POINT
;  HERE BY GETABS BEFORE ACT OPERATION BEGUN IN CASE ABORT
;  NECESSARY DUE TO PAGE FAIL,ETC.

RELACB:	SOSL ACREQ
	SETOM ACAVAL
	SETZM JBTABT(J)	;ABORT ROUTINE CLEAR. ACT OPERATION SUCCEEDED
	POPJ P,


;-DISPATCH TABLE FOR WRTACR, TYPE IN T3. XCT WRTACT(T3)

WRTACT:	PUSHJ P,WACRHW	;TYPES 0-6, JUST WRITE
	PUSHJ P,WACRHW	;USUAL HEADER & CONTINUE
	PUSHJ P,WACRHW	;INLINE IN WRTACR CODE
	PUSHJ P,WACRHW
	PUSHJ P,WACRHW
	PUSHJ P,WACRHW
	PUSHJ P,WACRHW
	JRST W7ACR	;7--SPECIAL
	STOPCD		;10-XCHARG NOT DONE THIS WAY
	STOPCD		;11-SYSTEM UP..NEVER WRITTEN
	JRST W12ACR	;12-DATE CHANGE
	PUSHJ P,WACRHW	;13-LOCAL STR ACCTG. WRITE USUAL HEADER
	JRST W14ACR	;14-SETLIC UUO
	JRST W15ACR	;15-SETE UUO
	JRST W16ACR	;16-RUN LICENSE
	STOPCD		;17-TRANSACTION CHG, NOT DONE THIS WAY
	STOPCD		;20-ROYALTY BEGIN/END, NOT DONE THIS WAY
	STOPCD		;21-TRU COMPONENT CHECKPNT, NOT DONE THIS WAY
	JRST	W22ACR	;22-AUX CIR CREATION

;TABLE TO WRITE "SPECIAL" (40 OR MORE) RECORDS

SPCACT:	JRST	W40ACR	;WRITE TYPE 40 (LOGIN FOR FRMOP)
	STOPCD		;(41) CHKPNT, DON'T DO IT FOR NOW
	STOPCD		;(42) LOGOUT, DON'T DO IT YET.
	STOPCD		;(43) CHKPNT UPTIME, DON'T DO IT YET.
	STOPCD		;(44) TATTLE, NOT TO BE WRITTEN LIKE THIS
;- SUBROUTINE WACRHW - WRITE ACT 5 HEADER WORDS: HEADER,DATE,
;-	AUN, & TRU'S (DP QTY). COMMON TO ALL MONITOR-GENERATED STREAM RECORDS.
;-	THE MONITOR DATE-STAMPS ALL STREAM RECORDS. EVEN PUTSAR ONES.

;-ENTRY
;	T2=LOC OF CONTIG. BLOCK IN ACTBUF FOR START RECORD
;	T1=LENGTH OF BLOCK
;	T3=RECORD TYPE
;	T4=REC SIZE
;-RETURNS
;	T3,T4 DESTROYED. T1,T2 UPDATED. 5 WORDS WRITTEN.

;-CALLS PUTBUF,MORBUF
INTERNAL WACRHW
WACRHW:
	DPB T3,ACYCOD		;INSERT CODE
	DPB J,ACYJOB		;AND JOB NUMBER
	HRRM T4,(T2)		;AND RECORD SIZE
	PUSHJ P,MORBUF		;ADVANCE BUFFER
	PUSHJ	P,GETDT		;GET T3/ DATE,,TIME
	PUSHJ P,PUTBUF		;AND PUT IN BUFFER
	MOVE T3,JBTAUN(J)		;GET AUN
	PUSHJ P,PUTBUF
	PUSH P,T1
	PUSH P,T2
	PUSHJ P,CLKTRU		;RTN TRU'S T1=LOW,T2=HI ORDER DP. VALUE
	MOVE T3,T2	;T3:=HI ORDER
	MOVE T4,T1	;T4:=LOW ORDER
	POP P,T2
	POP P,T1
	PUSHJ P,PUTBUF	;STORE T3 HI ORDER IN BUFF
	MOVEM T4,(T2)	;& LOW ORDER
	PJRST MORBUF	;& ADVANCE PTR & RETURN


; RETURNS T3/ DATE,,TIME    - CLOBBERS T4

GETDT:	MOVE	T4,THSDAT
	MOVE	T3,TIME
	CAME	T4,THSDAT
	 JRST	GETDT
	PUSH	P,T4
	IDIV	T3,JFYSEC
	POP	P,T4
	HRL	T3,T4
	POPJ	P,
INTERNAL PUTBUF,MORBUF	;-CALLED FROM UUOCON XCHARG,PUTSAR
; PUT WORD IN MONITOR BUFFER
; ON ENTRY (T2)=CURRENT ADDRESS IN BUFFER
;          (T1)=NO. OF CONTIGUOUS WORDS LEFT IN BUFFER
;          (T3)=WORD TO STORE
;  RETURNS UPDATED T1 AND T2

COMMENT /@@SUBROUTINE PUTBUF (ENTRY MORBUF)
@@PURPOSE
STORE DATA IN MONITOR BUFFER AND ADVANCE POINTER.  IF ENTERED
AT MORBUF ONLY THE POINTERS ARE ADVANCED.
@@ENTRY
(T1)= NO. OF WORDS LEFT IN THIS CONTIGUOUS BLOCK IN THE  BUFFER
(T2)=CURRENT ADDRESS IN BUFFER TO STORE DATA
(T3)=DATA TO BE STORED  (NOT USED BY MORBUF)
@@TABLES
ACTBUF
@@FUNCTION
STORES DATA AND ADVANCES BUFFER POINTERS.  IF BUFFER
HAS WRAPPED AROUND CBLAMB IS CALLED TO SET UP ANOTHER CONTIGUOUS
BLOCK
@@EXIT
(T1)=NO. OF CONTIGUOUS WORDS REMAINING IN CURRENT BLOCK IN BUFFER
(T2)=NEXT ADDRESS IN BUFFER
@@CALLS
CBLAMB
@@/
PUTBUF:	MOVEM T3,(T2)		;STORE DATA
MORBUF:	ADDI T2,1		;ADVANCE BUFFER ADDRESS
	SOJG T1,CPOPJ		;IF ROOM STILL LEFT IN BLOCK
	ANDI T2,777777		;AAA. MAKE SURE LH = 0 FOR CAMN
	CAMN T2,ACTBUF-1	;WRAPPED AROUND?
	MOVEI T2,ACTBUF		;YES
;THE FOLLOWING INSTR. UPDATES CORE COPY OF IN PTR
;ONLY AT WRAP-AROUND TIME. THIS IS FOR BENEFIT OF
;CBLAMB. ASIDE FROM WRAPAROUND-TIME, THE T2/IN PTR IS
;NOT RESTORED TO CORE UNTIL RECORD IS COMPLETE.
	HRLM T2,ACTBUF-2	;SET NEW IN POINTER
	PJRST CBLAMB		;GET NEXT BLOCK
;-DEFINITIONS OF STREAM RECORD TYPES,SIZES. BYTE PTRS, ETC.
EXTERNAL XCHARL,TRNARL,TRNTAR,ROYARL,ROYTAR,TCCTAR,TCCARL
INTERNAL ACRTAB,ACUTAB,ACURTL,ACYSIZ,ACYTYP,ACYMIN

;-MACRO TO DEFINE A RECORD. MAKE ENTRY OF FORMAT:
;  BITS   0-17	LICENSES REQ'D TO WRITE THIS RECORD TYPE
;        18-23	RECORD TYPE OR NUMBER
;        24-29  MIN SIZE IF VAR LEN REC ELSE ZERO
;      	 30-35	SIZE OF THIS REC INCLUDING HEADER WORD; MAX SIZE IF VAR LEN REC

DEFINE ATB(LICENS,NUMB,SIZ,MINSIZ)<
    XWD LICENS,<<NUMB>B23+<MINSIZ>B29+<SIZ>> >


STDHED==5       ;SIZE OF STANDARD HEADER

;-RECORD LENGTHS: 
LGIRL==7	;LOGINN. MUST AGREE WITH LOGINN 'GTAB' TABLE ETC.
LGORL==41	;LOGOUT. DITTO MUST AGREE WITH "STMLEN"
CKPRL==41	;CHECKPOINT. DITTO MUST AGREE WITH "CKPLNG"
UPTRL==3	;UPTIME
SYURL==2	;SYSTEM UP RECORD. ONCE ONLY WHEN SYS COMES UP
DATRL==6	;DATE CHG RECORD
TATMIN==STDHED  ;TATTLETALE RECORD MIN SIZE
TATMAX==STDHED+20       ;TATTLETALE RECORD MAX SIZE
AUXMIN==STDHED+1        ;CREAUX RECORD MIN SIZE
AUXMAX==<STDHED+1>+<AUXSTM+6>/5  ;CREAUX RECORD MAX SIZE


;-ACT RECORD TABLE.  TYPES 0-7 WRITTEN BY MONITOR ONLY. NO LIC
; (TYPE 11 NEVER WRITTEN EVEN BY MONITOR.)

ACRTAB:	ATB 0,0,10,0
	ATB 0,1,10,0
	ATB 0,2,10,0
	ATB 0,3,10,0
	ATB 0,4,10,0
	ATB 0,5,10,0
	ATB 0,6,10,0
	ATB 0,7,11,0
	ATB LICXC,10,XCHARL,0	;XCHARG
	ATB 0,11,2,0	;NEVER WRITTEN. IN BUFFER AT INIT'ZN
	ATB LICOPR!LICJAL,12,DATRL,0
	ATB 0,13,10,0	;LOCAL STR ACCTG TYPE 13 REC
	ATB 0,14,10,0	;SETLIC UUO TYPE 14
	ATB 0,15,10,0	;SETE UUO TYPE 15
	ATB 0,16,3,0	;RUN LICENSE TYPE 16
	ATB LICXC,TRNTAR,TRNARL,0	;TRANSACTION CG
	ATB 0,ROYTAR,ROYARL,0		;ROYALTY BEGIN/END RECORD
	ATB LICXC,TCCTAR,TCCARL,0	;TRU COMPONENT CHECKPOINT
	ATB 0,22,AUXMAX,AUXMIN		;CREAUX
;USER TYPES
ACUTAB==.-ACRTAB	;-USED BY PUTSAR
	ATB LICJAL,40,LGIRL,0	;LOGIN TYPE 40
	ATB LICJAL,41,CKPRL,0	;CHKPNT TYPE 41
	ATB LICJAL,42,LGORL,0	;LOGOUT 42
	ATB LICJAL,43,UPTRL,0	;UPTIME REC (CHKPNT)
	ATB LICJAL,44,TATMAX,TATMIN	;TATTLETALE REC
ACURTL==.-ACUTAB-ACRTAB	;INDEX FOR SEARCH BY PUTSAR UUO
ACRTL==.-ACRTAB	;INDEX FOR ACTUGF

;-BYTE PTRS
ACYCOD: POINT 9,(T2),8	;TO DPB REC TYPE
ACYJOB: POINT 9,(T2),17		;TO DPB JOB, INTO HEADER WORD
ACYTYP: POINT 6,ACRTAB(T3),23	;TO MATCH TYPE
ACYMIN: POINT 6,ACRTAB(T3),29   ;TO GET MIN REC SIZE FOR VAR LEN RECS
ACYSIZ: POINT 6,ACRTAB(T3),35	;TO GET REC SIZE

;-MISC
ACJOB:	EXP 0	;SET TO JOB NO. BY GETABS WHEN JOB OWNS RESOURCE
		;& IS IN MLOWQ WAITING FOR CHKPNT TO EMPTY
		;BUFF TO MAKE ROOM FOR RECORD. RESET BY WAKACJ.
ACBLIM==^D350	;LIMIT BELOW WHICH WE TRAP ACT JOB TO EMPTY BUFF
EXTERNAL UPTPJC,UPTPJ1,UPTPJ2,JBTCNK,%UPT	;-

;-W7ACR..SPECIAL FOR TYPE 7 (PJC) RECORD

;-ENTRY FROM XCT WRTACT(T3) IN WRTACR
;-RETURN TO WRTACR AT FINBUF

W7ACR:	PUSHJ P,WACRHW	;REGS T1-4 SET ALREADY, WRITE HEADER
	MOVE T3,%UPT+UPTPJC	;FILL OUT WITH
	PUSHJ P,PUTBUF		;3 WDS OF PROJECT CODE
	MOVE T3,%UPT+UPTPJ1
	PUSHJ P,PUTBUF
	MOVE T3,%UPT+UPTPJ2
	PUSHJ P,PUTBUF
	MOVE T3,JBTCNK(J)	;CONNECT SECONDS
	PUSHJ P,PUTBUF
	JRST FINBUF	;RETURN T1,T2 TO MIDST OF WRTACR CODE
			;TO WRAP UP.

; W12ACR..SPECIAL FOR TYPE 12,DATE CHANGE, RECORD

W12ACR:	PUSHJ P,WACRHW	;USUAL HEADER
	HRL T3,NEWDAT
	HRR T3,NEWTIM
	PUSHJ P,PUTBUF
	JRST FINBUF

;(FOR NOW..LATERS THESE MUST BE EXTERNAL FROM COMCON)
NEWDAT: EXP 25252
NEWTIM: EXP 27272

W14ACR:	;SETLIC UUO. SAME RECORD, DIFFERENT TYPE NO. AS SETE
W15ACR:	;SETE UUO.
	PUSHJ P,WACRHW	;WRITE STANDARD HEADER WORDS
	MOVE T3,JBTNAM(J)
	PUSHJ P,PUTBUF
	MOVE T3,JBTFPN(J)
	PUSHJ P,PUTBUF
	MOVE T3,JBTLIC(J)
	PUSHJ P,PUTBUF	;PUT THE NEWLY-SET LICENSE INTO STREAM
	JRST FINBUF

W16ACR:	;RUN LICENSE. FOLLOWS IMMEDIATELY AFTER RUN TYPE 0 IN STREAM
	;THIS TINY RECORD IS AN ADDENDUM FOR RUN/RUNSEGS WHICH
	;SET LICENSE FROM FILE OR FROM JOB LICENSE. JUST 3 WORDS:
	;HEADER/ TYPE=16,JOB,LENGTH=3
	;DATA 1/ DATE,,TIME
	;DATA 2/ JBTLIC
	DPB T3,ACYCOD
	DPB J,ACYJOB
	HRRM T4,(T2)
	PUSHJ P,MORBUF
	PUSHJ P,GETDT		;GET T3/ DATE,,TIME
	PUSHJ P,PUTBUF
	MOVE T3,JBTLIC(J)
	PUSHJ P,PUTBUF
	JRST FINBUF
;WRITE CREAUX RECORD
;
EXTERN  AUXERR,AUXRCP,LDPLNO,AUXEND,AUXSTR,AUXSTM
W22ACR:	PUSHJ	P,WACRHW	;WRITE STANDARD HEADER
	MOVE	T3,AUXERR	;GET ERROR CODE, IF ANY,
	TLNE	T3,-1		; IN LEFT HALF OF T3
	TLOA	T3,(1B0)	; SET BIT 0 IF
	HRLZS	T3		; MONITOR DETECTED ERROR
	SKIPL	T4,AUXRCP	;GET PORT NUMBER INTO
	LDB	T4,LDPLNO	; RIGHT HALF
	HRR	T3,T4		; OF T3
	PUSHJ	P,PUTBUF
        MOVN    T4,AUXEND       ;SET UP AOBJN WORD
        ADDI    T4,AUXSTR-1     ; TO FETCH LOGIN
        HRLS    T4              ; STRING
        HRRI    T4,AUXSTR
        MOVE    T3,(T4)         ;GET A WORD FFROM LOGIN STRING
        PUSHJ   P,PUTBUF        ;PUT IN ACT BUF
        AOBJN   T4,.-2          ;REPEAT IF MORE
	JRST	FINBUF		;FINISH

;HERE TO WRITE LOGIN RECORD FOR FRMOP CREATE FRAME FUNCTION.

EXTERN	UPTLOG

W40ACR:	DPB	T3,ACYCOD	;PUT IN CODE
	DPB	J,ACYJOB	;JOB NUMBER
	HRRM	T4,(T2)		;RECORD SIZE
	PUSHJ	P,MORBUF	;(0)CODE AND JOB,,SIZE
	PUSHJ	P,GETDT		;GET T3/ DATE,,TIME
	PUSHJ	P,PUTBUF	;(1)PUT IN
	MOVE	T3,JBTAUN(J)	;GET AUN OF NEWLY CREATED FRAME
	PUSHJ	P,PUTBUF	;(2)AUN
	MOVE	T3,%UPT+UPTPJC	;USE CALLER'S PROJECT CODE
	PUSHJ	P,PUTBUF	;(3)PROJECT CODE 0
	MOVE	T3,%UPT+UPTPJ1
	PUSHJ	P,PUTBUF	;(4)PROJECT CODE 1
	MOVE	T3,%UPT+UPTPJ2
	PUSHJ	P,PUTBUF	;(5)PROJECT CODE 2
	MOVE	T3,%UPT+UPTLOG	; Use originating job's sup info
	PUSHJ	P,PUTBUF	;(6)SUPERVISOR INFO (32 BITS)
	JRST	FINBUF		;AND FINISH.
	END
    \ ]è