TITLE INITIA - START INITIAL PROGRAMS

if2,<printx License required: OP SY ST JL>

LOC <JOBVER=:137>
EXP  26	; 3/15/89 JMS - Run CRON on TTY5
;    25	; 5/19/87 JMS - Unshut system after 6 minutes so that date/time can
	;               be set by 2020 users without shut override.
;    24	; 5/15/87 JMS - Don't run XEXEC if (XEXEC)INITIA.SYS does not exist.
;    23	; 2/19/87 JMS - Login as PERPOPER on TTY4 to run XEXEC.
;    22	; 9/06/85 JMS - Make a version that runs on all CPUs
;	;Run KMCLOD on 4 if KS, run XEXEC on 3 instead of doing LOGOUT.
;    21	; 8/25/83 DLS - Add OPERATOR messages
;    20	; 8/19/82 Darren - if already logged in, run user's init file
;    17	; 7/30/80 WRS fixed to allow COPYCR to become CRSHID
;    16	; 7/02/80 WRS changed command string for XEXEC
;    15	; 2/02/80 WRS added call to XEXEC at end of job 3
RELOC


P=17	;PDL
N=15	;FOR LOGIN
N1=16	;THIS HAS USER NAME
FL=0		;FLAG REGISTER

T1=1
T2=2
T3=3
T4=4
T5=5
T6=6
T7=7

SYS==1
UFD==2	;FOR CREATING UFD'S
FIL==3	;IN CASE A LOOKUP NEEDED

OPDEF	CHANIO[043B8]
OPDEF	DATUUO[CALLI -55]
OPDEF	ATTACH[CALLI -6]

ARRAY UFDBUF,BUF[200],PDL[40],UFDARG[10]

COM:	IOWD 200,BUF
	0

LUDNAM:	3
	0
	SIXBIT /LUD/
	SIXBIT /SYS/

LUDHSH==^D887	;HAS INTO LUD
LPPN==0		;RELATIVE PPN LOC IN LUD
LLINK==2	;RELATIVE LINK LOCATION IN LUD
LHUN==4		;RELATIVE HASHED USER NAME LOCATION IN LUD
LAUN==0		;AUN FOR USER IN LUD
LBITS==2	;BITS (RUN, NO LOGIN, ETC.)
LCSN==5		;CUSP NAME TO RUN IF PRESENT
LCSU==6		;User name of cusp owner
LCSU1==7	; word 2
LCSPB==400	;CUSP BIT IN BITS WORDS
LTRUB==(1B4)	;USER HAS A TRU RESTRICTION
LFSNAM==5	;FILE STR NAME
LFSBTS==5	;STATUS BITS
LFSQIN==6	;QUOTA IN
LFSQOT==7	;QUOTA OUT
LPRV==3		;JBTPRV WORD
LNOLOG==400000	;CAN NOT LOGIN TO THIS DIRECTOR (BITS WORD)

JP.NAT==(1B1)
JBTSTS==0
JNA==(1B3)
JLOG==4
JBTLIN==-27
JBTPRG==3
SUUMSG==0
SUUSCH==1	;SETUUO SCHED ARGUMENT

LICOP==(1B2)	;OP LICENSE BIT

FLNLG==1	;FLAG TO JUST CREATE A DIRECTORY, NOT LOGIN
FLNGMT==2	;FLAG THAT DATUUO FAILED, TIME NOT GMT
FLNINI==4	;Flag to run user's init file only

JFYSEC==26	;JIFFIES/MIN FROM GETTAB
SUUDAT==4	;SET UUO FOR DATE
SUUTIM==3	;SETUUO FOR TIME
%AXRTY==2	;NUMBER OF RETRYS
;ARGUMENTS FOR EXTENDED LOOKUPS AND ENTERS

EXLLEN==34	;LENGTH OF ARG LIST
EXLPPN==1	;PPN
EXLNAM==2	;NAME
EXLEXT==3	;EXTENSION
EXLSTS==17	;STATUS BITS
   RIPLOG==400000	;LOGGED IN
   RIPDIR==400000	;RH DIRECTORY
EXLQTF==22	;LOGGED IN QUOTA
EXLQTO==23	;LOGGED OUT QUOTA
EXLUSD==25	;USED WORD
EXLUN1==27	;FIRST WORD OF USER NAME
EXLUN2==30	;SECOND WORD OF USER NAME
EXLMXA==24	;MAXIMUM SIZE SO FAR
EXLALP==33	;NUMBER OF PAGES ALLOCATED

ARRAY SRCBUF,LOKARG[EXLLEN]

;INTERLOCK CODES

UCLSTR==7	;CLEAR INTERLOCK
ULKSTR==6	;TEST AND SET INTERLOCK
UFDGIL==^D360	;NUBER OF RETRYS TO SET (6 MIN)

GOBJOB==0	;INDEX FOR JOB NUMBER IN GOBSTR UUO
GOBPPN==1	;INDEX FOR PPN
GOBNAM==2	;INDEX FOR STR NAME

ARRAY CHRBUF[3],RSTBUF[3]

SRCDSL==1	;STRUUO FUCNTION TO SET NEW SEARCH LIST
STRJMX==^D10	;MAX NUMBER OF STRS IN SEARCH LIST
STRWPS==3	;NUMBER OF WORDS PER ENTRY

ARRAY STRFNC[STRJMX*STRWPS+4]
STRJOB==STRFNC+1
STRPPN==STRFNC+2
STRDEL==STRFNC+3
STRNMS==STRFNC+4

UFDALC==4	;RELATIVE WORD OF UFD ENTRY FOR ALLOC

SEGPTR==15	;PLACE IN CONF TABLE FOR SEG PNTR
CNFTBL==11	;GETTAB FOR CONFIG TABLE


RSTFIL==3	;RESTRICTION FILE NUMBER

JFYITM==26
JFYTAB==11		;ITEM AND TABLE FOR JIFFIES/SEC


	DEFINE CMDMAK (A,LINES)
<	DEFINE X (C,D,N,QX1,QX2)
<	<SIXBIT /C/>>
A'TAB:	LINES
A'LEN==.-A'TAB

	DEFINE X(C,D,N,QX1,QX2)
<	D>
A'DSP:	LINES

	DEFINE X (C,D,N,QX1,QX2)
<	[ASCIZ /N/]>
A'NAM:	LINES
>

;THIS MACRO GENERATES COMMAND DISPATCHES

	DEFINE LINES,<
	X 0,DSKCLN,DSKCLN,0,0
	X 1,CHKPNT,CHKPNT,1,CHKPNT
	X 2,COPYCR,COPYCRASH,0,0
	X 3,WAITP,INITIALIZATION,1,INITIA
	X 4,KMCLOD,KMC LOADER,0,0	;Ignored for non KS CPUs
	X 5,CRON,CRON,0,0		;The old XEXEC
>
	CMDMAK (LN0,LINES)
SUBTTL	Start of program

START:	RESET
	MOVEI FL,0
	MOVE P,[IOWD 40,PDL]
	SETOM JOBNUM#		;NUMBER NOT FOUND YET
	MOVEI T1,10
	MOVEM T1,FAILNM		;GETTAB FAILURE
	HRROI T1,JBTSTS
	GETTAB T1,
	 JRST INIFAL
	TLNE T1,JLOG
	  JRST	RUNINI		;Already logged in, run init file
	HRROI T1,JBTLIN
	GETTAB T1,		;GET LINE NAME
	 JRST INIFAL
	HRLZS T1
	MOVSI T2,-LN0LEN
DSPSRC:	CAME T1,LN0TAB(T2)
	JRST DSPSR1
	HRRZM T2,JOBNUM
	SLEEP T2,
	JRST @LN0DSP(T2)
DSPSR1:	AOBJN T2,DSPSRC
	LOGOUT			;NOT FOUND, GIVE UP
SUBTTL	Get date and time from AUX circuit
  REPEAT 0,< ;JMS This code not used in version 15 and later
TIMSET:	MOVSI T7,-NUMSYS	;NUMBER OF SYSTEMS TO TRY
	MOVEI T6,%AXRTY		;RETRY COUUNT
NEWSYS:	MOVE T1,SYSNUM(T7)
	IDIVI T1,^D10
	ADDI T1,"0"
	DPB T1,[POINT 7,AUXSTR+1,6]
	ADDI T2,"0"
	DPB T2,[POINT 7,AUXSTR+1,13]
	MOVEI T1,AUXSTR
	CREAUX T1,
	 JRST NOAUX		;DID NOT GET AN AUX CRCUIT
	MOVEM T1,AUXPRT#
	HRL T2,T1
	HRRI T2,3
	MOVE T1,[POINT 7,[ASCIZ /



GO (SYS)@DATEX
/]]
AXSLP:	ILDB T3,T1
	JUMPE T3,AXSDN		;FINISHED
	CAIE T3,12		;DO NOT SEND LINE FEEDS
	AUXCAL T2,T3
	JRST AXSLP
AXSDN:	MOVE T1,T2
	HRRI T1,1		;SET TO READ
AXRDL:	PUSHJ P,RDCH
	CAIE T3,1
	JRST AXRDL
	PUSHJ P,RDCH
	CAIE T3,2
	JRST AXRDL
	PUSHJ P,RDCH
	CAIE T3,3
	JRST AXRDL
	MOVE T3,[JFYSEC,,JFYTAB]
	GETTAB T3,
	 JRST AUXDON		;FORGET IT
	MOVEM T3,SAVJFY#	;SAVE JIFFIES/SEC
	MOVE T3,[TIMITM,,TIMTAB]
	GETTAB T3,
	 JRST AUXDON
	MOVEM T3,SAVTIM#
	MOVEI T3,4
	AUXCAL T2,T3		;SEND IT
	MOVEI T2,^D12
	MOVE T4,[POINT 6,DATIM]
	PUSHJ P,RDCH
	IDPB T3,T4
	SOJG T2,.-2

;NOW WE HAVE READ THE TIME

	PUSHJ P,RDCH
	MOVE T2,AUXPRT
	ZAPCIR T2,
	CAIE T3,123
	JRST NXTSYS		;NOT RIGHT ENDING CODE
	MOVE T3,[DATITM,,DATTAB]
	GETTAB T3,
	 JRST AUXDON
	JUMPGE T3,AUXDON	;FOUND THE DATE
	MOVE T3,[TIMITM,,TIMTAB]
	GETTAB T3,
	 JRST AUXDON
	SUB T3,SAVTIM
	LSH T3,-1		;DIVIDE BY 2
	MOVE T2,DATIM+1
	IMUL T2,SAVJFY
	ADD T2,T3
	MOVEM T2,DATIM+1
	MOVEI T2,DATIM
	DATUUO T2,
	 JFCL
	MOVE T1,DATIM
	HRLI T1,SUUDAT
	SETUUO T1,
	 JFCL
	MOVE T1,DATIM+1
	IDIV T1,SAVJFY
	ADDI T1,^D30		;NOW CONVERT TO MINUTES WITH ROUNDING
	IDIVI T1,^D60
	HRLI T1,SUUTIM
	SETUUO T1,
	JFCL
	POPJ P,


RDCH:	HRRZ T3,AUXPRT
	ROT T3,-11
	IOR T3,[110,,^D60000]
	HIBER T3,
	 JRST AXRDA
	AUXCAL T1,T3
	 SKIPA
	POPJ P,
AXRDA:	POP P,T1		;GET RID OF EXTRA CALL
	MOVE T1,AUXPRT
	ZAPCIR T1,
	JRST NXTSYS

NOAUX:	CAIN T1,4		;IF SYSTEM UNAVAILABLE
	JRST NXTSYS		;GO TRY NEXT ONE
	MOVE T1,[SUUMSG,,[ASCIZ /
FAILURE CREATING AUXILIARY CIRCUIT
/]]				;TELL OPERATOR SOMETHING IS WRONG
	SETUUO T1,
	 JFCL
	SOJL T6,AUXDON		;GIVE UP IF TOO MANY OF THESE
NXTSYS:	AOBJN T7,NEWSYS
AUXDON:	POPJ P,			;FAILED

DATIM:	BLOCK 2
	400020,,0		;FROM GMT TO THIS USER

SYSNUM:	DEC 31,32,35,1,4,33,36,14,34,37,38,17
NUMSYS==.-SYSNUM

AUXSTR:	ASCIZ /OPER:33;
/
>  ;End of REPEAT 0
SUBTTL	Login a job and run a program

RUNCOM:	PUSH P,T1		;LH is RUN uuo pointer, RH is directory pointer
	PUSHJ P,LOGIT
	 JRST INIFAL		;CAN NOT GET HIM LOGGED IN
	PUSHJ P,INIUP		;"INITIA starting <program> on Line <number>"
	MOVEI T1,11
	MOVEM T1,FAILNM		;RUN FAILURE
	MOVNI T1,1
	ATTACH T1,		;DETACH FROM LINE
	POP P,T1
	HLRZS T1
	RUN T1,
	JRST INIFAL

OPRNAM:	SIXBIT /OPER/	;Username for LOGIN
	0

ACTNAM:	SIXBIT /UN1/	;Directory name for accounting data
	0

PRPNAM:	SIXBIT	/PERPOPER/	;Username for XEXEC

SUBTTL	Run DSKCLN if started on TTY0:

DSKCLN:	MOVE T1,[RUNCLN,,OPRNAM]
	JRST RUNCOM

RUNCLN:	SIXBIT /SYS/
	SIXBIT /DSKCLN/
	OCT 0,0,0
	LICOP,,0


SUBTTL	Run CHKPNT is started on TTY1:

CHKPNT:	MOVEI T1,ACTNAM		;MAKE SURE ACCOUNTING DIRECTORY THERE
	PUSHJ P,DIRMAK
	 JRST INIFAL
	MOVE T1,[RUNCHK,,OPRNAM]
	JRST RUNCOM

RUNCHK:	SIXBIT /SYS/
	SIXBIT /CHKPNT/
	OCT 0,0,0
	LICOP,,0


SUBTTL	Run COPYCR if started on TTY2:

COPYCR:	MOVE T1,[RUNCPY,,OPRNAM]
	JRST RUNCOM

RUNCPY:	SIXBIT /SYS/
	SIXBIT /COPYCR/
	OCT 0,0,0
	LICOP,,0


SUBTTL	Run KMCLOD if started on TTY4: on a KS-2020

	%CNSER==20,,11
KMCLOD:	MOVE	T1,[%CNSER]	;Get CPU serial number
	GETTAB	T1,
	  MOVEI	T1,0
	CAIGE	T1,^D4096	;GETTAB returns number .GT. ^O7777 for KS
	 JRST	NOTKS		;Do nothing on KI, KL, or F3
	MOVE	T1,[RUNKMC,,OPRNAM]
	JRST	RUNCOM

RUNKMC:	SIXBIT	/SYS/
	SIXBIT	/KMCLOD/
	OCT 0,0,0
	LICOP,,0


SUBTTL	Run nothing if started on TTY4: on a KI, KL, or F3

NOTKS:	LOGOUT			;[22] May want to add code for F3 here


SUBTTL	Run CRON if started on TTY25

CRON:	MOVE T1,[RUNCRO,,PRPNAM]
	JRST RUNCOM

RUNCRO:	SIXBIT /SYS/
	SIXBIT /CRON/
	OCT 0,0,0
	LICOP,,0
SUBTTL	Error routines

INIFAL:	MOVE T1,[POINT 7,TRMBUF]
	MOVEI T2,[ASCIZ /
  ? Initialization failure/]
	PUSHJ P,ASCTRN
	SKIPGE JOBNUM
	  JRST NOJOB
	MOVEI T2,[ASCIZ / job /]
	PUSHJ P,ASCTRN
	MOVE T2,JOBNUM
	MOVE T2,LN0NAM(T2)
	PUSHJ P,ASCTRN
NOJOB:	MOVE T2,FAILNM
	MOVE T2,FAILTB(T2)
	PUSHJ P,ASCTRN
	MOVEI T2,[BYTE (7)15,12,7,0]
	PUSHJ P,ASCTRN
	PUSHJ P,CTYMSG
	LOGOUT

ARRAY TRMBUF[50]

FAILTB:	[ASCIZ / NO SYS/]
	[ASCIZ / NO LUD/]
	[ASCIZ / NO USER/]
	[ASCIZ / CAN NOT LOGIN/]
	[ASCIZ / INTERLOCK/]
	[ASCIZ / NO FSN/]
	[ASCIZ / BAD UFD/]
	[ASCIZ / STRUUO/]
	[ASCIZ / GETTAB/]
	[ASCIZ / RUN/]
	[ASCIZ / TIMEOUT/]
	[ASCIZ / ACT DEVICE NOT INITTED BY CHKPNT/]   ;AAA DEC 76


INIUP:	MOVE T1,[ POINT 7,TRMBUF ]	;KEEP OPERATOR INFORMED
	MOVEI T2,[ ASCIZ /INITIA starting / ]
	PUSHJ P,ASCTRN
	MOVE T2,JOBNUM
	MOVE T2,LN0NAM(T2)
	PUSHJ P,ASCTRN
	MOVEI T2,[ ASCIZ / on TTY/ ]
	PUSHJ P,ASCTRN
	MOVE T2,JOBNUM
	ILDB T3,[ POINT 6,LN0TAB(T2) ]
	ADDI T3,40
	IDPB T3,T1
	MOVEI T2,[ ASCIZ /
/ ]
	PUSHJ P,ASCTRN
	PUSHJ P,CTYMSG
	POPJ P,

ASCTRN:	HRLI T2,(POINT 7,0)
ASCTR1:	ILDB T3,T2
	JUMPE T3,CPOPJ
	IDPB T3,T1
	JRST ASCTR1

SIXTRN:	SETZ	T3,		;Output SIXBIT in T2, uses T3
	ROTC	T2,6
	ADDI	T3," "
	IDPB	T3,T1
	JUMPN	T2,SIXTRN

DECTRN:	IDIVI T2,^D10
	HRLM T3,(P)
	SKIPE T2
	PUSHJ P,DECTRN
	HLRZ T2,(P)
	ADDI T2,"0"
	IDPB T2,T1
	POPJ P,


CTYMSG:	MOVEI T2,0			; insure that it's an ASCIZ
	IDPB T2,T1
	MOVE T1,[SUUMSG,,TRMBUF]
	SETUUO T1,			; output TRMBUF to CTY
	  JFCL
	MOVE T1,[POINT 7,TRMBUF]	; in case we do more output
	POPJ P,
SUBTTL	Run INITIA if started on TTY3:

UPENT==15	;ENTRY FOR UPTIME
UPTAB==12	;TABLE FOR UPTIME
HJENT==20	;ENTRY FOR HIGHJOB
HJTAB==12	;TABLE FOR HIGH JOB

WAITP:	MOVEI T1,8
	SLEEP T1,
	PUSHJ P,INIUP		;"INITIA starting INITIALIZATION on Line 3"
	MOVEI T1,10
	MOVEM T1,FAILNM
	MOVE T1,[UPENT,,UPTAB]
	GETTAB T1,
	 JRST INIFAL
	MOVEM T1,STRUPT#	;SAVE TIME FOR 5 MIN TIMEOUT
SRCLP:	MOVEI	T1,0		;Sleep for 0 seconds
	SLEEP	T1,		;(delay 1 tick for other job to run)
	MOVE T1,[HJENT,,HJTAB]
	GETTAB T1,
	 JRST INIFAL
	SETZM CHKTAB
	MOVE T2,[CHKTAB,,CHKTAB+1]
	BLT T2,CHKTAB+CHKLEN-1	;CLEAR CHECK TABLE
				;T1 as HIJOB
SRCJOB:	MOVEI T2,JBTSTS
	HRL T2,T1
	GETTAB T2,
	 JRST NXTJOB		;NOT THERE, TRY NEXT JOB
	TLNN T2,JNA
	JRST NXTJOB		;JOB NOT ASSIGNED, IGNORE
	MOVEI T2,JBTPRG
	HRL T2,T1
	GETTAB T2,
	 JRST NXTJOB
	MOVSI T3,-CHKLEN
MTCCHK:	CAMN T2,CHKNAM(T3)	;LOOK FOR PROGRAM NAME
	 JRST MTCFND		;Found it
MTCNXT:	AOBJN T3,MTCCHK
	JRST NXTJOB		;EXTRANIOUS JOB, KEEP GOING

MTCFND:	SKIPE CHKTAB(T3)
	 JRST MTCNXT		;FOUND, GO IN CASE MORE OF SAME NAME
	SETOM CHKTAB(T3)
NXTJOB:	SOJG T1,SRCJOB		;SEARCH MORE JOBS
	MOVSI T3,-CHKLEN
	SKIPE CHKTN2(T3)	;THIS IS NON-ZERO FOR OPTIONAL PROGS
	 SETOM CHKTAB(T3)	;MARK THEM AS ALWAYS THERE
	AOBJN T3,.-2
	MOVSI T3,-CHKLEN	;ALL CHECKED
ALLCHK:	SKIPN CHKTAB(T3)
	 JRST LOPWAT		;1 NOT FOUND
	AOBJN T3,ALLCHK
	JRST RSTLUK		;Go unrestrict the system

LOPWAT:	MOVE T1,[UPENT,,UPTAB]
	GETTAB T1,
	 JRST INIFAL
	SUB T1,STRUPT
	CAIG T1,^D<5*60*60>	;5 MIN WAIT
	 JRST SRCLP		;CHECK AGAIN
	PUSH	P,CHKNAM(T3)	;Name of program being looked at
	MOVE	T1,[ POINT 7,TRMBUF ]	;KEEP OPERATOR INFORMED
	MOVEI	T2,[ ASCIZ /Cannot find job running /]
	PUSHJ	P,ASCTRN
	POP	P,T2
	PUSHJ	P,SIXTRN
	PUSHJ	P,CTYMSG
	MOVEI T1,12
	MOVEM T1,FAILNM
	JRST INIFAL		;Failure, TIMEOUT

	DEFINE X (QX1,QX2,QX3,KEPFLG,RUNNAM)
<	IFN KEPFLG,<<SIXBIT /RUNNAM/>>>

CHKNAM:	LINES
CHKLEN==.-CHKNAM

ARRAY CHKTAB[CHKLEN]

	DEFINE X (QX1,QX2,QX3,KEPFLG,RUNNAM)
<	IFN KEPFLG,<IFN KEPFLG-2,<0>
		IFE KEPFLG-2,<-1>>>

CHKTN2:	LINES
SUBTTL	Wait for CHKPNT to INIT the ACT device before unshutting system.

ACTTST:	DEC 3*60		;Wait 3 minutes

RSTLUK:	MOVE	T2,ACTTST	;Get time limit
RSTLK1:	MOVE	T1,[SUUMSG,,[ASCIZ /Waiting for date and time
/]]
	TRNN	T2,77		;Multiple of 64 seconds?
	 SETUUO	T1,		;Yes, tell OPR why the delay
	  JFCL
	MOVSI	T1,(SIXBIT/ACT/)
	DEVCHR	T1,
	TRNN	T1,200000	;BIT ON IF DEVICE BEEN INITTED
	 JRST  [MOVEI T1,^D1	;TRY AGAIN IN 1 SEC
		SLEEP T1,
		SOJG T2,RSTLK1	;Loop for up to 3 minutes
		JRST .+1]	;AH,WELL, GO ON. CHKPNT PROBABLY WAITING ON DATE
	MOVE T1,[DATITM,,DATTAB]
	GETTAB T1,
	 SETO  T1,
	JUMPL T1,[MOVEI T1,400000
		  JRST SCHCOM]	;SET SCH 400000
	PUSHJ P,GETRST		;CHECK ON RSTRCT TIME
	 TDZA T1,T1		;NO FILE, NOT RESTRICTED
	MOVE T1,RSTRCT
SCHCOM:	HRLI T1,SUUSCH
	SETUUO T1,
	 JFCL
	JRST TELFIN

RSTNUM:	MOVEI T2,0
RSTNM1:	PUSHJ P,RDCHR
	CAIN T3,11
	JRST RSTNM1
	CAIGE T3,40
	POPJ P,
	CAIG T3,"9"
	CAIGE T3,"0"
	JRST RSTNM1
RSTNM2:	IMULI T2,^D10
	ADDI T2,-"0"(T3)
	PUSHJ P,RDCHR
	CAIG T3,"9"
	CAIGE T3,"0"
	SKIPA
	JRST RSTNM2
	IDIVI T2,^D100
	IMULI T2,^D60
	ADD T2,T3
	IMUL T2,RSTJIF
	AOS (P)
	POPJ P,

RDCHR:	SOSLE RSTBUF+2
	JRST RDCHR1
	INPUT RSTFIL,0
	STATZ RSTFIL,20000
	JRST [MOVEI T3,12
		POPJ P,]
RDCHR1:	IBP RSTBUF+1
	MOVEI T3,1
	TDNE T3,@RSTBUF+1
	JRST SKPLNM
	LDB T3,RSTBUF+1
	JUMPE T3,RDCHR
	CAIN T3,12
	AOS RSTLIN
	POPJ P,

SKPLNM:	MOVNI T3,4
	ADDM T3,RSTBUF+2
	REPEAT 4,<IBP RSTBUF+1>
	PUSHJ P,RDCHR
	JRST RDCHR
;LOOKUP DSK:RSTRCT.TIM[1,4]

GETRST:	INIT RSTFIL,0
	SIXBIT /DSK/
	RSTBUF
	 POPJ P,
	LOOKUP RSTFIL,RSTNAM
	 JRST LEVRST
	SETOM RSTLIN#	;LINE WE ARE ON
	MOVEI T1,0
	MOVE T2,[POINT 7,T1]
	REPEAT 3,<PUSHJ P,RDCHR
	IDPB T3,T2>		;GET TIME ZONE
	MOVSI T2,-ZONUM
	CAME T1,ZONES(T2)
	AOBJN T2,.-1
	JUMPGE T2,LEVRST	;TIME ZONE NOT FOUND
	MOVE T3,ZONCNV(T2)
	MOVSM T3,RSTZON	;SAVE ZONE
	MOVE T1,[JFYITM,,JFYTAB]
	GETTAB T1,
	 MOVEI T1,^D60
	IMULI T1,^D60
	MOVEM T1,RSTJIF#
	MOVE T1,[DATITM,,DATTAB]
	GETTAB T1,
	 JRST LEVRST
	MOVE T2,[TIMITM,,TIMTAB]
	GETTAB T2,
	 JRST LEVRST
	MOVEI T4,T1
	DATUUO T4,
	 JRST LEVRST	;BAD TIME OF SOME KIND
	SETZM RSTRCT#	;NOT RESTRICTED
	MOVEM T1,RSTTDY#	;CURRENT TIME IN USERS ZONE
	MOVEM T2,RSTNOW#	;BOTH DATE AN TIME
	MOVEI T2,10
	MOVEM T2,RSTCNT#
RSTNXD:	MOVEM T1,RSTDAT	;TIME TO CHANGE RESTRICT
	ADDI T1,2
	IDIVI T1,7		;GET DAY OF WEEK
	CAMG T2,RSTLIN
	JRST	[USETI RSTFIL,1	;NEED TO BACK UP
		SETOM RSTLIN
		SETZM RSTBUF+2
		JRST .+1]
RSTSKP:	PUSHJ P,RDCHR
	CAIE T3,12
	JRST RSTSKP
	CAMLE T2,RSTLIN
	JRST RSTSKP
;Read RSTRCT.TIM
RSTNXE:	PUSHJ P,RSTNUM		;READ A NUMBER FROM LINE
	 JRST RSTFIN
	MOVEM T2,RSTN1#
	PUSHJ P,RSTNUM
	 JRST LEVRST	;SHOULD BE IN PAIRS
	MOVEM T2,RSTN2#
	MOVE T1,RSTDATT
	CAME T1,RSTTDY
	JRST RSTUN1	;USE N1
	MOVE T2,RSTN2
	CAMGE T2,RSTNOW
	JRST RSTNXE	;LOOK FOR MORE ON LINE
	MOVE T2,RSTN1
	CAMGE T2,RSTNOW
	JRST RSTUN2	;USE N2 AND RESTRICT ON
RSTUN1:	MOVE T1,RSTN1
RSTGOD:	MOVEM T1,RSTTIM
	MOVEI T1,RSTDAT
	DATUUO T1,
	 SKIPA
	AOS (P)
LEVRST:	RELEASE RSTFIL,0
	POPJ P,

RSTUN2:	MOVEI T1,100000
	MOVEM T1,RSTRCT
	MOVE T1,RSTN2
	JRST RSTGOD

RSTFIN:	AOS T1,RSTDAT
	SOSL RSTCNT
	JRST RSTNXD	;TRY NEXT DAY
	JRST LEVRST

RSTDAT:	0
RSTTIM:	0
RSTZON:	0

RSTNAM:	3
	1,,4
	SIXBIT /RSTRCT/
	SIXBIT /TIM/

	DEFINE ZONEM
<	X PDT,50
	X MDT,51
	X CDT,52
	X EDT,53
	X PST,10
	X MST,11
	X CST,12
	X EST,13>

	DEFINE X (A,B)
<	ASCII /A/>

ZONES:	ZONEM
	ZONUM==.-ZONES

	DEFINE X (A,B)
<	400020,,400000+B>
ZONCNV:	ZONEM


DATITM==11
DATTAB==11
TIMITM==10
TIMTAB==11	;DATE AND TIME ENTRIES

TELFIN:	MOVE T1,[POINT 7,TRMBUF]
	MOVEI T2,[ASCIZ /
       Initialization completed /]
	PUSHJ P,ASCTRN
	MOVE T2,[DATITM,,DATTAB]
	GETTAB T2,
	 JRST INIFAL
	SKIPGE T2			;.LT. 0 means date not set yet
	 PUSHJ	P,NODATE		;Beep until it is set
	MOVE T3,[TIMITM,,TIMTAB]
	GETTAB T3,
	 JRST INIFAL
	MOVE T4,[400020,,20]	;FROM DAY GMT TO STANDARD DEC GMT
	MOVEI T5,T2
	DATUUO T5,
	 TRO FL,FLNGMT	;TIME NOT IN GMT
	PUSH P,T3
	IDIVI T2,^D31
	PUSH P,T2
	MOVEI T2,1(T3)	;DAY IN T2
	PUSHJ P,DECTRN
	POP P,T2
	IDIVI T2,^D12
	PUSH P,T2
	MOVE T2,MONTAB(T3)
	PUSHJ P,ASCTRN
	POP P,T2
	ADDI T2,^D1964
	PUSHJ P,DECTRN
	MOVEI T2," "
	IDPB T2,T1
	POP P,T2	;TIME
	IDIVI T2,^D3600	;TO MINS
	IDIVI T2,^D60
	IMULI T2,^D100
	ADD T2,T3	;TO PRINTABLE FORM
	PUSHJ P,DECTRN
	MOVEI T2,[ASCIZ / GMT/]
	TRNN FL,FLNGMT
	PUSHJ P,ASCTRN
	PUSHJ P,CTYMSG

;[24] Update access date on HOME.SYS and SAT.SYS so ONCE can get date next time.

	OPEN	FIL,SYSOPN	;[24] Open channel to SYS
	  JRST	TELFN1		;[24]
	LOOKUP	FIL,HOMSYS	;[24] Locate HOME.SYS
	  JRST	TELFN1		;[24]
	INPUT	FIL,DUMRED	;[24] This updates the access date
	CLOSE	FIL,		;[24] ...
	LOOKUP	FIL,SATSYS	;[24] Locate SAT.SYS
	  JRST	TELFN1		;[24]
	INPUT	FIL,DUMRED	;[24] This updates the access date
	CLOSE	FIL,		;[24] ...
TELFN1:	JRST	SYSJOB		;[22] Go run XEXEC SYSJOB

SYSOPN:	EXP 17,SIXBIT/SYS/,0	;[24] Open block
HOMSYS:	EXP SIXBIT/HOME/,SIXBIT/SYS/,0,0 ;[24] Lookup block
SATSYS:	EXP SIXBIT/SAT/,SIXBIT/SYS/,0,0  ;[24] Lookup block
	0			;[24] Word read in here
DUMRED:	IOWD	1,.-1		;[24] Read 1 word
	0			;[24] End of IO LIST

NODATE:	MOVEI	T2,[ASCIZ /? Date and time not set    /]
	PUSHJ	P,ASCTRN
	PUSHJ	P,CTYMSG
	MOVE	T1,[%CNSER]	;[25] Get CPU serial number
	GETTAB	T1,		;[25]
	  MOVEI	T1,0		;[25]
	MOVE	T3,ACTTST	;[25] Wait another 3 minutes on KS
	CAIGE	T1,^D4096	;[25] GETTAB returns number .GT. ^O7777 for KS
	 IMULI	T3,^D10		;[25] Wait 30 minutes if KI or KL or F3

BELLP:	MOVE	T2,[SUUSCH,,0]	;[25] Unshut the system after 3 more
	SOSN	T3		;[25]  minutes (6 minutes total wait on KS)
	 SETUUO	T2,		;[25] So that anyone at TRW can set date/time
	  JFCL			;[25]  if their microTYMbase is not responding
	MOVE	T2,[SUUMSG,,[BYTE (7) 7,7,0]]
	SETUUO	T2,
	  JFCL
	MOVEI	T2,1		;SEND BELLS EVERY SECOND
	SLEEP	T2,
	MOVE	T2,[DATITM,,DATTAB]
	GETTAB	T2,
	  MOVEI	T2,0
	JUMPL	T2,BELLTIL TIME SET

	MOVE	T1,[POINT 7,TRMBUF]
	MOVEI	T2,[ASCIZ /
       Date and time set at /]
	PUSHJ	P,ASCTRN
	MOVE	T2,[DATITM,,DATTAB]
	GETTAB	T2,		;Return date in T2
	  MOVEI	T2,0
	POPJ	P,		;End of NODATE



	DEFINE DATIT (A)
<IRP A,<[ASCIZ /-A-/]>>

MONTAB:	DATIT <JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC>
SUBTTL	Run XEXEC if started on TTY3:

.GTLIC==-20			; JBTLIC gettab index

XEXEC0:	EXP 17,SIXBIT/DSK/,0	;[24] Open block

XEXEC1:	SIXBIT	/INITIA/	;[24] Name
	SIXBIT	/SYS/		;[24] Ext
	EXP	0		;[24]
	XWD	0,XEXEC2	;[24] Directory
XEXEC2:	SIXBIT	/XEXEC       /	;[24]


SYSJOB:	MOVEI	T1,PRPNAM	;Log in as PERPOPER
	PUSHJ	P,LOGIT
	  JRST	INIFAL
SYSJB0:	OPEN	FIL,XEXEC0	;[24] Open channel to disk
	  JRST	SYSJB1		;[24] Punt
	LOOKUP	FIL,XEXEC1	;[24] Look for (XEXEC)INITIA.SYS
	  SKIPA	T1,XEXEC1+1	;[24] Failed, get error code
	 JRST	SYSJB1		;[24] OK, continue
	TLZ	T1,-1		;[24] Clear left half
	JUMPN	T1,SYSJB1	;[24] Continue if anything but FILE NOT FOUND
	RELEAS	FIL,		;[24] Must be a system rebuild
	MOVEI	T1,5		;[24]
	MOVEI	T2,^D60		;[24] Try again in 5 minutes
	SLEEP	T2,		;[24]
	SOJG	T1,.-1		;[24]
	MOVE	T1,[SUUMSG,,NOXEXC] ;[24]
	SETUUO	T1,		;[24]
	  JFCL			;[24]
	JRST	SYSJB0		;[24]

NOXEXC:	ASCIZ /
?INITIA cannot run XEXEC until (XEXEC)INITIA.SYS is restored from tape./;[24]

SYSJB1:	RELEAS	FIL,		;[24]
	MOVE	T1,[SUUMSG,,SYJMSG]
	SETUUO	T1,		;Tell OPR about XEXEC
	  JFCL
	MOVEI	T1,^D15
	SLEEP	T1,
	MOVNI	T1,1
	ATTACH	T1,		;DETACH FROM LINE
	HRROI	0,.GTLIC
	GETTAB	0,
	  LOGOUT
	HLLZM	0,SYSJBR+5	; setup LIC,,CORE
	MOVE	0,[SYSJBA,,1]
	BLT	0,17		; load up ACs for call to EXEC
	MOVE	0,[1,,SYSJBR]
	RUN	0,		; run XEXEC at start+1
	  LOGOUT

SYJMSG:	ASCIZ /

INITIA running XEXEC SYSJOB
/				;Last message output by INITIA

SYSJBA:				; argument block for EXEC
PHASE	1
	1b0+"C"			; out to CTY, Cont on errors
	asciz "@(SYS)SYSJOB.X\LOGOUT
"				; command string
IFG <.-20>,<printx **command string for SYSJOB too big!**>
DEPHASE
RELOC	SYSJBA+17		; #s 1-17
;Note: The XEXEC program that is the PERP replacement does not look at
;its accumulators at startup.  The above is for Bill Soley's old XEXEC
;and is obsolete.   1-Jan-87

SYSJBR:	sixbit	"SYS"		; device name
	sixbit	"XEXEC"		; program name
	Z			; extension (default)
	Z			; not used
	Z			; ppn
	Z			; LIC,,CORE
SUBTTL	LUD and LOGIN routines

LOGSIZ==5	;SIZE OF LOGIN BLOCK TO MONITOR

ARRAY LOGARG[LOGSIZ]

INPPN=LOGARG
INPRV=LOGARG+1
INAUN=LOGARG+2
INNAM=LOGARG+3

RND:	ADD T2,N	;RANDOMIZE FUNCTION
	ROTC N,-22
	MOVEI T5,5
RND1:	MOVE T6,T2(T4)
	MUL T6,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM T7,T3(T4)
	AOJE T4,RND2
	MOVNI T4,1
	TRNE T2,1
	SKIPL T3
	MOVEI T4,0
	EXCH T1,T3
RND2:	SOJG T5,RND1
	POPJ P,

;SETUP ARGS FOR EXTEND UFD LOOKUP OR ENTER

SETEXL:	MOVEI T1,EXLLEN-1
	MOVEM T1,SRCBUF	;LENGTH
	MOVE T1,INPPN
	MOVEM T1,SRCBUF+EXLNAM
	MOVSI T1,'UFD'
	MOVEM T1,SRCBUF+EXLEXT
	MOVE T1,[1,,1]
	MOVEM T1,SRCBUF+EXLPPN
	POPJ P,

;SET AND CLEAR UFD LINTERLOCKS

STUFCL:	MOVEI T2,UCLSTR		;CLEAR CODE
STUFC1:	MOVE T1,[3,,T2]
	MOVE T3,UFDFSN
	MOVE T4,INPPN
	STRUUO T1,
	 POPJ P,
CPOPJ1:	AOS (P)
CPOPJ:	POPJ P,
SUBTTL	Run user's INIT file if already logged in
;T1 HAS A POINTER TO A USER NAME (IN SIXBIT). GET PROGRAM LOGGED IN

ARRAY RUNBLK[6]
.GTUNM==-22	;GETTAB index for first half of user name
.GTUN1==-21	; second half

RUNINI:	TRO	FL,FLNINI ;Already logged in, just run init file
	HRROI	T1,.GTUNM
	GETTAB	T1,
	  JRST	RUNXIT
	MOVEM	T1,INNAM
	HRROI	T1,.GTUN1
	GETTAB	T1,
	  JRST	RUNXIT
	MOVEM	T1,INNAM+1
	MOVEI	T1,INNAM
	PUSHJ	P,LOGIT
	  EXIT
	SKIPN	T1,BUF+LCSN(T3)
	  JRST	RUNXIT
	MOVEM	T1,RUNBLK+1
	DMOVE	T1,BUF+LCSU(T3)
	DMOVEM	T1,INNAM
	CLOSE	SYS,0		;LUD.SYS
	MOVEI	T1,INNAM	;PT AT USERNAME
	SKIPN	INNAM
	  SETZ	T1,
	MOVEM	T1,RUNBLK+4	;USERNAME
	MOVSI	T1,(SIXBIT/DSK/)
	MOVEM	T1,RUNBLK	;DEV
	MOVEI	T1,RUNBLK	;RUN INIT FILE AT ENTRY+0
RUNIT:	RUN	T1,		;DO IT
	JFCL
RUNXIT:	OUTSTR	[ASCIZ/
can't run required program/]
	EXIT

SUBTTL	Find entry in LUD.SYS

DIRMAK:	TROA FL,FLNLG	;JUST CREATE DIRECTORY
LOGIT:	TRZ FL,FLNLG
	SETZM FAILNM#	;REASON FOR FAILURE
	INIT SYS,16
	SIXBIT /SYS/
	0
	 POPJ P,		;FAILURE REASON 0
	AOS FAILNM
	LOOKUP SYS,LUDNAM
	 POPJ P,	;FAILURE REASON 1
	AOS FAILNM
	MOVE N,(T1)
	MOVEM N,INNAM
	MOVE N1,1(T1)
	MOVEM N1,INNAM+1
	MOVE T1,[555555555555]
	MOVE T2,[361275431652]
	MOVE T3,[612754316523]
	MOVEI T4,0
	REPEAT 4,<PUSHJ P,RND>
	XOR T3,T2
	MOVE N,T3
	TLZ N,400000
	IDIVI N,LUDHSH
	MOVEM N1,HSHBLK#		;BLOCK NUMBER TO START ON
	XOR T1,T3
	JUMPE T1,CPOPJ	;FAILURE REASON 2 (NOT FOUND)
	MOVEM T1,HSHNAM#
	AOS T2,HSHBLK	;BLOCK TO START ON
SRUS0:	USETI SYS,(T2)
	INPUT SYS,COM
	STATZ SYS,760000
	POPJ P,		;ABORT ON ERRORS
	MOVEI T3,0
SRUSR:	CAMN T1,BUF+LHUN(T3)
	JRST FNDUSR	;GOT HIM
NUSER:	SKIPG T2,BUF+LPPN(T3)
	JRST BLKLNK	;LINK WORD
	MOVE T2,BUF+LLINK(T3)	;LINK
	ANDI T2,177
	ADD T3,T2
	CAIL T3,177
	POPJ P,
	JRST SRUSR	;TRY NEXT

BLKLNK:	JUMPE T2,CPOPJ	;END OF LINK
	HRRZS T2
	CAMG T2,HSHBLK	;MUST ALWAYS GO UP
	POPJ P,
	MOVEM T2,HSHBLK	;SAVE FOR NEXT TIME
	JRST SRUS0
FNDUSR:	MOVE T1,BUF+LPPN(T3)
	MOVEM T1,INPPN
	MOVE T1,BUF+LAUN(T3)
	MOVEM T1,INAUN
	MOVE T1,BUF+LPRV(T3)
	TLO T1,JP.NAT		;don't ask to att this job at LOGINNs
	MOVEM T1,INPRV		;NOW HAVE LOGIN AGR SETUP
	MOVE T2,BUF+LLINK(T3)
	ANDI T2,177
	MOVE T1,BUF+LBITS(T3)
	AOS FAILNM	;CAN NOT LOGIN
	TLNE T1,LNOLOG
	POPJ P,
	TLNN T1,LTRUB
	JRST NOTRU	;WORRY ABOUT TRU CONTROL WORD
	ADDI T3,1
	SUBI T2,1
NOTRU:	TRNN	FL,FLNINI	;Want to run init file?
	  JRST	CHKCSP		;No
	TRNN	T1,LCSPB	;Yes, is there one?
	  POPJ	P,		;No, give error return
	JRST	CPOPJ1		;Ace

CHKCSP:	TRNN T1,LCSPB	;IS THERE AN INIT CUSP?
	JRST NOCSP
	ADDI T3,3
	SUBI T2,3	;ADJUST FOR COUNT
NOCSP:	MOVEM T3,STRPTR#	;POINTER TO STR LIST
	CLOSE SYS,0	;CLOSE FILE, DONE WITH LUD
	AOS FAILNM
	MOVE T7,STRPTR
	MOVE T2,[SIXBIT /DSKB/]	;FOR HISTORICAL REASONS
	MOVEM T2,UFDFSN#	;SAVE NAME
	MOVEI N1,UFDGIL	;TIMES TO TRY FOR INTERLOCK
UFDSTA:	MOVEI T2,ULKSTR
	PUSHJ P,STUFC1
	 JRST	[MOVEI T1,1
		SLEEP T1,
		SOJG N1,UFDSTA
		POPJ P,]	;ERROR, INTERLOCK BUSY
	AOS FAILNM	;READ FOR ERROR 5
	MOVEI T1,16
	HRLI T1,400000
	MOVE T2,UFDFSN
	MOVEI T3,0
	OPEN UFD,T1
	 JRST ILKCLN	;CLEAR INTERLOCK AND LEAVE
	AOS FAILNM
	PUSHJ P,SETEXL	;SET EXTENDED LOOKUP LIST
	SETZM SRCBUF+4	;CLEAR MOST OF IT
	MOVE T1,[SRCBUF+4,,SRCBUF+5]
	BLT T1,SRCBUF+EXLLEN-1
	LOOKUP UFD,SRCBUF
	 SKIPA T1,SRCBUF+EXLEXT	;GET ERROR CODE
	JRST UFDEX	;EXISTS
;DID NOT FIND A UFD

	TRNE T1,-1
	JRST ILKCLN	;CLEAR INTERLOCK AND LEAVE
	MOVE T1,[RIPLOG,,RIPDIR]
	MOVEM T1,SRCBUF+EXLSTS
	PUSHJ P,UFDSET	;SET SOME COMMON DATA
	ENTER UFD,SRCBUF
	 JRST ILKCLN		;BAD UFD
	USETO UFD,2
	CLOSE UFD,0
	JRST UFDOK

UFDSET:	MOVE T1,INNAM
	MOVEM T1,SRCBUF+EXLUN1
	MOVE T1,INNAM+1
	MOVEM T1,SRCBUF+EXLUN2
	MOVE T3,STRPTR
	MOVE T1,BUF+LFSQIN(T3)
	ADDI T1,3
	LSH T1,-2	;CONVERT TO PAGES
	MOVEM T1,SRCBUF+EXLQTF
	MOVE T1,BUF+LFSQOT(T3)
	ADDI T1,3
	LSH T1,-2
	MOVEM T1,SRCBUF+EXLQTO
	JRST SETEXL

ILKCN1:	AOS (P)
ILKCLN:	PUSHJ P,STUFCL	;CLEAR INTERLOCK
	 POPJ P,		;RETURN IN EITHER CASE
	POPJ P,
UFDEX:	MOVSI T1,RIPLOG
	TDNN T1,SRCBUF+EXLSTS	;ALREADY LOGGED IN
	JRST UFDLNG		;UFD NOT MARKED LOGGED IN
	MOVE T2,[SEGPTR,,CNFTBL]
	GETTAB T2,
	 MOVEI T2,^D96	;CHECK JOBS FOR UFD IN SEARCH LIST
	MOVNI T2,-1(T2)
	HRLZS T2		;AOBJN POINTER
UFDSRC:	MOVE T3,UFDFSN
	MOVEM T3,CHRBUF+GOBNAM
	MOVE T3,INPPN
	MOVEM T3,CHRBUF+GOBPPN
	MOVEI T3,1(T2)
	MOVEM T3,CHRBUF+GOBJOB
	MOVEI T3,CHRBUF
	GOBSTR T3,
	 SKIPA
	JRST UFDLNG		;TREAT AS IF NOT LOGGED IN
	AOBJN T2,UFDSRC
	SETZM SRCBUF+EXLUSD	;NOTHING USED YET
	OPEN FIL,[16
	SIXBIT /DSK/
	0]	;IN CASE WE NEED LOOKUP
	 JRST ILKCLN	;ERROR
	MOVE T1,[-200,,UFDBUF]
	MOVEM T1,UFDARG
	MOVSI T1,(SIXBIT /*/)
	MOVEM T1,UFDARG+1
	MOVEM T1,UFDARG+2
	MOVSI T1,200000
	MOVEM T1,UFDARG+3
	SETZM UFDARG+4
UFDUSD:	MOVE T1,[33,,UFD]
	CHANIO T1,UFDARG
	 JRST UFDRDY	;ASSUME EOF
	MOVE T1,UFDARG
UFDUS1:	SOSGE UFDARG+7
	JRST UFDUSD	;TIME FOR ANOTHER
	SKIPGE T2,2(T1)	;GET SIZE
	JRST	[DMOVE T2,(T1)
		DMOVEM T2,LOKARG+2
		MOVEI T2,EXLALP	;NUMBER OF PAGES
		MOVE T3,INPPN
		DMOVEM T2,LOKARG
		LOOKUP FIL,LOKARG
		 TDZA T2,T2
		MOVE T2,LOKARG+EXLALP
		CLOSE FIL,
		JRST FRCLOK]
	ADDI T2,3	;IF NOT NEG (LOOKUP NEEDED)
	LSH T2,-2	;CONVERT TO PAGES
FRCLOK:	ADDM T2,SRCBUF+EXLUSD
	ADD T1,[3,,3]
	JRST UFDUS1
UFDLNG:	SETOM SRCBUF+EXLUSD	;USE OLD VALUE
UFDRDY:	PUSHJ P,UFDSET		;SET ARGUMENTS
	SETOM SRCBUF+EXLMXA	;NEVER SET MXA
	MOVSI T1,RIPLOG
	IORM T1,SRCBUF+EXLSTS
	RENAME UFD,SRCBUF
	 JRST ILKCLN		;CLEARR INTERLOCK AND EXIT
UFDOK:	AOS FAILNM	;READY FOR ERROR 7
	TRNE FL,FLNLG
	JRST ILKCN1	;DIRECTORY NOW CREATED, GET OUT
	PUSHJ P,STUFCL	;UNLOCK
	 JFCL
STRDON:	MOVE T1,[-LOGSIZ,,LOGARG]
	LOGIN T1,
	 JFCL
	JRST CPOPJ1	;ALL DONE

	END START
  dQYú