;DETERMINE TYPE OF DISK PACK -- DEVICE ADDR IN A
DSET:	PUSHJ P,DSET0
	JRST DSETE
	POPJ P,
	POPJ P,

DSET0:	DPB A,[141000,,DSETP]	;INTO SEEK COMMAND
	DPB A,[141000,,DSETP2]	;AND INTO NOP
	MOVEM A,DSETD
	ANDI A,<1_LNPAKS>-1
	CAIL A,NPACKS
	JRST ILLDRV
	HRR G,A
	MOVSI C,DSETT0-DSETT1	;- # POSSIBILITIES
DSET1:	MOVE A,DSETT0(C)	;1ST NONEXISTENT CYL
	DPB A,[42000,,DSETPD]	;CC
	MOVEI A,DSETP	;CHAN PGM ADDR
	PUSHJ P,SARNX0	;TRY THE SEEK
	SKIPGE A,SENSED	;LOOK AT COMMAND REJECT BIT
	AOBJN C,DSET1	;IF COMMAND REJECT
	TLNE A,376000
	JRST CPOPJ1	;DON'T CLOBBER DATA ON OTHER ERRS
	SOJGE C,CPOPJ1	;ERR IF NONE GOOD - BACK UP TO WORKING ONE
	MOVE A,DSETT0(C)
	MOVEM A,DSETCY(G)	;TOTAL # CYLS
	MOVE A,DSETT1(C)
	MOVEM A,DSETUC(G)	;USER CYLS
	MOVE A,DSETT2(C)
	MOVEM A,DSETSU(G)	;# SURFS
	IMUL A,DSETUC(G)
	MOVEM A,NUTRAK(G)
	MOVE A,DSETSU(G)
	IMUL A,DSETCY(G)
	MOVEM A,NTRAKS(G)
	SUB A,NUTRAK(G)
	MOVEM A,NXTRAK(G)
	MOVE B,CRECL	;RECORD LENGTH IN WORDS
	ASH B,-1
	IMULI B,11	;9 BYTES PER 2 WORDS
	ADD B,DSETT3(C)	;# OVERHEAD BYTES PER RECORD
	MOVE A,DSETT4(C)	;TOTAL # BYTES PER TRACK
	IDIV A,B
	MOVEM A,DSETNR(G)	;# RECORDS PER TRACK
	ASH A,^D28
	MOVEM A,DSETR2(G)
	MOVE A,DSETNR(G)
	ADDI A,1
	ASH A,^D28
	MOVEM A,DIRW1V(G)
	MOVE A,CRECL
	IMUL A,DSETNR(G)
	MOVEM A,NWTRAK(G)	;# WORDS PER TRACK
	MOVEI A,BLKSKP
	IDIV A,DSETNR(G)
	JUMPN B,[AOJA A,.+1]
	MOVEM A,NSTRAK(G)	;# TRACKS TO AVOID AT BEGINNING
	HRRZM C,DSETTY(G)	;TYPE CODE
	AOS (P)
	JRST CPOPJ1

ILLDRV:	MOVEI C,[ASCIZ /
DRIVE #%A TOO BIG.
/]
	JRST STYOXT
DSETP:	BYTE (8)70,SEEK,IDEV
	IOW 6,DSETPD
DSETP2:	BYTE (8)240,3,IDEV	;CHAIN TO NOP TO WAIT FOR COMPLETION
	0
DSETPD:	0
	0

;TOTAL # CYLS -- 1ST INVALID CYL #
DSETT0:	^D815	;3330-11
	^D704	;3340-70
	^D560	;3350
	^D411	;3330-1
	^D356	;3340-35

;# CYLS FOR USER
DSETT1:	^D808
	^D696
	^D555
	^D404
	^D348

;# SURFS
DSETT2:	^D19
	^D12
	^D30
	^D19
	^D12

;# OVERHEAD BYTES PER RECORD
DSETT3:	^D135
	^D185
	^D185
	^D135
	^D185

;# BYTES PER TRACK
DSETT4:	^D13165
	^D8553
	^D19254
	^D13165
	^D8553

DSETE:	MOVEI C,[ASCIZ /
SEEK TO CYL 348. REJECTED.
/]
	PUSHJ P,STYO
	JRST CI0	;ABORT

DSETD:	0
DSETCY:	BLOCK NPACKS	;TOTAL # CYLINDERS
DSETUC:	BLOCK NPACKS	;# CYLINDERS FOR USER
DSETSU:	BLOCK NPACKS	;# SURFACES
DSETNR:	BLOCK NPACKS	;# RECORDS PER TRACK
DSETR2:	BLOCK NPACKS	;# RECORDS PER TRACK * 2000,,0
DSETTY:	BLOCK NPACKS	;DISK DRIVE TYPE # (E.G. INDEX ON DSETT0)
NWTRAK:	BLOCK NPACKS	;# WDS PER TRACK
NTRAKS:	BLOCK NPACKS	;TOTAL # CYLS * SURFS
NUTRAK:	BLOCK NPACKS	;# UCYLS * SURFS
NXTRAK:	BLOCK NPACKS	;NTRAKS - NUTRAK
NSTRAK:	BLOCK NPACKS	;# TRACKS PROTECTED FROM DSKT, BAFGO
DIRW1V:	BLOCK NPACKS
;FORMAT A PACK. ASSUMES REC 0 EXISTS, WRITES 1-5

FMT:	MCONO SA0,400000
	PUSHJ P,CSET	;SET UP FOR PROPER SA-10 CHANNL
	SKIPL A,PREARG
	MOVEM A,FMTD
	LDB G,[POINT LNPAKS,FMTD,35]
	MOVEI C,[ASCIZ /
UWP NOT SET FOR DRIVE %G.
/]
	SKIPN UWP(G)
	JRST STYOXT
	MOVEI C,[ASCIZ /
FORMATTER READY, C TO CONTINUE:
/]
	PUSHJ P,ABORTQ
	SETZM FMZBF
	MOVE A,[FMZBF,,FMZBF+1]
	BLT A,FMZBF+BLKSIZ*11/10-1
	MOVE A,FMTD	;DEVICE ADDRESS
	PUSHJ P,DSET	;DETERMINE DISK DRIVE SIZE
	MOVE A,DSETNR(G)
	ADDI A,1
	IMULI A,^D40
	MOVEM A,FMT1T
	MOVE D,FMTD		; DEVICE TO FORMAT
	MOVE P,[-LPDL,,PDL-1]
	XCT IORST
	XCT PIRST
FMT0:	LSH D,^D12
	MOVE I,FMTM	;0=HA, R0 ARE GOOD; 1=WRT R0; -1=WRT HA AND R0
	;PUT IN DEV FLD
	MOVE J,CRECL	;RECORD LENGTH
	IMULI J,^D9
	ADDI J,1
	ASH J,-1	;J HAS SIZE IN BYTES
	LSH J,4
	MOVEI E,0
FMT1:	TRZ E,7760		; CLEAR HEAD FIELD
	MOVE H,ADRLO	;PLACE TO PUT C C H H # KL DL DL
	MOVSI C,200000	;A TIC CMD
	ADD C,H
	ADD C,FMT1T	;LEAVE ROOM FOR DATA
	MOVEM C,FWP0
	JUMPE I,FMT1A	;IF NO SET FM NEEDED
	MOVE K,[BYTE (8)70,37]	;SET FM
	PUSHJ P,FMTS0
	MOVE K,[-1,,[BYTE (8)300]]	;ALLOW ALL
	PUSHJ P,FMTS1
FMT1A:	TRNE E,7760	;ALWAYS SEEK IF HEAD 0
	JUMPGE I,FMT1B	;OR IF WRITING HA'S
	MOVE K,[BYTE (8)70,7]	;SEEK
	PUSHJ P,FMTS0
	MOVE K,[400000-2,,[0]]	;2 BYTES OF BB
	PUSHJ P,FMTS1
	MOVSI K,-4
	PUSHJ P,FMTS1A
FMT1B:	JUMPL I,FMT1C
	JUMPG I,FMT1D
	MOVE K,[BYTE (8)70,61]	;SRCH ID EQ
	TRNE E,7760
	TLO K,1000	;SET M-T IF NOT 1ST REC
	PUSHJ P,FMTS0
	MOVSI K,-5
FMT1BR:	PUSHJ P,FMTS1A
	PUSHJ P,FMTS2
;FALLS THRU
;FALLS IN
FMT1E:	TLZ J,776000	;SET RN TO 0
FMT2:	MOVEM E,(H)
	MOVEM J,1(H)
	MOVEI A,200
	TLNN J,776000
	HRRM A,1(H)	;R0 HAS DL OF 8
	ADDI H,2
	CAML J,DSETR2(G)
	JRST FMT2A	;NO MORE RECS ON TRK
	ADD J,[2000,,0]
	MOVE K,[BYTE (8)70,35]	;WRT CKD
	PUSHJ P,FMTS0
	MOVSI K,400000-^D8
	PUSHJ P,FMTS1A
	MOVN K,J
	LSH K,^D18-4
	HRRI K,FMZBF
	PUSHJ P,FMTS1A
	JRST FMT2
FMT2A:	ADDI E,20
	LDB A,[41000,,E]
	CAMGE A,DSETSU(G)
	JRST FMT1A	;MORE TRKS ON CYL
	MOVE K,[BYTE (8)240,3]	;NOP
	PUSHJ P,FMTS0
	SETZM (C)
	MOVE A,FWP0
	PUSHJ P,SCHNX	;FORMATS ONE CYL
	JRST CI0	;IF ERROR
	ADD E,[4,,0]		; COUNT A CYLINDER
	LDB A,[242000,,E]	; WHAT CYLINDER ARE WE UP TO?
	CAMGE A,DSETCY(G)
	JRST FMT1		; NOT DONE YET.
	MOVEI C,[ASCIZ /
FORMATTER DONE.
/]
STYOXT:	PUSHJ P,STYO
	JRST CI0

FMT1D:	MOVE K,[BYTE (8)70,71]	;SRCH HA EQ
	TRNE E,7760
	TLO K,1000	;SET M-T IF NOT 1ST TRK
	PUSHJ P,FMTS0
	MOVSI K,-4
	PUSHJ P,FMTS1A
	PUSHJ P,FMTS2
	JRST FMT1CR

ABORTQ:	PUSHJ P,STYO	;TYPE MESSAGE
	PUSHJ P,TYI	;ACCEPT CHAR
	CAIN B,"C"
	POPJ P,	;CHAR WAS C, CONTINUE
FMTAX:	MOVEI C,[ASCIZ /
ABORTED.
/]
	PUSHJ P,STYO
	JRST CI0
FMT1C:	MOVE K,[BYTE (8)70,31]	;WRT HA
	PUSHJ P,FMTS0
	MOVE K,[400000-1,,[0]]
	PUSHJ P,FMTS1	;PUT OUT A ZERO FLAG BYTE
	MOVSI K,-4
	PUSHJ P,FMTS1A
FMT1CR:	MOVE K,[BYTE (8)70,25]	;WRITE R0
	PUSHJ P,FMTS0
	MOVSI K,400000-^D8
	PUSHJ P,FMTS1A
	MOVE K,[-^D8,,FMZBF]
	PUSHJ P,FMTS1A
	JRST FMT1E

FMTS0:	ADD K,D
FMTS3:	MOVEM K,(C)
	ADDI C,1
	POPJ P,

FMTS2:	MOVE K,C
	SUBI K,2
	JRST FMTS3

FMTS1A:	HRR K,H
FMTS1:	HLLZ A,K
		LSH A,CNTSFT
	SKIPL K
	TLZ A,400000
	HLL K,A
	JRST FMTS3

CRECL:	BLKSIZ

MXRECT=40			; MAX RECORDS PER TRACK
DEVFLD=141000
; FOLLOWING 4 ARE TABLES, OF LENGTH 1 NOW, DEFINING A FORMAT OF
; A PACK. INDEXED BY F IN FMT ABOVE

FWP0:	0	;PTR TO CHNL PRGM
FMTM:	0	;FORMATTING MODE
FMTD:	IDEV	;DEV ADDR

FMT1T:	0
